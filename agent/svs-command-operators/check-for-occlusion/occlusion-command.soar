sp {check-for-occlusion*propose*occlusion-command
   (state <s> ^name check-for-occlusion
              ^object-info.belief-obj.id <id>
              ^candidate-set <cand>
             -^redo-command)
-->
   (<s> ^operator <o> +)
   (<o> ^name occlusion-command
        ^object-id <id>
        ^candidate-set <cand>)
}

sp {check-for-occlusion*issue*reset
   (state <s> ^name check-for-occlusion
              ^redo-command true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name issue-occlusion-reset)
}

sp {check-for-occlusion*apply*issue-occlusion-reset*Remove*redo
   (state <s> ^name check-for-occlusion
              ^operator.name issue-occlusion-reset
              ^redo-command true)
-->
   (<s> ^redo-command true -)
}

sp {check-for-occlusion*apply*issue-occlusion-reset*remove*filter
   (state <s> ^name check-for-occlusion
              ^operator.name issue-occlusion-reset
              ^occlusion-filter <filter>
              ^top-state.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
-->
   (<cmd> ^extract_once <filter> -)
}

sp {check-for-occlusion*apply*issue-occlusion-reset*remove*occlusion-filter
   (state <s> ^name check-for-occlusion
              ^operator.name issue-occlusion-reset
              ^occlusion-filter <filter>)
-->
   (<s> ^occlusion-filter <filter> -)
}


# occlusin-amount <num>
#   value between 0 and 1 indicating how much the object is estimated to be occluded
sp {check-for-occlusion*apply*occlusion-command*copy*occlusion-amount
   (state <s> ^name check-for-occlusion
              ^operator.name occlusion-command
              ^occlusion-command.result <val>)
-->
   (<s> ^occlusion-amount <val>)
}

# object-occluded <bool>
#   Based on the occlusion-threshold, whether the object is occluded
sp {check-for-occlusion*apply*occlusion-command*copy*object-occluded*true
   (state <s> ^name check-for-occlusion
              ^operator.name occlusion-command
              ^occlusion-threshold <threshold>
              ^occlusion-amount >= <threshold>) 
-->
   (<s> ^object-occluded true)
}

sp {check-for-occlusion*apply*occlusion-command*copy*object-occluded*false
   (state <s> ^name check-for-occlusion
              ^operator.name occlusion-command
              ^occlusion-threshold <threshold>
              ^occlusion-amount < <threshold>)
-->
   (<s> ^object-occluded false)
}

sp {check-for-occlusion*apply*occlusion-command*report-to-superstate
   (state <s> ^name check-for-occlusion
              ^operator.name occlusion-command
              ^object-occluded <bool>
              ^occlusion-amount <val>
              ^superstate <ss>)
-->
   (<ss> ^object-occluded <bool>
         ^occlusion-amount <val>)
}

# Debugging only, report occlusion amount
sp {check-for-occlusion*apply*occlusion-command*print-perception*report
   (state <s> ^name check-for-occlusion
              ^operator.name occlusion-command
              ^occlusion-amount <val>
              ^object-info.belief-obj.id <id>
              ^agent-params.print-perception true)
-->
   (write |Occlusion Result for | <id> | is | <val>  (crlf))
}
