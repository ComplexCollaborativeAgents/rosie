#### operator create-semantic-structure
# initializes a new smem structure for the action the first time we see it

# propose create-semantic-structure if no ^procedural link exists on the action-id
sp {generate-action-operator*propose*create-semantic-structure
   (state <s> ^name generate-action-operator
              ^action-id <action-id>)
   (<action-id> -^procedural)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-semantic-structure)
}

# create the basic semantic structure for the action, 
#   and a new-action flag
sp {generate-action-operator*apply*create-semantic-structure
   (state <s> ^name generate-action-operator
              ^operator.name create-semantic-structure
              ^action-id <action-id>)
   (<action-id> ^handle <handle>)
-->
   (<action-id> ^item-type action ^procedural <proc>)
   (<proc> ^op_name (make-constant-symbol (concat |op_| <handle>)))
   (<s> ^to-store <action-id> <proc>)
   (<s> ^new-action true)
}

        
