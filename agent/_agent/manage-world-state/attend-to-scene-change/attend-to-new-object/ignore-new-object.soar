# If the object-info for the given handle already exists, 
#   Then ignore the new object
sp {attend-to-new-object*propose*ignore-new-object*object-info*already*exists
   (state <s> ^name attend-to-new-object
              ^object-handle <obj-handle>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^object-handle <obj-handle>)
-->
   (<s> ^operator <o> +)
   (<o> ^name ignore-new-object =)
}

# Remove the new-object change from the change list
sp {attend-to-new-object*apply*ignore-new-object
   (state <s> ^name attend-to-new-object
              ^operator.name ignore-new-object
              ^top-state.perception-monitor.changes <chs>
              ^superstate.change-info <info>)
   (<chs> ^change <c>)
   (<c> ^info <info>)
-->
   (<chs> ^change <c> -)
}

sp {attend-to-new-object*apply*ignore-new-object*imagined-obj*link*to*wm-obj
  (state <s> ^name attend-to-new-object
             ^operator.name ignore-new-object
             ^source-type imagined-obj
             ^source-obj <imag>
             ^world-match-result.wm-obj <obj>)
-->
  (<imag> ^linked-to <obj>)
}

# If debug-perception true, report that the new-object was ignored (possibly an error)
sp {attend-to-new-object*apply*ignore-new-object*debug-perception*print
   (state <s> ^name attend-to-new-object
              ^operator.name ignore-new-object
              ^object-handle <obj-handle>)
-->
   (write |Ignoring new-object | <obj-handle> |: that handle already exists as an object-info| (crlf))
}


