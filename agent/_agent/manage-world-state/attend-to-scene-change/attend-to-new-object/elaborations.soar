sp {attend-to-new-object*elaborate*source*info*from*superoperator
   (state <s> ^name attend-to-new-object
              ^superstate.operator.{ <type> << perception-obj smem-obj epmem-obj imagined-obj >> } <obj>)
-->
   (<s> ^source-type <type>
        ^source-obj <obj>)
}

#####################################################################
# object-handle
#   Source preference: wm-obj > smem-obj > epmem-obj > perception-obj

sp {attend-to-new-object*elaborate*object-handle*from*wm-obj
   (state <s> ^name attend-to-new-object
              ^wm-obj.handle <obj-handle>)
-->
   (<s> ^object-handle <obj-handle>)
}

sp {attend-to-new-object*elaborate*object-handle*from*smem-obj
   (state <s> ^name attend-to-new-object
              ^smem-obj.handle <obj-handle>
             -^wm-obj)
-->
   (<s> ^object-handle <obj-handle>)
}

sp {attend-to-new-object*elaborate*object-handle*from*epmem-obj
   (state <s> ^name attend-to-new-object
              ^epmem-obj.handle <obj-handle>
             -^wm-obj
             -^smem-obj)
-->
   (<s> ^object-handle <obj-handle>)
}

sp {attend-to-new-object*elaborate*object-handle*from*perception-obj
   (state <s> ^name attend-to-new-object
              ^perception-obj.id <obj-handle>
             -^wm-obj
             -^smem-obj
             -^epmem-obj)
-->
   (<s> ^object-handle <obj-handle>)
}

####################################################################
# wm-obj

# use imagined-obj, otherwise it will have to be manually created
sp {attend-to-new-object*elaborate*wm-obj*from*imagined-obj
   (state <s> ^name attend-to-new-object
              ^source-type imagined-obj
              ^source-obj <obj>
              -^world-match-result.wm-obj)
-->
   (<s> ^wm-obj <obj>)
}

# if there's already an object in the world, use that
sp {attend-to-new-object*elaborate*wm-obj*using*object-handle
   (state <s> ^name attend-to-new-object
              ^object-handle <handle>
              ^top-state.world.objects.object <obj>)
   (<obj> ^handle <handle>)
-->
   (<s> ^wm-obj <obj>)
}

# if matches-world-object, use that
sp {attend-to-new-object*elaborate*wm-obj*using*world-match-result
   (state <s> ^name attend-to-new-object
              ^world-match-result.wm-obj <obj>
              ^top-state.world.objects.object <obj>)
-->
   (<s> ^wm-obj <obj>)
}


#####################################################################
# belief-id

sp {attend-to-new-object*elaborate*belief-id*using*handle
   (state <s> ^name attend-to-new-object
              ^object-handle <obj-handle>)
-->
   (<s> ^belief-id (concat |bel-| <obj-handle>))
}

#####################################################################
# perception-id

sp {attend-to-new-object*elaborate*perception-id*from*perception-obj
   (state <s> ^name attend-to-new-object
              ^source-type perception-obj
              ^source-obj.id <perc-id>)
-->
   (<s> ^perception-id <perc-id>)
}

sp {attend-to-new-object*elaborate*perception-id*using*handle
   (state <s> ^name attend-to-new-object
              ^object-handle <obj-handle>
              ^source-type <> perception-obj)
-->
   (<s> ^perception-id <obj-handle>)
}
              
#####################################################################
# perception-obj

sp {attend-to-new-object*elaborate*perception-obj*from*source-obj
   (state <s> ^name attend-to-new-object
              ^source-type perception-obj
              ^source-obj <perc-obj>)
-->
   (<s> ^perception-obj <perc-obj>)
}

sp {attend-to-new-object*elaborate*perception-obj*matching*perception-id
   (state <s> ^name attend-to-new-object
              ^source-type <> perception-obj
              ^perception-id <perc-id>
              ^top-state.svs.spatial-scene.child <perc-obj>)
   (<perc-obj> ^id <perc-id>)
-->
   (<s> ^perception-obj <perc-obj>)
}

#####################################################################
# input-link-obj

sp {attend-to-new-object*elaborate*input-link-obj*matching*perception-obj
   (state <s> ^name attend-to-new-object
              ^perception-id <perc-id>
              ^top-state.io.input-link.objects.object <obj>)
   (<obj> ^handle <perc-id>)
-->
   (<s> ^input-link-obj <obj>)
}

#####################################################################
# epmem-obj

sp {attend-to-new-object*elaborate*epmem-obj*from*source-obj
   (state <s> ^name attend-to-new-object
              ^source-type epmem-obj
              ^source-obj <obj>)
-->
   (<s> ^epmem-obj <obj>)
}

#####################################################################
# smem-obj

sp {attend-to-new-object*elaborate*smem-obj*from*source-obj
   (state <s> ^name attend-to-new-object
              ^source-type smem-obj
              ^source-obj <obj>)
-->
   (<s> ^smem-obj <obj>)
}

#####################################################################
# visible << true false >>

sp {attend-to-new-object*elaborate*visible*true*has*perception-obj
   (state <s> ^name attend-to-new-object
              ^perception-obj <obj>)
-->
   (<s> ^visible true)
}

sp {attend-to-new-object*elaborate*visible*false*no*perception-obj
   (state <s> ^name attend-to-new-object
             -^perception-obj <obj>)
-->
   (<s> ^visible false)
}

#####################################################################
# predicates

# Order: perception > smem > epmem

sp {attend-to-new-object*elaborate*predicates
   (state <s> ^name attend-to-new-object)
-->
   (<s> ^predicates <preds>)
}

sp {attend-to-new-object*elaborate*predicate*from*perception
   (state <s> ^name attend-to-new-object
              ^input-link-obj.classifications.<prop> <pred>
              ^predicates <preds>)
-->
   (<preds> ^<prop> <pred>)
   (<s> ^perception-predicate <prop>)
}

sp {attend-to-new-object*elaborate*predicate*from*smem*level*2
   (state <s> ^name attend-to-new-object
              ^smem-obj.<prop> <pred>
              ^top-state.agent-params.object-rep-info <obj-rep>
              ^predicates <preds>
             -^perception-predicate <prop>)
   # copy everything not otherwise listed
   (<obj-rep> -^ignore-level1 <prop>
              -^ignore-level2 <prop>
              -^copy-level1 <prop>)
-->
   (<preds> ^<prop> <pred>)
   (<s> ^smem-predicate <prop>)
}

sp {attend-to-new-object*elaborate*predicate*from*epmem
   (state <s> ^name attend-to-new-object
              ^epmem-obj.predicates.<prop> <pred>
              ^top-state.agent-params.object-rep-info <obj-rep>
              ^predicates <preds>
             -^perception-predicate <prop>
             -^smem-predicate <prop>)
   (<obj-rep> -^ignore-level2 <prop>)
-->
   (<preds> ^<prop> <pred>)
   (<s> ^epmem-predicate <prop>)
}
