# Report failure if there are no more candidates for the missing object
sp {op_put-down1*tabletop*propose*report-failure
   (state <s> ^name op_put-down1
              ^current-action.put-down-command.status complete
             -^top-state.perception-monitor.object-monitor.new-object-info.status
              ^held-object.predicates.visible false)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-failure)
}

# Report a failure, may lead to more interactions
sp {op_put-down1*tabletop*apply*report-failure*report*failure
   (state <s> ^name op_put-down1
              ^operator.name report-failure
              ^held-object <obj>
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure <f>)
   (<f> ^type missing-object
        ^missing-object <obj>)
}

sp {op_put-down1*tabletop*apply*report-failure*remove*grabbed
   (state <s> ^name op_put-down1
              ^operator.name report-failure
              ^held-object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -)
}

sp {op_put-down1*tabletop*apply*report-failure*remove*put-down-command
   (state <s> ^name op_put-down1
              ^operator.name report-failure
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^put-down-command <cmd>)
   (<out> ^put-down <cmd>)
-->
   (<a> ^put-down-command <cmd> -)
   (<out> ^put-down <cmd> -)
}
