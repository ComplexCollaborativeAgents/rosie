# elaborate problem-space attend-to-all-perception
sp {op_turn1*elaborate*problem-space*attend-to-all-perception*yes
   (state <s> ^name op_turn1
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

### direction-handle
# The direction the agent should turn (defaults to right)
                                      
sp {op_turn1*elaborate*direction-handle*from*action-operator
   (state <s> ^name op_turn1
              ^action-operator.arg1.handle <dir-handle>)
-->
   (<s> ^direction-handle <dir-handle>)
}

# default is right1
sp {op_turn1*elaborate*direction-handle*default
   (state <s> ^name op_turn1
             -^action-operator.arg1.handle)
-->
   (<s> ^direction-handle right1)
}

### direction-id
# The smem info about the direction to turn
#   (lookup using direction-handle)

sp {op_turn1*elaborate*smem-query-cue*direction
   (state <s> ^name op_turn1
              ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
   (<cue> ^handle <dir-handle>)
}

sp {op_turn1*elaborate*direction-id*from*retrieval
   (state <s> ^name op_turn1
              ^direction-handle <dir-handle>
              ^smem-query.result <dir-id>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}
