#### Different failure cases that can arise during pick-up

# svs-filter-error: One of the position filters had an error
sp {op_pick-up1*magicbot*propose*report-failure*svs-filter-error
   (state <s> ^name op_pick-up1
              ^problem-space.action.type execute
              ^current-action.execution-operator.action-handle <handle>
              ^object-position-filter <pos-filter>)
   (<pos-filter> ^result <res>)
   (<res> -^record.value)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type svs-filter-error
        ^failure-info object-position-filter)
}

# missing-object: no input-link rep for the object we are trying to pick up
sp {op_pick-up1*magicbot*propose*report-failure*missing-object
   (state <s> ^name op_pick-up1
              ^problem-space.action.type execute
             -^current-action.pick-up-command
              -^object-info.input-link-obj
               ^target-object.handle <handle>
               ^world.robot.arm.state external)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type missing-object
        ^failure-info <handle>)
}

# command-error: The pick-up command got an error status
sp {op_pick-up1*magicbot*propose*report-failure*command-error
   (state <s> ^name op_pick-up1
              ^problem-space.action.type execute
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << drive-command pick-up-command >> }.status error)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type command-error
        ^failure-info <handle>)
}

# execution-failed: The pick-up command got a failure status
sp {op_pick-up1*magicbot*propose*report-failure*execution-failed
   (state <s> ^name op_pick-up1
              ^problem-space.action.type execute
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << drive-command pick-up-command >> }.status failure)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type execution-failed
        ^failure-info <handle>)
}

