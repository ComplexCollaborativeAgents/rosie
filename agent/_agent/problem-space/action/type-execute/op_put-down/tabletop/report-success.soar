sp {op_put-down1*tabletop*propose*report-success
   (state <s> ^name op_put-down1
              ^problem-space.action.type execute
              ^current-action.put-down-command.status complete
              ^held-object.predicates.visible true)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-success)
}

sp {op_put-down1*tabletop*apply*report-success
   (state <s> ^name op_put-down1
              ^operator.name report-success
              ^current-action <a>)
-->
   (<a> ^status complete)
}

sp {op_put-down1*tabletop*apply*report-success*remove*grabbed
   (state <s> ^name op_put-down1
              ^operator.name report-success
              ^held-object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -
            ^arm-status not-grabbed)
}

sp {op_put-down1*tabletop*apply*report-success*remove*put-down-command
   (state <s> ^name op_put-down1
              ^operator.name report-success
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^put-down-command <cmd>)
   (<out> ^put-down <cmd>)
-->
   (<a> ^put-down-command <cmd> -)
   (<out> ^put-down <cmd> -)
}
