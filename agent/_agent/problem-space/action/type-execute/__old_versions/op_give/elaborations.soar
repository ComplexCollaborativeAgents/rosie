sp {op_give1*elaborate*held-object*superoperator
   (state <s> ^name op_give1
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^held-object <obj>)
}

sp {op_give1*elaborate*object-info
   (state <s> ^name op_give1
              ^held-object <held>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^wm-obj <held>)
-->
   (<s> ^object-info <obj-info>)
}

sp {op_give1*elaborate*person*arg2
   (state <s> ^name op_give1
              ^action-operator.arg2.{2} <person>)
-->
   (<s> ^person <person>)
}

sp {op_give1*elaborate*calculation
   (state <s> ^name op_give1
              ^robot-position <rp>
              ^person-position <op>)
   (<rp> ^x <rx> ^y <ry>)
   (<op> ^x <px> ^y <py>)
-->
   (<s> ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^person-x <px> ^person-y <py>)
}

sp {op_give1*elaborate*calculations*distance
   (state <s> ^name op_give1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^person-x <px> ^person-y <py>)
-->
   (<c> ^distance (sqrt (+ (* (- <px> <rx>) (- <px> <rx>) )
                           (* (- <py> <ry>) (- <py> <ry>) ) ) ) )
}

sp {op_give1*elaborate*calculations*theta
   (state <s> ^name op_give1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^person-x <px> ^person-y <py>)
-->
   (<c> ^theta (atan2 (- <py> <ry>) (- <px> <rx>) ) )
}

sp {op_give1*elaborate*calcuations*drive-x*drive-y
   (state <s> ^name op_give1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^person-x <px> ^person-y <py>
        ^theta <theta>)
-->
   (<c> ^drive-x (- <px> (* 1.5 (cos <theta>) ) ) )
   (<c> ^drive-y (- <py> (* 1.5 (sin <theta>) ) ) )
}
   
