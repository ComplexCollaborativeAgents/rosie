####################### predicate-handle ###########################

# procedural match - the predicate-handle matches one on the reference-action
sp {generalize-action*propose*generalize-argument*predicate-handle*procedural
  (state <s> ^name generalize-action
             ^generalize-argument <arg>
             ^reference-id.<arg-name> <parg>
             ^reference-action.<arg-name>.handle <handle>)
  (<arg> ^instance <i>
        -^generalized-rep)
  (<i> ^arg-type predicate-handle
       ^handle <handle>)
  (<parg> ^arg-type predicate
          ^id <pred-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type procedural
       ^id <pred-slot>)
}

sp {generalize-action*apply*generalize-argument*predicate-handle*procedural
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type procedural
       ^id <pred-slot>)
-->
  (<arg> ^generalized-rep <gen>)
  (<gen> ^arg-type predicate-handle
         ^id <pred-slot>)
}

# default match - copy the predicates onto a default rep
sp {generalize-action*propose*generalize-argument*predicate-handle*default
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^instance <i>
        -^generalized-rep)
  (<i> ^arg-type predicate-handle
       ^handle <handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^predicate-handle <handle>)
}

sp {generalize-action*apply*generalize-argument*predicate-handle*default
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^predicate-handle <handle>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^arg-type predicate-handle
         ^id <pred-slot>)
  (<pred-slot> ^default <def>)
  (<def> ^predicate-handle <handle>)
}
