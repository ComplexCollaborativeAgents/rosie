####################### concept ###########################

# procedural match - the concept matches one on the reference-action
sp {generalize-action*propose*generalize-argument*concept*procedural
  (state <s> ^name generalize-action
             ^generalize-argument <arg>
             ^reference-id.<arg-name> <parg>
             ^reference-action.<arg-name>.handle <handle>)
  (<arg> ^instance <i>
        -^generalized-rep)
  (<i> ^arg-type concept
       ^handle <handle>)
  (<parg> ^arg-type concept
          ^id <conc-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^id <conc-slot>)
}

sp {generalize-action*apply*generalize-argument*concept*procedural
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^id <conc-slot>)
-->
  (<arg> ^generalized-rep <gen>)
  (<gen> ^arg-type concept
         ^id <conc-slot>)
}

# default match - copy the predicates onto a default rep
sp {generalize-action*propose*generalize-argument*concept*default
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^instance <i>
        -^generalized-rep)
  (<i> ^arg-type concept
       ^handle <handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^predicate-handle <handle>)
}

sp {generalize-action*apply*generalize-argument*concept*default
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^predicate-handle <handle>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^arg-type concept
         ^id <conc-slot>)
  (<conc-slot> ^default <def>)
  (<def> ^predicate-handle <handle>)
}
