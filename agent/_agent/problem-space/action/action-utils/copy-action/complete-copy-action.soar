sp {learn-copy-action-elaboration-rule*propose*complete-copy-action
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^copy-name <name>
         -^destination.<name> <a>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
}

sp {learn-copy-action-elaboration-rule*apply*complete-copy-action*create*copied-action
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^source-action <src>)
   (<src> ^name <name>
          ^action-handle <h>)
-->
   (<ca> ^copied-action <copy>)
   (<copy> ^name <name>
           ^action-handle <h>)
}

sp {learn-copy-action-elaboration-rule*apply*complete-copy-action*copy*subaction-handle
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^source-action.subaction-handle <h>
         ^copied-action <copy>)
-->
   (<copy> ^subaction-handle <h>)
}

sp {learn-copy-action-elaboration-rule*apply*complete-copy-action*copy*perform
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^source-action.perform <perf>
         ^copied-action <copy>)
-->
   (<copy> ^perform <perf>)
}


# Also, arg-status


# Copy the arguments onto the copied-action and copy the copied-action to the superstate
# (Will create the chunked application rule)
# ENUMERATE: 0-5 arguments

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*0*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^num-args 0
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
-->
   (<dest> ^<name> <copy>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*1*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^num-args 1
         ^copy-argument   <arg1>
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*2*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^num-args 2
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
		   ^<arg2-name> <arg-copy2>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*3*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^num-args 3
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copy-argument { <arg3> > <arg2>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
   (<arg3> ^arg-name <arg3-name> ^arg-copy <arg-copy3>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
		   ^<arg2-name> <arg-copy2>
		   ^<arg3-name> <arg-copy3>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*4*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^num-args 4
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copy-argument { <arg3> > <arg2>}
         ^copy-argument { <arg4> > <arg3>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
   (<arg3> ^arg-name <arg3-name> ^arg-copy <arg-copy3>)
   (<arg4> ^arg-name <arg4-name> ^arg-copy <arg-copy4>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
	  	   ^<arg2-name> <arg-copy2>
	   	   ^<arg3-name> <arg-copy3>
		   ^<arg4-name> <arg-copy4>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*5*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^operator <o>)
   (<o> ^name complete-copy-action
        ^copy-action <ca>)
   (<ca> ^num-args 5
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copy-argument { <arg3> > <arg2>}
         ^copy-argument { <arg4> > <arg3>}
         ^copy-argument { <arg5> > <arg4>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
   (<arg3> ^arg-name <arg3-name> ^arg-copy <arg-copy3>)
   (<arg4> ^arg-name <arg4-name> ^arg-copy <arg-copy4>)
   (<arg5> ^arg-name <arg5-name> ^arg-copy <arg-copy5>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
			  ^<arg2-name> <arg-copy2>
			  ^<arg3-name> <arg-copy3>
			  ^<arg4-name> <arg-copy4>
			  ^<arg5-name> <arg-copy5>)
}
