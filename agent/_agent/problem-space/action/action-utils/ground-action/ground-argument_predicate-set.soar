###############################################################
#####                predicate-set                        #####

sp {ground-action*ground-argument*expand*predicate-set*1
  (state <s> ^name ground-action
             ^ground-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^generalized-rep <gen>)
  (<gen> ^predicate { <p1>         }
        -^predicate { <p2> <> <p1> })
-->
  (<s> ^ground-argument <gp1>)
  (<gp1> ^arg-type predicate
         ^generalized-rep <p1>)
  (<arg> ^ground-pred <gp1>)
}

sp {ground-action*ground-argument*expand*predicate-set*2
  (state <s> ^name ground-action
             ^ground-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^generalized-rep <gen>)
  (<gen> ^predicate { <p1>         }
         ^predicate { <p2> > <p1>  }
        -^predicate { <p3> <> <p2> <> <p1> })
-->
  (<s> ^ground-argument <gp1> <gp2>)
  (<gp1> ^arg-type predicate
         ^generalized-rep <p1>)
  (<gp2> ^arg-type predicate
         ^generalized-rep <p2>)
  (<arg> ^ground-pred <gp1> <gp2>)
}

sp {ground-action*ground-argument*expand*predicate-set*3
  (state <s> ^name ground-action
             ^ground-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^generalized-rep <gen>)
  (<gen> ^predicate { <p1>         }
         ^predicate { <p2> > <p1>  }
         ^predicate { <p3> > <p2>  }
        -^predicate { <p4> > <p3> <> <p2> <> <p1> })
-->
  (<s> ^ground-argument <gp1> <gp2> <gp3>)
  (<gp1> ^arg-type predicate
         ^generalized-rep <p1>)
  (<gp2> ^arg-type predicate
         ^generalized-rep <p2>)
  (<gp3> ^arg-type predicate
         ^generalized-rep <p3>)
  (<arg> ^ground-pred <gp1> <gp2> <gp3>)
}

sp {ground-action*propose*ground-argument*predicate-set
  (state <s> ^name ground-action
             ^ground-argument <arg>)
  (<arg> ^arg-type predicate-set
        -^instance.predicate)
-->
  (<s> ^operator <o> + =)
  (<o> ^name ground-argument
       ^argument <arg>
       ^arg-type predicate-set)
}

sp {ground-action*reject*ground-argument*predicate-set*ungrounded*pred
  (state <s> ^name ground-action
             ^operator <o> +)
  (<o> ^name ground-argument
       ^argument <arg>
       ^arg-type predicate-set)
  (<arg> ^ground-pred <gp>)
  (<gp> -^instance)
-->
  (<s> ^operator <o> -)
}

sp {ground-action*apply*ground-argument*predicate-set*create*instance
  (state <s> ^name ground-action
             ^operator <o>)
  (<o> ^name ground-argument
       ^argument <arg>
       ^arg-type predicate-set)
-->
  (<arg> ^instance <i>)
}

sp {ground-action*apply*ground-argument*predicate-set*copy*predicate
  (state <s> ^name ground-action
             ^operator <o>)
  (<o> ^name ground-argument
       ^argument <arg>
       ^arg-type predicate-set)
  (<arg> ^instance <i>
         ^ground-pred.instance <gp>)
-->
  (<i> ^predicate <gp>)
}
