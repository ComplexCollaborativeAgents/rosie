sp {ground-action*propose*complete-ground-action
   (state <s> ^name ground-action)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-ground-action)
}

sp {ground-action*apply*complete-ground-action*create*grounded-action
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^source-id <src-id>
              ^perform <perf>)
   (<src-id> ^handle <handle>
             ^op_name <name>
             ^action-handle <ah>)
-->
   (<s> ^grounded-action <a>)
   (<a> ^name <name>
        ^action-handle <ah>
        ^perform <perf>)
}

sp {ground-action*apply*complete-ground-action*copy*arg-status
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^ground-argument <ga>)
   (<ga> ^generalized-rep.arg-status <arg-status>
         ^instance <i>)
-->
   (<i> ^arg-status <arg-status>)
}


# Copy the arguments onto the grounded-action and copy the grounded-action to the superstate
# (Will create the chunked application rule)
# ENUMERATE: 0-5 arguments

sp {ground-action*apply*complete-ground-action*copy*grounded-action*superstate*0*arguments
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^num-args 0
              ^grounded-action <gnd>
              ^ss-ground-info <info>)
-->
   (<info> ^grounded-action <gnd>)
}

sp {ground-action*apply*complete-ground-action*copy*grounded-action*superstate*1*arguments
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^num-args 1
              ^grounded-action <gnd>
              ^ground-argument   <arg1>
              ^ss-ground-info <info>)
   (<arg1> ^arg-name <arg1-name> ^instance <argi1>)
-->
   (<info> ^grounded-action <gnd>)
   (<gnd> ^<arg1-name> <argi1>)
}

sp {ground-action*apply*complete-ground-action*copy*grounded-action*superstate*2*arguments
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^num-args 2
              ^grounded-action <gnd>
              ^ground-argument   <arg1>
              ^ground-argument { <arg2> > <arg1>}
              ^ss-ground-info <info>)
   (<arg1> ^arg-name <arg1-name> ^instance <argi1>)
   (<arg2> ^arg-name <arg2-name> ^instance <argi2>)
-->
   (<info> ^grounded-action <gnd>)
   (<gnd> ^<arg1-name> <argi1>
          ^<arg2-name> <argi2>)
}

sp {ground-action*apply*complete-ground-action*copy*grounded-action*superstate*3*arguments
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^num-args 3
              ^grounded-action <gnd>
              ^ground-argument   <arg1>
              ^ground-argument { <arg2> > <arg1>}
              ^ground-argument { <arg3> > <arg2>}
              ^ss-ground-info <info>)
   (<arg1> ^arg-name <arg1-name> ^instance <argi1>)
   (<arg2> ^arg-name <arg2-name> ^instance <argi2>)
   (<arg3> ^arg-name <arg3-name> ^instance <argi3>)
-->
   (<info> ^grounded-action <gnd>)
   (<gnd> ^<arg1-name> <argi1>
          ^<arg2-name> <argi2>
          ^<arg3-name> <argi3>)
}

sp {ground-action*apply*complete-ground-action*copy*grounded-action*superstate*4*arguments
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^num-args 4
              ^grounded-action <gnd>
              ^ground-argument   <arg1>
              ^ground-argument { <arg2> > <arg1>}
              ^ground-argument { <arg3> > <arg2>}
              ^ground-argument { <arg4> > <arg3>}
              ^ss-ground-info <info>)
   (<arg1> ^arg-name <arg1-name> ^instance <argi1>)
   (<arg2> ^arg-name <arg2-name> ^instance <argi2>)
   (<arg3> ^arg-name <arg3-name> ^instance <argi3>)
   (<arg4> ^arg-name <arg4-name> ^instance <argi4>)
-->
   (<info> ^grounded-action <gnd>)
   (<gnd> ^<arg1-name> <argi1>
          ^<arg2-name> <argi2>
          ^<arg3-name> <argi3>
          ^<arg4-name> <argi4>)
}

sp {ground-action*apply*complete-ground-action*copy*grounded-action*superstate*5*arguments
   (state <s> ^name ground-action
              ^operator.name complete-ground-action
              ^num-args 5
              ^grounded-action <gnd>
              ^ground-argument   <arg1>
              ^ground-argument { <arg2> > <arg1>}
              ^ground-argument { <arg3> > <arg2>}
              ^ground-argument { <arg4> > <arg3>}
              ^ground-argument { <arg5> > <arg4>}
              ^ss-ground-info <info>)
   (<arg1> ^arg-name <arg1-name> ^instance <argi1>)
   (<arg2> ^arg-name <arg2-name> ^instance <argi2>)
   (<arg3> ^arg-name <arg3-name> ^instance <argi3>)
   (<arg4> ^arg-name <arg4-name> ^instance <argi4>)
   (<arg5> ^arg-name <arg5-name> ^instance <argi5>)
-->
   (<info> ^grounded-action <gnd>)
   (<gnd> ^<arg1-name> <argi1>
          ^<arg2-name> <argi2>
          ^<arg3-name> <argi3>
          ^<arg4-name> <argi4>
          ^<arg5-name> <argi5>)
}
