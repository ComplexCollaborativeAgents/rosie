### state.deep-copy-subactions

# Support structure used when deep copying a list of subactions
#   Elaborated automatically [see copy-type.soar]

# First create a map with all subactions on it
sp {anystate*deep-copy-subactions*elaborate*copy
   (state <s> ^deep-copy-subactions <dcs>)
   (<dcs> ^source <source>)
-->
   (<s> ^subactions <subs>)
   (<dcs> ^copy-subactions-map <map>)
}

# Elaborate the top subaction onto the map
sp {anystate*deep-copy-subactions*elaborate*next*subaction
   (state <s> ^deep-copy-subactions <dcs>)
   (<dcs> ^source.next { <src-next-sub> <> none }
          ^copy-subactions-map <map>)
-->
   (<map> ^entry <e>)
   (<e> ^source <src-next-sub>)
}

# Recursively elaboration the next subactions onto the map
sp {anystate*deep-copy-subactions*elaborate*subaction*recursively
   (state <s> ^deep-copy-subactions.copy-subactions-map <map>)
   (<map> ^entry <e>)
   (<e> ^source <src-sub>)
   (<src-sub> ^next {<src-next-sub> <> none })
-->
   (<map> ^entry <new-e>)
   (<new-e> ^source <src-next-sub>)
}

# For each subaction, create a copy of it using the copy-action util (in action-utils)
sp {anystate*deep-copy-subactions*elaborate*copy-action*subaction
   (state <s> ^deep-copy-subactions.copy-subactions-map.entry <e>
              ^deep-copy-world.copy-objects-map <map>)
   (<e> ^source <src-sub>)
-->
   (<s> ^copy-action <ca>)
   (<ca> ^source-action <src-sub>
         ^destination <e>
         ^copy-name copy
         ^object-map <map>)
}

## Then create the new subactions structure using the subaction copies

# Rule for an empty list of subactions
sp {anystate*deep-copy-subactions*elaborate*next*subaction*none
   (state <s> ^deep-copy-subactions <dcs>
              ^subactions <subs>)
   (<dcs> ^source.next none)
-->
   (<subs> ^next none)
}

sp {anystate*deep-copy-subactions*elaborate*first*subaction
   (state <s> ^deep-copy-subactions <dcs>
              ^subactions <subs>)
   (<dcs> ^source.next { <src-sub> <> none }
          ^copy-subactions-map.entry <e>)
   (<e> ^source <src-sub>
        ^copy <sub-copy>)
-->
   (<subs> ^next <sub-copy>)
}

sp {anystate*deep-copy-subactions*elaborate*subaction*next*copy
   (state <s> ^deep-copy-subactions.copy-subactions-map <map>)
   (<map> ^entry <e1> <e2>)
   (<e1> ^source.next { <next-sub-src> <> none }
         ^copy <sub-copy>)
   (<e2> ^source <next-sub-src>
         ^copy <next-sub-copy>)
-->
   (<sub-copy> ^next <next-sub-copy>)
}

sp {anystate*deep-copy-subactions*elaborate*subaction*copy*next*none
   (state <s> ^deep-copy-subactions.copy-subactions-map <map>)
   (<map> ^entry <e>)
   (<e> ^source.next none
        ^copy <sub-copy>)
-->
   (<sub-copy> ^next none)
}


# LOOKAT
#sp {anystate*deep-copy-subactions*elaborate*answer
#   (state <s> ^deep-copy-subactions <dcs>)
#   (<dcs> ^source.answer <h>
#          ^copy <copy>)
#-->
#   (<copy> ^answer <h>)
#}
#
