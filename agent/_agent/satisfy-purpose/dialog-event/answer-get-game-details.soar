sp {dialog-event*agent-game-action-description*propose*get-game-details
   (state <s> ^name dialog-event
              -^generated
              -^combined
              ^gtype <gtype>
              ^<gtype> <p1>)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-game-details
        ^<gtype> <p1>
        ^descriptions <descs>)
}

###################################################################
# Apply operator get-game-details
###################################################################

sp {dialog-event*apply*get-game-details*add-descriptions-to-state
   (state <s> ^name dialog-event
              ^operator <o>
              ^gtype <gtype>
              ^<gtype> <p1>)
   (<o> ^name get-game-details
        ^<gtype> <p1>
        ^descriptions <descs>)
-->              
   (<s> ^descriptions <descs>)
}

# if there is only one argument, it is typically a property
sp {dialog-event*apply*get-game-details*retrieve-one-argument-predicates
   (state <s> ^name dialog-event
   		      ^descriptions <descs>
              ^operator <o>
              ^gtype <gtype>
              ^<gtype> <p1>)
   (<o> ^name get-game-details
        ^<gtype> <p1>
        ^descriptions <descs>)
   (<p1> ^handle <handle>
         ^nlp-set <set>)
   (<set> ^conditions <cond>)
   (<cond> ^name <name>
           ^args <args>
   		   ^parameter <param>
   		   ^negative <neg>)
   (<args> ^1 <obj1>
           -^2
           -^3)
   (<param> ^1 <first>)
   (<first> ^param-id <param-id>) # PR - it is not always the first param for eg: adjacent
-->
   (<descs> ^obj-desc <desc>)
   (<desc> ^1 <cond>
           ^param-id <param-id>
           ^negative <neg>)
}

# if only one param-id exists in a prepositional phrase, store it as one argument
sp {dialog-event*apply*get-game-details*retrieve-PP-predicate
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>
              ^gtype <gtype>
              ^<gtype> <p1>)
   (<o> ^name get-game-details
        ^<gtype> <p1>)
   (<p1> ^handle <handle>
         ^nlp-set <set>)
   (<set> ^conditions <cond>)
   (<cond> ^name <name>
   		   ^args <args>
   		   ^parameter <param>)
   (<args> ^1 <obj1>
           ^2 <obj2>
           -^3)
   (<param> ^1 <first>
            -^2)
   (<first> ^param-id <param-id>) # PR - you might have to consider the fact that this is a type single and not set, TODO handle it in soar
-->
   (<descs> ^obj-desc <obj-desc>
            ^description <desc>)
   (<desc> ^prep <name>
           ^1 <param-id>)
   (<obj-desc> ^prep <name>
           ^1 <obj1> # possibly send the args <args> instead of 1 and 2 since there could be more. and then convert integer to string in java
           ^2 <obj2>
           ^param-id <param-id>)
}

# More than one param-id in a prepositional phrase indicates the object in the PP is used somewhere else?
sp {dialog-event*apply*get-game-details*retrieve-PP-and-individual-objects
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>
              ^gtype <gtype>
              ^<gtype> <p1>)
   (<o> ^name get-game-details
        ^<gtype> <p1>)
   (<p1> ^handle <handle>
         ^nlp-set <set>)
   (<set> ^conditions <cond>)
   (<cond> ^name <name>
   		   ^args <args>
   		   ^parameter <param>
   		   ^negative <neg>)
   (<args> ^1 <obj1>
           ^2 <obj2>
           -^3) # PR - maybe remove -^3 and instead add a rule that adds a third value if it exists?
   (<param> ^1 <first>
            ^2 <second>)
   (<first> ^param-id <param-id-1>) # PR - you might have to consider the fact that this is a type single and not set
   (<second> ^param-id <param-id-2>)
-->
   (<descs> ^description <desc>
            ^obj-desc <desc2>
            ^obj-desc <desc3>)
   (<desc> ^prep <name>
           ^1 <param-id-1> # PR - possibly send the args <args> instead of 1 and 2 since there could be more. and then convert integer to string in java
           ^2 <param-id-2>)
   (<desc2> ^1 <obj1>
            ^param-id <param-id-1>)
   (<desc3> ^1 <obj2>
            ^param-id <param-id-2>)
}

# PR -  this only handles equal values - greater than, lesser than must be handled 
# The following rule handles predicates with the same param-id for a given condition for e.g. Color of object A is color of object B, here both colors will have the same param-id
# both predicates would have return-type number
sp {dialog-event*apply*get-game-details*retrieve-attribute-based-conditions
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>
              ^gtype <gtype>
              ^<gtype> <p1>)
   (<o> ^name get-game-details
        ^<gtype> <p1>)
   (<p1> ^handle <handle>
         ^nlp-set <set>)
   (<set> ^conditions <cond>
          ^conditions { <cond2> < <cond> })
   (<cond> ^name <name>
           ^return-type number
           ^args <arg1>
           ^parameter <param1>)
   (<cond2> ^name <name>
            ^return-type number
            ^args <arg2>
            ^parameter <param2>)
   (<param1> ^1.param-id <param-value>
             ^2.param-id <param-obj1-id>)
   (<param2> ^1.param-id <param-value>
             ^2.param-id { <> <param-obj1-id> <param-obj2-id> })
   (<arg1> -^1
           ^2 <obj1>)
   (<arg2> -^1
           ^2 <obj2>)
-->
   (<descs> ^obj-desc <desc1>
            ^obj-desc <desc2>
            ^description <desc>)
   (<desc1> ^1 <obj1>
            ^param-id <param-obj1-id>)
   (<desc2> ^1 <obj2>
            ^param-id <param-obj2-id>)
   (<desc> ^name <name>
           ^prep is
           ^1 <param-obj1-id>
           ^2 <param-obj2-id>)
}

# The following rule handles predicates with the same param-id where one of the predicates is an attribute and the other is a value (String/number), both predicates have
# return-type number for e.g. The number of matched locations is 5
sp {dialog-event*apply*get-game-details*retrieve-attribute-based-condition-and-value
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>
              ^gtype <gtype>
              ^<gtype> <p1>)
   (<o> ^name get-game-details
        ^<gtype> <p1>)
   (<p1> ^handle <handle>
         ^nlp-set <set>)
   (<set> ^conditions <cond>
          ^conditions <cond2>)
   (<cond> ^name <name>
           ^return-type number
           ^args <arg1>
           ^parameter <param1>)
   (<arg1> -^1
           ^2 <obj1>)
   (<cond2> ^name <name2>
            ^return-type number
            ^result <res>
            ^parameter <param2>)
   (<param1> ^1.param-id <param-value>
             ^2.param-id <param-obj1-id>)
   (<param2> ^1.param-id <param-value>)
   (<res> ^set <res-set>)
   (<res-set> ^instance <inst>)
   (<inst> ^1 <res-val>)
-->
   (<descs> ^obj-desc <desc1>
            ^description <desc>)
   (<desc1> ^1 <obj1>
            ^param-id <param-obj1-id>)
   (<desc> ^name <name>
           ^prep number
           ^1 <param-obj1-id>
           ^2 <res-val>)
}

# The following rule will fire only in case of actions that have verbs present in them       
sp {dialog-event*apply*get-game-details*retrieve-verbs
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>
              ^action <p1>)
   (<o> ^name get-game-details
        ^action <p1>)
   (<p1> ^handle <handle>
         ^nlp-set <set>
         ^verb-structure <verb-struct>)
   (<verb-struct> ^<arg-num> <verb>)
   (<verb> ^verb-name <verb-name>
           ^verb-prep <verb-prep>
           ^1 <param-id1>
           ^2 <param-id2>)
-->
   (<descs> ^verb <verb>)
}

######################################
# Propose add generated attribute
######################################
# Adding generated attribute after the completion of retrieved game details

sp {dialog-event*op-no-change*propose*add-generated-attribute
   (state <s> ^superstate.name dialog-event
              ^attribute operator
              ^impasse no-change)            
-->
   (<s> ^operator <o> +)
   (<o> ^name add-generated-attribute)
}

# The following rule fires in case of an operator no-change, no individual description/obj-desc/verb have been added to the ^descriptions
sp {dialog-event*op-no-change*apply*add-generated-attribute*no
   (state <s> ^superstate <ss>
              ^attribute operator
              ^impasse no-change
              ^operator.name add-generated-attribute)
   (<ss> ^name dialog-event
         ^descriptions <descs>)
   (<descs> -^description
            -^obj-desc
            -^verb)
-->
   (<ss> ^generated no)
}

sp {dialog-event*op-no-change*apply*add-generated-attribute*yes
   (state <s> ^superstate <ss>
              ^attribute operator
              ^impasse no-change
              ^operator.name add-generated-attribute)
   (<ss> ^name dialog-event
         ^descriptions <descs>)
   (<descs> ^<< description obj-desc verb >> <desc>)
   -{ (<descs> ^<< description obj-desc verb >> {> <desc> <desc2>})}
-->
   (<ss> ^generated yes)
}

########################################################
# Report answer using complete-answer-get-game-details
########################################################

# PR - TODO: Add a ^status complete to ^<gtype> in the apply-get-game-details rule and then on that basis it completes 
sp {dialog-event*propose*complete-answer-get-game-details
   (state <s> ^name dialog-event
              ^generated <gen>
              ^combined
              ^descriptions <descs>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-answer-get-game-details
        ^descriptions <descs>
        ^generated <gen>)   
}


sp {dialog-event*apply*complete-answer-get-game-details*send-message
   (state <s> ^name dialog-event
              ^purpose.type <type>
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name complete-answer-get-game-details
        ^descriptions <descs>
        ^generated <gen>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type <type>
          ^fields.descriptions <descs>)
   (<descs> ^generated <gen>)
}
 
sp {dialog-event*apply*complete-answer-get-game-details*mark*performed
   (state <s> ^name dialog-event
              ^operator.name complete-answer-get-game-details
              ^top-state.interaction.status <status>)
-->
   (<status> ^performed.dialog-event <de>)
   (<de> ^type explanation
         ^originator agent)
}     
