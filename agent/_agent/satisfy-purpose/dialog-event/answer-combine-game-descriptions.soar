# This operator is proposed when multiple predicates can be combined as one for e.g. red block on a location and location is near clear location can be combined as
# red block is on a location that is near a clear location.

sp {dialog-event*propose*combine-game-descriptions
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^generated yes)
   (<descs> ^description <desc1>
            ^description { <desc2> <> <desc1> })
   (<desc1> ^prep <prep1>
            ^1 <paramid1>
            ^2 <paramid2>)
   (<desc2> ^prep <prep2>
            ^1 <paramid2>
            ^2 <paramid3>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name combine-game-descriptions
        ^description <desc1>
        ^description <desc2>)
}

sp {dialog-event*apply*combine-game-descriptions
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>)
   (<descs> ^description <desc1>
            ^description { <desc2> <> <desc1> })
   (<desc1> ^prep <prep1>
            ^1 <paramid1>
            ^2 <paramid2>)
   (<desc2> ^prep <prep2>
            ^1 <paramid2>
            ^2 <paramid3>)
   (<o> ^name combine-game-descriptions
        ^description <desc1>
        ^description <desc2>)
-->
   (<descs> ^description <new-desc>)            
   (<new-desc> ^prep1 <prep1>
               ^prep2 <prep2>
               ^1 <paramid1>
               ^2 <paramid2>
               ^3 <paramid3>)
   (<s> ^combined yes)
}

sp {dialog-event*apply*combine-game-descriptions*remove-redundant-description
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>)
   (<o> ^name combine-game-descriptions
        ^description <desc1>
        ^description <desc2>)
-->
   (<descs> ^description <desc1> -
            ^description <desc2> -)
}

# When no descriptions that need to be combined exist
sp {dialog-event*state-no-change*descriptions-not-combined
   (state <s> ^superstate <ss>
              ^attribute state
              ^impasse no-change)
   (<ss> ^name dialog-event
         ^descriptions <descs>
         ^generated yes)
-->
   (<ss> ^combined no)
}
              