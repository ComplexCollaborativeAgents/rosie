# elaborate dx, dy
sp {selection*elaborate*evaluation*dx*dy
   (state <s> ^name selection
              ^evaluation <e>
              ^superstate.desired.waypoint <dwp>
             -^superstate.current-location <cwp>)
   (<e> ^current-location <cwp>)
   (<cwp> ^x <cx> ^y <cy>)
   (<dwp> ^x <dx> ^y <dy>)
-->
   (<e> ^dx (- <dx> <cx>)
        ^dy (- <dy> <cy>))
}

# elaborate estimated-cost

# Not at desired waypoint
sp {selection*elaborate*evaluation*estimated-cost
   :default
   (state <s> ^name selection
              ^evaluation <e>
             -^superstate.current-location <cwp>
              ^superstate.desired.waypoint <dwp>)
   (<e> ^current-location <cwp>
       -^updated-estimate true  ## JEL 2/11/
        ^dx <dx>
        ^dy <dy>)
   (<cwp> ^handle <ch>)
   (<dwp> ^handle { <dh> <> <ch> })
-->
   # Pythagorean Theorem
   (<e> ^estimated-cost (sqrt (+ (* <dx> <dx>) (* <dy> <dy>))))
}

# estimated-cost during adjacent area search
# set to 0 because we don't know which waypoint we want, 
#   accept the closest that satisfies the test
sp {selection*elaborate*evaluation*estimated-cost*adjacent-area*search
   (state <s> ^name selection
              ^evaluation <e>
             -^superstate.current-location <cwp>
              ^superstate.desired.area <area>)
   (<e> ^current-location <cwp>
       -^updated-estimate true)  ## JEL 2/11/
   (<cwp> ^handle <ch>)
-->
   # Pythagorean Theorem
   (<e> ^estimated-cost 0.000001)
}

# elaborate: total-estimated-cost
sp {selection*elaborate*evaluation*total-estimated-cost
   :default
   (state <s> ^name selection
              ^evaluation <e>)
   (<e> ^estimated-cost <ec>
        ^path-cost <pc>)
-->
   (<e> ^total-estimated-cost (+  <ec> <pc>))
}

# print out info about path costs
sp {selection*monitor*write*path*costs
   :default
   (state <s> ^name selection
              ^agent-params.debug-path-planning true
              ^evaluation <e>
              ^superstate.desired.waypoint.handle <des-handle>)
   (<e> ^estimated-cost <ec>
        ^path-cost <pc>
        ^current-location.handle <cur-handle>)
-->
   (write (crlf) |Estimated Cost from | <cur-handle> | to | <des-handle> |: | <ec>)
   (write (crlf) |Path Cost at | <cur-handle> |: | <pc>)
   (write (crlf) |Total Estmated Cost: | <e> | | (+  <ec> <pc>) | : | <e> | path: | <pc> | Estimated Cost: | <ec> )
}

sp {selection*monitor*write*path*costs*area
   :default
   (state <s> ^name selection
              ^agent-params.debug-path-planning true
              ^evaluation <e>
              ^superstate.desired.area.handle <des-handle>)
   (<e> ^estimated-cost <ec>
        ^path-cost <pc>
        ^current-location.handle <cur-handle>)
-->
   (write (crlf) |Estimated Cost from | <cur-handle> | to | <des-handle> |: | <ec>)
   (write (crlf) |Path Cost at | <cur-handle> |: | <pc>)
   (write (crlf) |Total Estmated Cost: | <e> | | (+  <ec> <pc>) | : | <e> | path: | <pc> | Estimated Cost: | <ec> )
}

