###################################################################
#
# interpret-describe-object
#   Occurs when the instructor asks Rosie to describe the said object
#   Construction: Describe the DP-QT

###################################################################

#PR - probably a hack, find ways to edit top state dialog context to do the following
sp {interpret-message*propose*interpret-describe-object
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type command
          ^action.handle describe
          -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-describe-object
        ^message <msg>)
}

# Elaborate useful information from the message onto the operator
# object: REQUIRED - the object asked about
sp {interpret-message*elaborate*interpret-describe-object
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-describe-object
        ^message <msg>)
   (<msg> ^arg1 <obj>
          ^action.handle <handle>)
-->
   (<o> ^object <obj>
        ^handle <handle>)
}

# here <handle> refers to the action command handle i.e. describe

sp {interpret-message*apply*interpret-describe-object
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-describe-object
        ^message <in-msg> 
        ^object <obj>
        ^handle <handle>)
-->
   (<in-msg> ^interpreted true)
   (<s> ^outgoing-message <msg>)
   (<msg> ^type feature-relation-description
          ^object <obj>
          ^handle <handle>)
}


