# PR - propose generate-feature requires the -^feature-relation-description to be distinguished from
# the create-object proposal. else it creates an operator tie. 
# But if you have -^feature-description in propose but ^feature-description in elaborate it results in a problem.
#Possibly create a separate operator to add feature-description to the state and then do the following ones
# The way to distinguish these two will be the generated thing.
# Explain all of this in a different file for this as well as what is and what predicate is question.

sp {send-message*feature-relation-description*propose*generate-feature-relation-description
   (state <s> ^name send-message
              ^message <msg>
              -^generated)
   (<msg> ^type feature-relation-description
          ^object <obj>
          ^handle <handle>)
-->
   (<s> ^operator <o> +)
   (<o> ^name generate-feature-relation-description
        ^object <obj>
        #^handle <handle> # include if this rule needs to fire only when it needs to describe an object
   )
}

sp {send-message*elaborate*feature-relation-description*generate-feature-relation-description
   (state <s> ^name send-message
              ^message.type feature-relation-description)
-->
   (<s> ^feature-relation-description <descs>)
}
                    
sp {send-message*feature-relation-description*apply*generate-feature-relation-description*get-2
   (state <s> ^name send-message
              ^operator <o>
              ^feature-relation-description <descs>
              ^top-state.world.predicates <wpreds>)
   (<o> ^name generate-feature-relation-description
        ^object <obj>)
   (<wpreds> ^predicate <pred>)
   (<pred> ^instance <instance>
           ^handle <relation-handle>)
   (<instance> ^1 <obj>
               ^2 <pred-2>)
-->
   (<s> ^generated yes)
   (<descs>	^description <desc>
            ^object <obj>)
   (<desc> ^position 1 # position of the object to be described
           ^handle <relation-handle>
           ^2 <pred-2>)
}

sp {send-message*feature-relation-description*apply*generate-feature-relation-description*get-1
   (state <s> ^name send-message
              ^operator <o>
              ^feature-relation-description <descs>
              ^top-state.world.predicates <wpreds>)
   (<o> ^name generate-feature-relation-description
        ^object <obj>)
   (<wpreds> ^predicate <pred>)
   (<pred> ^instance <instance>
           ^handle <relation-handle>)
   (<instance> ^1 <pred-1>
               ^2 <obj>)
-->
   (<s> ^generated yes)
   (<descs>	^description <desc>
            ^object <obj>)
   (<desc> ^position 2
           ^1 <pred-1>
           ^handle <relation-handle>)
}

sp {generate-feature-relation-description*elaborate*feature-relation-description*not-generated
   (state <s> ^name generate-feature-relation-description
              ^superstate <ss>)
   (<ss> ^message.type feature-relation-description)
-->
   (<ss> ^generated no)
}       

# Creating a message to send back to the instructor based on the description generated by the above rules

sp {send-message*propose*create-feature-relation-description
   (state <s> ^name send-message
              ^message.type feature-relation-description
              ^generated <gen> # PR  - This will be added only if it goes to apply-generate. If the object does not exist, must write a propose rule/condition to handle the same (if generated is no, send empty object - condition spoken about before)
              ^feature-relation-description <desc>
              -^created-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-feature-relation-description
        ^feature-relation-description <desc>)
}

sp {send-message*apply*create-object-description
   (state <s> ^name send-message
              ^operator <o>
              ^generated <gen>)
   (<o> ^name create-feature-relation-description
        ^feature-relation-description <desc>)
-->
   (<s> ^created-message <msg>)
   (<desc> ^generated <gen>)
   (<msg> ^type feature-relation-description
          ^originator agent
          ^fields.object <desc>)
}                     