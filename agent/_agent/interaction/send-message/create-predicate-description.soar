sp {send-message*predicate-description*propose*get-predicate-description
   (state <s> ^name send-message
              ^message <msg>
              -^predicate-description)
   (<msg> ^type predicate-description
          ^object <obj>
          ^property <prop>)
   (<prop> ^handle <att>)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-predicate-description
        ^object <obj>
        ^attribute <att>)
}

sp {send-message*predicate-description*apply*get-predicate-description
   (state <s> ^name send-message
              ^operator <o>
              -^predicate-description)
   (<o> ^name get-predicate-description
        ^object.predicates <preds>
        ^attribute <att>)
   (<preds> ^<att> <val>)
-->
   (<s> ^predicate-description <desc>)
   (<desc> ^attribute <att>
           ^value <val>)
}

# The following rules create the description to send back as an answer
sp {send-message*propose*create-predicate-description
   (state <s> ^name send-message
              ^message.type predicate-description
              ^predicate-description <desc>
              -^created-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-predicate-description
        ^description <desc>)
}

sp {send-message*apply*create-predicate-description
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-predicate-description
        ^description <desc>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type predicate-description
          ^originator agent
          ^fields.predicate <desc>)
}         