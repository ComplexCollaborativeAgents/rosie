sp {send-message*object-description*propose*generate-object-description
   (state <s> ^name send-message
              ^message <msg>
             -^object-description)
   (<msg> ^type object-description
          ^object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name generate-object-description
        ^object <obj>)
}

sp {send-message*object-description*apply*generate-object-description*get-1
   (state <s> ^name send-message
              ^operator <o>
              -^object-description
              ^top-state.world.predicates <wpreds>)
   (<o> ^name generate-object-description
        ^object <obj>)
   (<obj> ^handle <obj-handle>
          ^2 <pred-2>)
   (<wpreds> ^predicate <pred>)
   (<pred> ^instance <instance>
           ^handle <obj-handle>)
   (<instance> ^2 <pred-2>
               ^1 <pred-1>)
-->
   (<s> ^object-description <pred-1>)     
}

# This handles questions where the given object is the second predicate in a relation.
sp {send-message*object-description*apply*generate-object-description*get-2
   (state <s> ^name send-message
              ^operator <o>
              -^object-description
              ^top-state.world.predicates <wpreds>)
   (<o> ^name generate-object-description
        ^object <obj>)
   (<obj> ^handle <obj-handle>
          ^2 <pred-2>)
   (<wpreds> ^predicate <pred>)
   (<pred> ^instance <instance>
           ^handle <obj-handle>)
   (<instance> ^1 <pred-2>
               ^2 <pred-1>)
-->
   (<s> ^object-description <pred-1>)     
}
   
sp {send-message*propose*create-object-description
   (state <s> ^name send-message
              ^message.type object-description
              ^object-description <desc>
             -^created-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-object-description
        ^description <desc>)
}

sp {send-message*apply*create-object-description
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-object-description
        ^description <desc>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type object-description
          ^originator agent
          ^fields.object <desc>)
}