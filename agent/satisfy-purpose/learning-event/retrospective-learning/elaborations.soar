sp {retrospective-learning*elaborate*present-id
   (state <s> ^name retrospective-learning
              ^superstate.operator.present-id <id>)
-->
   (<s> ^present-id <id>)
}

sp {retrospective-learning*elaborate*current-action
   (state <s> ^name retrospective-learning
              ^superstate.purpose.parameters.action <action>)
-->
   (<s> ^current-action <action>)
}

sp {retrospective-learning*elaborate*action-handle
   (state <s> ^name retrospective-learning
              ^current-action.execution-operator.action-handle <h>)
-->
   (<s> ^action-handle <h>)
}

sp {retrospective-learning*elaborate*subactions-stack
   (state <s> ^name retrospective-learning)
-->
   (<s> ^subactions-stack <subs>)
   (<subs> ^next none
           ^segments <segs>)
}

sp {retrospective-learning*elaborate*subactions-stack*segment
   (state <s> ^name retrospective-learning
              ^subactions-stack <subs>)
   (<subs> ^next { <seg> <> none }
           ^segments <segs>)
-->
   (<segs> ^segment <seg>)
}
        
sp {retrospective-learning*elaborate*subactions-stack*segment*next
   (state <s> ^name retrospective-learning
              ^subactions-stack <subs>)
   (<subs> ^segments <segs>)
   (<segs> ^segment.next { <seg> <> none })
-->
   (<segs> ^segment <seg>)
}

sp {retrospective-learning*elaborate*subactions*next*none
   (state <s> ^name retrospective-learning
              ^subactions-stack.segments.segment <seg>)
   (<seg> ^subaction-copy <sub>
          ^next none)
-->
   (<sub> ^next none)
}

sp {retrospective-learning*elaborate*subactions*next*sub
   (state <s> ^name retrospective-learning
              ^subactions-stack.segments.segment <seg>)
   (<seg> ^subaction-copy <sub>
          ^next.subaction-copy <next>)
-->
   (<sub> ^next <next>)
}

sp {retrospective-learning*elaborate*subactions*top
   (state <s> ^name retrospective-learning
              ^subactions-stack.next.subaction-copy <sub>)
-->
   (<s> ^subactions <subs>)
   (<subs> ^next <sub>)
}
              

#sp {retrospective-learning*elaborate*subactions*answer
#   (state <s> ^name retrospective-learning
#              ^subactions <subs>
#              ^current-action.answer.handle <h>)
#-->
#  (<subs> ^answer <h>)
#}

sp {retrospective-learning*elaborate*world*predicate*on*top-state
   (state <s> ^name retrospective-learning
              ^learning-context.episode.world.predicates <preds>
              ^top-state.world.predicates.predicate <pred>
              ^world.predicates <cur-preds>)
   (<preds> -^predicate.handle <pred-handle>)
   (<pred> ^handle <pred-handle>)
-->
   (<cur-preds> ^predicate <new-pred>)
   (<new-pred> ^handle <pred-handle>)
}
