sp {learn-proposal-rule*propose*retrieve-initial-episode
   (state <s> ^name learn-proposal-rule
             -^initial-episode
             -^copy-action-workspace)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-initial-episode)
}

sp {learn-proposal-rule*apply*retrieve-initial-episode
   (state <s> ^name learn-proposal-rule
              ^operator.name retrieve-initial-episode
              ^superstate.action-handle <action-handle>
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^action-stack.top <top>)
   (<top> ^start-of-execution true
          ^execution-operator.action-handle <action-handle>)
}

sp {learn-proposal-rule*apply*retrieve-initial-episode*success
   (state <s> ^name learn-proposal-rule
              ^operator.name retrieve-initial-episode
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^result.retrieved <episode>
            ^command <cmd>)
   (<res> ^success <q>
          ^retrieved <episode>
          ^memory-id <ep-id>
          ^normalized-match-score 1.0)
   (<cmd> ^query <q>)
-->
   (<s> ^initial-episode <episode>)
   (<cmd> ^query <q> -)
}

