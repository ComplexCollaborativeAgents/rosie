sp {retrospective-learning*SNC*elaborate*name*learn-proposal-rule
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate.name retrospective-learning)
-->
   (<s> ^name learn-proposal-rule)
}

sp {learn-proposal-rule*elaborate*retrieved-action
   (state <s> ^name learn-proposal-rule
              ^initial-episode.operator.action.execution-operator <op>)
-->
   (<s> ^retrieved-action <op>)
}

sp {learn-proposal-rule*elaborate*world*superstate
   (state <s> ^name learn-proposal-rule
              ^superstate.world <w>)
-->
   (<s> ^world <w>)
}

sp {learn-proposal-rule*elaborate*arg-set
   (state <s> ^name learn-proposal-rule)
-->
   (<s> ^arg-set <set>)
}

sp {learn-proposal-rule*elaborate*arg-info*no*args
   (state <s> ^name learn-proposal-rule
              ^retrieved-action <a>)
   (<a> -^<name1>.arg-type <t1>)
-->
   (<s> ^arg-info <i>)
   (<i> ^num-args 0)
}

sp {learn-proposal-rule*elaborate*arg-info*1*args
   (state <s> ^name learn-proposal-rule
              ^retrieved-action <a>)
   (<a> ^{ <name1> }.arg-type <t1>
       -^{ <name2> <> <name1> }.arg-type <t2>)
-->
   (<s> ^arg-info <i>)
   (<i> ^num-args 1
        ^1 <name1>)
}

sp {learn-proposal-rule*elaborate*arg-info*2*args
   (state <s> ^name learn-proposal-rule
              ^retrieved-action <a>)
   (<a> ^{ <name1>           < <name2> }.arg-type <t1>
        ^{ <name2> > <name1>           }.arg-type <t2>
       -^{ <name3> <> <name2> <> <name1> }.arg-type <t3>)
-->
   (<s> ^arg-info <i>)
   (<i> ^num-args 2
        ^1 <name1>
        ^2 <name2>)
}

sp {learn-proposal-rule*elaborate*arg-set*object*arg
   (state <s> ^name learn-proposal-rule
              ^retrieved-action.<arg-name> <arg>
              ^arg-info <info>
              ^arg-set <set>
              ^world.objects.object <obj>)
   (<info> ^<arg-num> <arg-name>)
   (<arg> ^arg-type object 
          ^arg-status <status>
          ^id.predicates.category <cat>)
   (<obj> ^predicates.category <cat>)
-->
   (<set> ^<arg-num> <new-arg>)
   (<new-arg> ^arg-type object
              ^arg-status <status>
              ^id <obj>)
}

sp {learn-proposal-rule*elaborate*arg-set*predicate*arg
   (state <s> ^name learn-proposal-rule
              ^retrieved-action.<arg-name> <arg>
              ^arg-info <info>
              ^arg-set <set>
              ^world.objects.object <obj>)
   (<info> ^<arg-num> <arg-name>)
   (<arg> ^arg-type predicate 
          ^arg-status <status>
          ^handle <ph>
          ^{2}.predicates.category <cat>)
   (<obj> ^predicates.category <cat>)
-->
   (<set> ^<arg-num> <new-arg>)
   (<new-arg> ^arg-type predicate
              ^arg-status <status>
              ^handle <ph>
              ^2 <obj>)
}

sp {learn-proposal-rule*propose*action-operator*no*args
   (state <s> ^name learn-proposal-rule
              ^retrieved-action <a>
              ^arg-info.num-args 0)
   (<a> ^name <name>
        ^action-handle <h>
        ^perform <perf>
       -^<arg-name>.arg-type <t>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name <name>
        ^action-handle <h>
        ^subaction-handle none
        ^perform <perf>)
}

sp {learn-proposal-rule*propose*action-operator*1*arg
   (state <s> ^name learn-proposal-rule
              ^retrieved-action <a>
              ^arg-info <info>
              ^arg-set <set>)
   (<a> ^name <name>
        ^action-handle <h>
        ^perform <perf>)
   (<info> ^num-args 1
           ^1 <arg1-name>)
   (<set> ^1 <arg1>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name <name>
        ^action-handle <h>
        ^subaction-handle none
        ^perform <perf>
        ^<arg1-name> <arg1>)
}

sp {learn-proposal-rule*propose*action-operator*2*args
   (state <s> ^name learn-proposal-rule
              ^retrieved-action <a>
              ^arg-info <info>
              ^arg-set <set>)
   (<a> ^name <name>
        ^action-handle <h>
        ^perform <perf>)
   (<info> ^num-args 2
           ^1 <arg1-name>
           ^2 <arg2-name>)
   (<set> ^1 <arg1>
          ^2 <arg2>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name <name>
        ^action-handle <h>
        ^subaction-handle none
        ^perform <perf>
        ^<arg1-name> <arg1>
        ^<arg2-name> <arg2>)
}

sp {learn-proposal-rule*reject*operator*object*arg*no*match
   (state <s> ^name learn-proposal-rule
              ^retrieved-action.<arg-name> <arg>
              ^operator <o> +)
   (<arg> ^arg-type object
          ^id.handle <h>)
   (<o> ^<arg-name> <op-arg>)
   (<op-arg> ^arg-type object
             ^id.handle <> <h>)
-->
   (<s> ^operator <o> -)
}

sp {learn-proposal-rule*reject*operator*predicate*arg*no*match
   (state <s> ^name learn-proposal-rule
              ^retrieved-action.<arg-name> <arg>
              ^operator <o> +)
   (<arg> ^arg-type predicate
          ^{2}.handle <h>)
   (<o> ^<arg-name> <op-arg>)
   (<op-arg> ^arg-type predicate
             ^{2}.handle <> <h>)
-->
   (<s> ^operator <o> -)
}
              

