sp {learn-whenever-task*propose*create-new-tcn
   (state <s> ^name learn-whenever-task
              ^whenever-segment <seg>
              ^general-conditions <conds>
             -^task-concept-network)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-new-tcn
        ^conditions <conds>
        ^segment <seg>)
}

sp {learn-whenever-task*elaborate*task-handle
   (state <s> ^name learn-whenever-task
              ^operator <o> +)
   (<o> ^name create-new-tcn)
-->
   (<o> ^task-handle (make-constant-symbol |task|))
}

sp {learn-whenever-task*apply*create-new-tcn
   (state <s> ^name learn-whenever-task
              ^operator <o>)
   (<o> ^name create-new-tcn
        ^conditions <conds>
        ^task-handle <task-handle>)
-->
   (<s> ^task-concept-network <tcn>)
   (<tcn> ^handle <task-handle>
          ^item-type action
          ^procedural <proc>
          ^unlearned-whenever-proposal true)
   (<proc> ^op_name (concat |op_| <task-handle>)
           ^conditions <conds>
           ^subtasks <subs>)

   (<s> ^to-store <tcn> <proc> <conds>)
}

sp {learn-whenever-task*apply*store*conditions*predicates
   (state <s> ^name learn-whenever-task
              ^operator <o>)
   (<o> ^name create-new-tcn
        ^conditions.{ <pnum> << 1 2 3 4 5 >> } <pred>)
-->
   (<s> ^to-store <pred>)
}

sp {learn-whenever-task*apply*store*conditions*predicates*args
   (state <s> ^name learn-whenever-task
              ^operator <o>)
   (<o> ^name create-new-tcn
        ^conditions.<pnum>.{ <parg> << 1 2 3 4 5 >> } <arg>)
-->
   (<s> ^to-store <arg>)
}

sp {learn-whenever-task*apply*store*conditions*predicates*arg*defaults
   (state <s> ^name learn-whenever-task
              ^operator <o>)
   (<o> ^name create-new-tcn
        ^conditions.<pnum>.<parg>.{ << default imagined >> } <def>)
-->
   (<s> ^to-store <def>)
}
