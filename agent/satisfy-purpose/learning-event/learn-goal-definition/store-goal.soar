sp {learn-goal-definition*propose*store-goal
   (state <s> ^name learn-goal-definition
              ^generalized-goal <goal>
             -^stored <goal>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name store-goal
        ^goal <goal>)
}

sp {learn-goal-definition*apply*store-goal
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^action-id <action>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal <goal>)
-->
   (<action> ^goal <goal>)
   (<cmd> ^store <goal> <action>)
   (<s> ^stored <goal>)
}

sp {learn-goal-definition*apply*store-goal*predicate
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal.predicate <pred>)
-->
   (<cmd> ^store <pred>)
}

sp {learn-goal-definition*apply*store-goal*predicate*arg
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal.predicate.{ << id 1 2 3 4 5 >> } <arg>)
-->
   (<cmd> ^store <arg>)
}

sp {learn-goal-definition*apply*store-goal*predicate*arg*default
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal.predicate.{ << id 1 2 3 4 5 >> }.default <def>)
-->
   (<cmd> ^store <def>)
}

sp {learn-goal-definition*apply*store-goal*predicate*arg*imagined
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal.predicate.{ << id 1 2 3 4 5 >> }.imagined <img>)
-->
   (<cmd> ^store <img>)
}
