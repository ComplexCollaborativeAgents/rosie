sp {learn-goal-definition*elaborate*state*name*generalize-task-operator
   (state <s> ^name learn-goal-definition)
-->
   (<s> ^name generalize-task-operator)
}

sp {learn-goal-definition*elaborate*generalize-info*reference*action
   (state <s> ^name learn-goal-definition
              ^task-operator <aop>
              ^task-concept-network.procedural <aid>)
-->
   (<s> ^reference-task-op <aop>
        ^reference-tcn-proc <aid>)
}

## we want to generalize the goal rep
sp {learn-goal-definition*elaborate*generalize-task-argument*predicate-set
  (state <s> ^name learn-goal-definition)
-->
  (<s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^instantiation <i>)
}

sp {learn-goal-definition*elaborate*generalize-task-argument*object*reference-info
   (state <s> ^name learn-goal-definition
              ^generalize-task-argument.instantiation <i>
              ^reference-info <info>)
   (<i> ^arg-type object
        ^id <obj>)
   (<info> ^referent <obj>)
-->
   (<i> ^reference-info <info>)
}


# ENUMERATE: 1-5 goal predicates

sp {learn-goal-definition*elaborate*generalize-task-argument*predicate-set*1*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instantiation <i>)
   (<o> ^goal-predicate <p1>
       -^goal-predicate { <p2> <> <p1> })
-->
   (<i> ^pred-count 1
        ^1 <p1>)
}

sp {learn-goal-definition*elaborate*generalize-task-argument*predicate-set*2*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instantiation <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
       -^goal-predicate { <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 2
        ^1 <p1>
        ^2 <p2>)
}

sp {learn-goal-definition*elaborate*generalize-task-argument*predicate-set*3*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instantiation <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
       -^goal-predicate { <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 3
        ^1 <p1>
        ^2 <p2>
        ^3 <p3>)
}

sp {learn-goal-definition*elaborate*generalize-task-argument*predicate-set*4*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instantiation <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
        ^goal-predicate { <p4> > <p3> }
       -^goal-predicate { <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 4
        ^1 <p1>
        ^2 <p2>
        ^3 <p3>
        ^4 <p4>)
}

sp {learn-goal-definition*elaborate*generalize-task-argument*predicate-set*5*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instantiation <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
        ^goal-predicate { <p4> > <p3> }
        ^goal-predicate { <p5> > <p4> }
       -^goal-predicate { <p6> <> <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 5
        ^1 <p1>
        ^2 <p2>
        ^3 <p3>
        ^4 <p4>
        ^5 <p5>)
}

# copy the result of the generalization process
sp {learn-goal-definition*elaborate*generalized-goal*from*generalize-task-argument
  (state <s> ^name learn-goal-definition
             ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^general-arg-rep <rep>)
-->
  (<s> ^generalized-goal <rep>)
}
