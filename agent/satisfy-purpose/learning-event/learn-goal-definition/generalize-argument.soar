sp {learn-goal-definition*elaborate*state*name*generalize-action
   (state <s> ^name learn-goal-definition)
-->
   (<s> ^name generalize-action)
}

sp {learn-goal-definition*elaborate*generalize-info*reference*action
   (state <s> ^name learn-goal-definition
              ^action-operator <aop>
              ^task-concept-network.procedural <aid>)
-->
   (<s> ^reference-action <aop>
        ^reference-id <aid>)
}

## we want to generalize the goal rep
sp {learn-goal-definition*elaborate*generalize-argument*predicate-set
  (state <s> ^name learn-goal-definition)
-->
  (<s> ^generalize-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^instance <i>)
}

sp {learn-goal-definition*elaborate*generalize-argument*object*reference-info
   (state <s> ^name learn-goal-definition
              ^generalize-argument.instance <i>
              ^reference-info <info>)
   (<i> ^arg-type object
        ^id <obj>)
   (<info> ^referent <obj>)
-->
   (<i> ^reference-info <info>)
}


# ENUMERATE: 1-5 goal predicates

sp {learn-goal-definition*elaborate*generalize-argument*predicate-set*1*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instance <i>)
   (<o> ^goal-predicate <p1>
       -^goal-predicate { <p2> <> <p1> })
-->
   (<i> ^pred-count 1
        ^1 <p1>)
}

sp {learn-goal-definition*elaborate*generalize-argument*predicate-set*2*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instance <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
       -^goal-predicate { <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 2
        ^1 <p1>
        ^2 <p2>)
}

sp {learn-goal-definition*elaborate*generalize-argument*predicate-set*3*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instance <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
       -^goal-predicate { <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 3
        ^1 <p1>
        ^2 <p2>
        ^3 <p3>)
}

sp {learn-goal-definition*elaborate*generalize-argument*predicate-set*4*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instance <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
        ^goal-predicate { <p4> > <p3> }
       -^goal-predicate { <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 4
        ^1 <p1>
        ^2 <p2>
        ^3 <p3>
        ^4 <p4>)
}

sp {learn-goal-definition*elaborate*generalize-argument*predicate-set*5*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-argument <arg>)
   (<arg> ^arg-type predicate-set
          ^instance <i>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
        ^goal-predicate { <p4> > <p3> }
        ^goal-predicate { <p5> > <p4> }
       -^goal-predicate { <p6> <> <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<i> ^pred-count 5
        ^1 <p1>
        ^2 <p2>
        ^3 <p3>
        ^4 <p4>
        ^5 <p5>)
}

# copy the result of the generalization process
sp {learn-goal-definition*elaborate*generalized-goal*from*generalize-argument
  (state <s> ^name learn-goal-definition
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^generalized-rep <rep>)
-->
  (<s> ^generalized-goal <rep>)
}
