##### operator create-action-operator
# creates the basic structure for the operator as action-operator

# propose create-action-operator
sp {generate-action-operator*propose*create-action-operator
   (state <s> ^name generate-action-operator
              ^task-concept-network <tcn>
             -^action-operator.name)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-action-operator
        ^task-concept-network <tcn>)
} 

# create the action-operator strucutre with action-handle
sp {generate-action-operator*apply*create-action-operator
   (state <s> ^name generate-action-operator
              ^operator <o>)
   (<o> ^name create-action-operator
        ^task-concept-network <tcn>)
   (<tcn> ^handle <action-handle>)
-->
   (<s> ^action-operator <op>)
   (<op> ^action-handle <action-handle>)
}

sp {generate-action-operator*apply*create-action-operator*create*subaction-handle*none
   (state <s> ^name generate-action-operator
              ^operator.name create-action-operator
              ^top-state.action-stack.top none
              ^action-operator <a>)
-->
   (<a> ^subaction-handle none)
}

# Copy the name of the operator
sp {generate-action-operator*apply*create-action-operator*copy*operator*name
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^task-concept-network.procedural.op_name <name>)
-->
   (<op> ^name <name>)
}

# Copy the perform flag
sp {generate-action-operator*apply*create-action-operator*copy*perform
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^task-concept-network.perform <perf>)
-->
   (<op> ^perform <perf>)
}

sp {generate-action-operator*apply*create-action-operator*copy*perform*default
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^task-concept-network <tcn>)
   (<tcn> -^perform <perf>)
-->
   (<op> ^perform multiple)
}

sp {generate-action-operator*apply*create-action-operator*mark*learn-proposal
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^task-concept-network.learn-proposal <bool>)
-->
  (<op> ^learn-proposal <bool>)
}
