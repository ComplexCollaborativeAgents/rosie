# If any ^to-store <lti> wmes exist, store them in smem

# elaborate ^concepts-to-store true flag if any ^to-store <lti> exist
sp {generate-action-operator*elaborate*concepts-to-store
   (state <s> ^name generate-action-operator
              ^to-store <lti>)
-->
   (<s> ^concepts-to-store true)
}

# propose store-concepts if ^concepts-to-store true is present
sp {generate-action-operator*propose*store-concepts
   (state <s> ^name generate-action-operator
              ^concepts-to-store true
             -^stored-concepts)
-->
   (<s> ^operator <o> +)
   (<o> ^name store-concepts)
}

# create store command on smem.command link and mark stored-concepts true
sp {generate-action-operator*apply*store-concepts*store
   (state <s> ^name generate-action-operator
              ^operator.name store-concepts
              ^smem.command <cmd>
              ^to-store <lti>)
-->
   (<cmd> ^store <lti>)
   (<s> ^stored-concepts true)
}

# if a retrieve command is there, remove it so the stores can work
sp {generate-action-operator*apply*store-concepts*remove*retrieve
   (state <s> ^name generate-action-operator
              ^operator.name store-concepts
              ^smem.command <cmd>)
   (<cmd> ^retrieve <ret>)
-->
   (<cmd> ^retrieve <ret> -)
}
              