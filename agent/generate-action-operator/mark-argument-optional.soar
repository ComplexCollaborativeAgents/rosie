
# If this is not a new action, but we created a new procedural argument, 
#   Then that argument is optional (we've encountered the action without it before)
sp {generate-action-operator*propose*mark-argument-optional*new*procedural*arg
   (state <s> ^name generate-action-operator
             -^new-action true
             ^argument-info <arg-info>)
   (<arg-info> ^created-procedural-argument true
               ^procedural <parg>)
   (<parg> ^importance unknown)
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-argument-optional
        ^argument <parg>)
}

# If there is a procedural argument not included on the command, it must be optional
sp {generate-action-operator*propose*mark-argument-optional*simple
   (state <s> ^name generate-action-operator
              ^action-id.procedural.<arg-name> <parg>
             -^argument-info.arg-name <arg-name>)
   (<parg> ^arg-type << object concept >>
           ^importance unknown)
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-argument-optional
        ^argument <parg>)
}

# If there is a procedural argument not included on the command, it must be optional
sp {generate-action-operator*propose*mark-argument-optional*predicate
   (state <s> ^name generate-action-operator
              ^action-id.procedural.<arg-name> <parg>
             -^argument-info.arg-name <arg-name>)
   (<parg> ^arg-type predicate
           ^importance unknown)
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-argument-optional
        ^argument <parg>)
}

sp {generate-action-operator*apply*mark-argument-optional*mark*optional
   (state <s> ^name generate-action-operator
              ^operator <o>)
   (<o> ^name mark-argument-optional
        ^argument <parg>)
-->
   (<parg> ^importance optional)
   (<s> ^to-store <parg>)
}

sp {generate-action-operator*apply*mark-argument-optional*remove*unknown
   (state <s> ^name generate-action-operator
              ^operator <o>)
   (<o> ^name mark-argument-optional
        ^argument <parg>)
   (<parg> ^importance unknown)
-->
   (<parg> ^importance unknown -)
   (<s> ^to-store <parg>)
}
   
