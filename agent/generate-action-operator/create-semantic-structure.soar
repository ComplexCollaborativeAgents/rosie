#### operator create-semantic-structure
# initializes a new smem structure for the action the first time we see it

# propose create-semantic-structure if no ^procedural link exists on the task-concept-network
sp {generate-action-operator*propose*create-semantic-structure
   (state <s> ^name generate-action-operator
              ^task-concept-network <tcn>)
   (<tcn> -^procedural)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-semantic-structure)
}

# create the basic semantic structure for the action, 
#   and a new-action flag
sp {generate-action-operator*apply*create-semantic-structure
   (state <s> ^name generate-action-operator
              ^operator.name create-semantic-structure
              ^task-concept-network <tcn>)
   (<tcn> ^handle <handle>)
-->
   (<tcn> ^item-type action ^procedural <proc>)
   (<proc> ^op_name (make-constant-symbol (concat |op_| <handle>)) ^subactions <subs>)
   (<s> ^to-store <tcn> <proc>)
   (<s> ^new-action true)
}

        
