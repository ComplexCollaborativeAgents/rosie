sp {learn-task-action-model-substate*propose*learn-action-model-rule
   (state <s> ^name learn-task-action-model-substate)
-->
   (<s> ^operator <o> + >)
   (<o> ^name learn-action-model-rule)
}

sp {learn-task-action-model-substate*apply*learn-action-model-rule*mark*finished
   (state <s> ^name learn-task-action-model-substate
              ^operator.name learn-action-model-rule
              ^quiescence t
              ^superstate <ss>)
-->
   (<ss> ^learning-finished true)
}

sp {__task__action__model__*learn-action-model-rule*state*predicate
   (state <s> ^name learn-task-action-model-substate
              ^operator.name learn-action-model-rule
              ^superstate.problem-space <ps>
              ^desired <des>)
   (<ps> ^name action
         ^execution-type internal)
   (<des> ^pred-count 1
          ^<gpnum> <gp>)
   (<gp> ^type state
         ^handle <pred-h>
         ^property-handle <prop-h>
         ^1 <obj>)
   (<obj> ^predicates <obj-preds>)
-->
   (<obj-preds> ^<prop-h> <pred-h>)
}

sp {__task__action__model__*learn-action-model-rule*remove*other*predicate
   (state <s> ^name learn-task-action-model-substate
              ^operator.name learn-action-model-rule
              ^superstate.problem-space <ps>
              ^desired <des>)
   (<ps> ^name action
         ^execution-type internal)
   (<des> ^pred-count 1
          ^<gpnum> <gp>)
   (<gp> ^type state
         ^handle <pred-h>
         ^property-handle <prop-h>
         ^1 <obj>)
   (<obj> ^predicates <obj-preds>)
   (<obj-preds> ^<prop-h> { <other-pred> <> <pred-h> })
-->
   (<obj-preds> ^<prop-h> <other-pred> -)
}


sp {__task__action__model__*learn-task-action-model*1*relation
   (state <s> ^name learn-task-action-model-substate
              ^task-operator <task-op>
              ^superstate <ss>
              ^desired <des>)
   (<ss> ^operator <task-op>
         ^superstate <sss>
         ^problem-space <ps>
         ^world.predicates.predicate <pred>)
   (<ps> ^name action
         ^execution-type internal)
   (<des> ^pred-count 1
          ^1 <p1>)
   (<p1> ^type relation
         ^handle <ph>
         ^1 <obj1>
         ^2 <obj2>)
   (<pred> ^handle <ph>)
   -{(<pred> ^instance <i>)
     (<i> ^1 <obj1> ^2 <obj2>)}
-->
    (<pred> ^instance <i>)
    (<i> ^1 <obj1> ^2 <obj2>)
}

