sp {learn-select-next-goal-rule*propose*propose-select-next-goal
   (state <s> ^name learn-select-next-goal-rule
              ^next-goal-link.goal <g>
              ^can-select-goal true)
   (<g> ^handle <goal-h>
        ^item-type <goal-t>)
-->
   (<s> ^operator <o> +)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>)
}

### Learn a proposal rule for select-next-goal

sp {__SEL_NEXT_GOAL__*propose*select-next-goal*superstate
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
             -^conditions
             -^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>)
#   (<ss> ^problem-space.subproblem execute)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>)
}

sp {__SEL_NEXT_GOAL__*propose*select-next-goal*superstate*conditions
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
              ^conditions <c>
             -^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>)
#   (<ss> ^problem-space.subproblem execute)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^has-conditions true
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>)
}

### If we shouldn't chunk the rule, check for quiescence
         
sp {learn-select-next-goal-rule*apply*propose-select-next-goal*dont-chunk
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
              ^quiescence t
             -^conditions
              ^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>)
#   (<ss> ^problem-space.subproblem execute)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>)
}

sp {learn-select-next-goal-rule*apply*propose-select-next-goal*conditions*dont-chunk
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
              ^quiescence t
              ^conditions <c>
              ^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>)
#   (<ss> ^problem-space.subproblem execute)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^has-conditions true
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>)
}
         

