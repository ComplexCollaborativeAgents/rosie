sp {learn-select-next-goal-rule*propose*propose-select-next-goal
   (state <s> ^name learn-select-next-goal-rule
              ^next-goal-link.goal <g>
              ^can-select-goal true)
   (<g> ^handle <goal-h>
        ^item-type <goal-t>)
-->
   (<s> ^operator <o> +)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>)
}

sp {learn-select-next-goal-rule*elaborate*propose-select-next-goal*special-flag*default-link
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o> +
             -^conditions)
   (<o> ^name propose-select-next-goal)
-->
   (<o> ^special-flag default-link)
}

sp {learn-select-next-goal-rule*elaborate*propose-select-next-goal*special-flag*has-conditions
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o> +
              ^conditions <conds>
             -^next-goal-link.loop-exit)
   (<o> ^name propose-select-next-goal)
-->
   (<o> ^special-flag has-conditions)
}

sp {learn-select-next-goal-rule*elaborate*propose-select-next-goal*special-flag*loop-exit
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o> +
              ^conditions <conds>
              ^next-goal-link.loop-exit true)
   (<o> ^name propose-select-next-goal)
-->
   (<o> ^special-flag loop-exit)
}

### Learn a proposal rule for select-next-goal

sp {__SEL_NEXT_GOAL__*propose*select-next-goal*superstate
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
             -^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>
        ^special-flag <flag>)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>
           ^<flag> true)
}

### If we shouldn't chunk the rule, check for quiescence
         
sp {learn-select-next-goal-rule*apply*propose-select-next-goal*dont-chunk
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
              ^quiescence t
              ^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>
        ^special-flag <flag>)
#   (<ss> ^problem-space.subproblem execute)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>
           ^<flag> true)
}


