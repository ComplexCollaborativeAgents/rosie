sp {learn-subaction-proposal*propose*return-unmatched-subaction
   (state <s> ^name learn-subaction-proposal
              ^subaction-operator <op>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name return-unmatched-subaction
        ^subaction-operator <op>
        ^always-propose true)
}

sp {learn-subaction-proposal*reject*return-unmatched-subaction*if*not*unmatched
   (state <s> ^name learn-subaction-proposal
              ^operator <o> +
             -^no-subaction-matches true)
   (<o> ^name return-unmatched-subaction)
-->
   (<s> ^operator <o> -)
}

# CREATE RESULT
sp {TASK*PROPOSE*SUBTASK*learn-subaction-proposal*unmatched*subaction
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^superstate.superstate <ss>)
   (<o> ^name return-unmatched-subaction
        ^subaction-operator <op>)
-->
   (<ss> ^operator <op> +)
}

sp {learn-subaction-proposal*apply*return-unmatched-subaction*removed*learned*no*superstate
   (state <s> ^name learn-subaction-proposal
              ^superstate <ss>
              ^operator <o>
              ^subaction-handle <h>)
   (<o> ^name return-unmatched-subaction
        ^subaction-operator <op>)
   (<ss> ^name action-state-no-change
         ^task-concept-network.procedural.subactions.subaction <sub>)
   (<sub> ^handle <h>
          ^learned no)
-->
   (<sub> ^learned no -)
}

