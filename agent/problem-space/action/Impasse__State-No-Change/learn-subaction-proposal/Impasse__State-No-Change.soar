# If no known proposal rule matched, then just copy the created one to the superstate

sp {learn-subaction-proposal*state*no-change*elaborate*name*learn-subaction-proposal-substate
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate.name learn-subaction-proposal
             -^learning-action-utils-rule true)
-->
   (<s> ^name learn-subaction-proposal-substate)
}

sp {learn-subaction-proposal*state*no-change*propose*complete-learn-subaction-proposal
   (state <s> ^name learn-subaction-proposal-substate
              ^superstate.subaction-operator <op>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete-learn-subaction-proposal
        ^subaction-operator <op>)
}

# CREATE RESULT
sp {TASK*PROPOSE*SUBTASK*learn-subaction-proposal*unmatched*subaction
   (state <s> ^name learn-subaction-proposal-substate
              ^operator <o>
              ^superstate.superstate.superstate <ss>)
   (<o> ^name complete-learn-subaction-proposal
        ^subaction-operator <op>)
-->
   (<ss> ^operator <op> +)
}

sp {learn-subaction-proposal*apply*complete-learn-subaction-proposal*removed*learned*no*superstate
   (state <s> ^name learn-subaction-proposal-substate
              ^superstate <ss>
              ^operator <o>)
   (<o> ^name complete-learn-subaction-proposal
        ^subaction-operator <op>)
   (<ss> ^subaction-handle <h>
         ^superstate <sss>)
   (<sss> ^name action-state-no-change
          ^action-id.procedural.subactions.subaction <sub>)
   (<sub> ^handle <h>
          ^learned no)
-->
   (<sub> ^learned no -)
}

