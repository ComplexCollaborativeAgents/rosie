sp {learn-task-subaction*propose*store-task-subaction
  (state <s> ^name learn-task-subaction
             ^action-id.procedural.subactions <subs>
             ^subaction-structure <sub>)
-->
  (<s> ^operator <o> + >)
  (<o> ^name store-task-subaction
       ^subaction <sub>
       ^subaction-list <subs>)
}

sp {learn-task-subaction*apply*store-task-subaction*store*root
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction <sub>
        ^subaction-list <subs>)
-->
   (<subs> ^subaction <sub>)
   (<cmd> ^store <subs> <sub>)
}

sp {learn-task-subaction*apply*store-task-subaction*argument
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type <any>)
-->
   (<cmd> ^store <arg>)
}

sp {learn-task-subaction*apply*store-task-subaction*arg*slots
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 predicate >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {learn-task-subaction*apply*store-task-subaction*predicate*arg*default
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}

sp {learn-task-subaction*apply*store-task-subaction*arg*predicate*slot
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {learn-task-subaction*apply*store-task-subaction*predicate*arg*predicate*default
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}

sp {learn-task-subaction*apply*store-task-subaction*success*return*created-subaction
   (state <s> ^name learn-task-subaction
             ^operator <o>
             ^subaction-structure <sub>
             ^smem.result.success <sub>
             ^superstate <ss>)
   (<o> ^name store-task-subaction
        ^subaction <sub>)
   (<sub> ^handle <h>)
-->
   (<ss> ^created-subaction <h>)
}
