## op_pick-up1
sp {composite-action*propose*op_pick-up1*internal
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>
             -^world.objects.object.predicates.arm-status grabbed)
   (<ps> ^name action
         ^action.propose-action-operators true)
   (<obj> ^predicates <preds>)
   (<preds> ^category object
            ^visible true
            ^arm-status not-grabbed)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_pick-up1 
         ^action-handle pick-up1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

## op_put-down1
sp {composite-action*propose*op_put-down1*internal
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>
              ^world.objects.object { <obj2> <> <obj> })
   (<ps> ^name action
         ^action.propose-action-operators true)
   (<obj> ^predicates.arm-status grabbed)
   (<obj2> ^predicates <obj2-preds>)
   (<obj2-preds> ^visible true)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down1
         ^action-handle put-down1
         ^arg1 <arg1>
         ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj>)
   (<arg2> ^arg-type predicate
           ^default-handle on1
           ^any-predicate true
           ^2 <obj2>)
}

sp {composite-action*propose*op_put-down1*internal*current-location
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>)
   (<ps> ^name action
         ^action.propose-action-operators true)
   (<obj> ^predicates.arm-status grabbed)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down1
         ^action-handle put-down1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

## op_go-to1
sp {action*propose*op_go-to1*internal
   (state <s> ^problem-space <ps>
              ^world.objects.object <loc>
             -^world.robot.current-location <loc>)
   (<ps> ^name action
         ^action.propose-action-operators true)
  (<loc> ^predicates.category location)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_go-to1
        ^action-handle go-to1
         ^arg2 <arg2>)
   (<arg2> ^arg-type predicate
           ^handle to1
           ^2 <loc>)
}

sp {composite-action*propose*op_give1
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj1>
              ^world.objects.object <person>)
   (<ps> ^name action
         ^action.propose-action-operators true)
   (<obj1> ^predicates.arm-status grabbed)
   (<person> ^predicates.visible true
             ^predicates.category person)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_give1
         ^action-handle give1
         ^arg1 <arg1>
         ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj1>)
   (<arg2> ^arg-type predicate
           ^handle to1
           ^2 <person>)
}

