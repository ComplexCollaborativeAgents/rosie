#### Different failure cases that can arise during put-down

sp {op_give1*propose*report-failure*svs-filter-error
   (state <s> ^name op_give1
              ^current-action.execution-operator.action-handle <handle>
              ^{ << person-position-filter robot-position-filter >> <type> } <pos-filter>)
   (<pos-filter> ^result <res>)
   (<res> -^record.value)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type svs-filter-error
        ^failure-info <type>)
}

## missing-object: no input-link rep for the object
#sp {op_give1*propose*report-failure*missing-object
#   (state <s> ^name op_give1
#             -^object-info.input-link-obj)
#-->
#   (<s> ^operator <o> + > =)
#   (<o> ^name report-failure
#        ^failure-type missing-object
#        ^failure-info <handle>)
#}

# command-error: The put-down command got an error status
sp {op_give1*propose*report-failure*command-error
   (state <s> ^name op_give1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << face-command drive-command put-down-command >> }.status error)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type command-error
        ^failure-info <handle>)
}

# execution-failed: The put-down command got a failure status
sp {op_give1*propose*report-failure*execution-failed
   (state <s> ^name op_give1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << face-command drive-command put-down-command >> }.status failure)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type execution-failed
        ^failure-info <handle>)
}
