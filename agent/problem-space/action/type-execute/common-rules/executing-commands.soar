sp {problem-space*action*type-execute*common*elaborate*executing-command*true
   (state <s> ^problem-space <ps>
              ^current-action.sent-command.status << sent executing >>)
   (<ps> ^name action
         ^action.type execute)
-->
   (<s> ^executing-command true)
}

sp {problem-space*action*type-execute*common*elaborate*performed-command
   (state <s> ^problem-space <ps>
              ^current-action.sent-command (<cmd> ^status success))
   (<ps> ^name action
         ^action.type execute)
-->
   (<s> ^performed-command <cmd>)
}

sp {action*type-execute*elaborate*failure*execution*failure*status
   (state <s> ^problem-space <ps>
              ^current-action.sent-command (<cmd> ^status failure))
   (<ps> ^name action
         ^action.type execute)
-->
   (<s> ^failure <f>)
   (<f> ^type execution
        ^info.command <cmd>)
}
   