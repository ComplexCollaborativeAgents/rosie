###############################################################################
#
# Inside the execute-close1 substate, this will put a close command on the output link
#
###############################################################################

sp {op_close1*propose*send-close-command
   (state <s> ^name op_close1
             -^current-task-seg.close-command
              ^target-object <obj>)
   (<obj> ^predicates.door2 open2)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-close-command
        ^object <obj>
        ^state-predicate door2)
}

sp {op_close1*apply*send-close-command*tabletop
   (state <s> ^name op_close1
              ^operator <o>
              ^agent-params.domain tabletop
              ^current-task-seg <a>
              ^top-state.io.output-link <out>)
   (<o> ^name send-close-command
        ^object.handle <obj-handle>
        ^state-predicate <state-pred>)
-->
   (<out> ^set-state <set>)
   (<set> ^object-handle <obj-handle>
          ^name <state-pred>
          ^value closed2)
   (<a> ^close-command <set>)
}

sp {op_close1*apply*send-close-command*ai2thor
   (state <s> ^name op_close1
              ^operator <o>
              ^agent-params.domain ai2thor
              ^current-task-seg <a>
              ^top-state.io.output-link <out>)
   (<o> ^name send-close-command
        ^object.handle <obj-handle>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name close
         ^object <obj-handle>)
   (<a> ^close-command <pa>)
}
