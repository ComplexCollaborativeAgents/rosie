sp {op_explore*propose*scan
   (state <s> ^name op_explore1
             -^current-task-seg.scanned <wp-handle>
             -^until-clause
              ^top-state.world.robot.current-waypoint.handle <wp-handle>)
-->
   (<s> ^operator <scan-op> +)
   (<scan-op> ^name op_scan1 
              ^task-handle scan1)
}

sp {op_explore*propose*scan*until-clause
   (state <s> ^name op_explore1
             -^current-task-seg.scanned <wp-handle>
              ^until-clause <until>
              ^top-state.world.robot.current-waypoint.handle <wp-handle>)
-->
   (<s> ^operator <scan-op> +)
   (<scan-op> ^name op_scan1
              ^task-handle scan1
              ^until-clause <until>)
}

sp {op_explore*apply*scan*mark*scanned
   (state <s> ^name op_explore1
              ^operator <o>
              ^current-task-seg <a>
              ^child-task-seg <ca>
              ^top-state.world.robot.current-waypoint.handle <wp-handle>)
   (<ca> ^task-operator <o>
         ^status <any>)
   (<o> ^name op_scan1)
-->
   (<a> ^scanned <wp-handle>)
}
              
