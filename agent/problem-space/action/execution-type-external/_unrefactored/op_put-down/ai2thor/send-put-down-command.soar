###############################################################################
#
# Inside the execute-put-down1 substate, this will put a grab command on the output link
# If the put-down fails and needs to be re-issued, this will cause that to occur
#
###############################################################################

sp {op_put-down1*ai2thor*propose*send-put-down-command
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              -^current-task-segment.put-down-command
              ^held-object <obj>
              ^receptacle <rec>)
   (<obj> ^predicates.arm-status grabbed)
   (<rec> ^predicates.inreach inreach)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-put-down-command
        ^object <obj>
        ^receptacle <rec>)
}

sp {op_put-down1*ai2thor*apply*send-put-down-command*send
   (state <s> ^name op_put-down1
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <a>)
   (<o> ^name send-put-down-command
        ^object.handle <obj-h>
        ^receptacle.handle <rec-h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^name put-down
         ^object <obj-h>
         ^receptacle <rec-h>)
   (<a> ^put-down-command <pa>)
}
