###############################################################################
#
# Inside the execute-pick-up1 substate, this will put a grab command on the output link
# If the pick-up fails and needs to be re-issued, this will cause that to occur
#
###############################################################################

sp {op_pick-up1*ai2thor*propose*send-pick-up-command
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              -^current-task-segment.pick-up-command
              ^target-object <obj>)
   (<obj> ^predicates.inreach inreach)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-pick-up-command
        ^object <obj>)
}

sp {op_pick-up1*ai2thor*apply*send-pick-up-command*send
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^name pick-up
         ^object <h>)
   (<seg> ^pick-up-command <pa>)
}
