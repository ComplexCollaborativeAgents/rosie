###############################################################################
#
# Inside the execute-turn1 substate, this will put a turn command on the output link
#
###############################################################################

sp {op_turn1*propose*send-turn-command
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^direction-id
             -^current-task-segment.turn-command)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-turn-command)
}

sp {op_turn1*apply*send-turn-command*magicbot
   (state <s> ^name op_turn1
              ^operator.name send-turn-command
              ^agent-params.domain magicbot
              ^current-task-segment <a>
              ^top-state.io.output-link <out>
              ^top-state.perception-monitor.input-link.self.pose.yaw <yaw>
              ^direction-id.side <side>
              ^direction-id.yaw <dy>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name orient
          ^parameters.yaw (+ <yaw> <dy>)
          ^termination-condition.name stabilized)
   (<a> ^turn-command <law>)
}

sp {op_turn1*apply*send-turn-command*ai2thor
   (state <s> ^name op_turn1
              ^operator.name send-turn-command
              ^agent-params.domain ai2thor
              ^current-task-segment <a>
              ^top-state.io.output-link <out>
              ^direction-handle <dir>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name turn
         ^direction <dir>)
   (<a> ^turn-command <pa>)
}
