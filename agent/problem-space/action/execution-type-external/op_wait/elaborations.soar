sp {op_wait*elaborate*propose-wait
   (state <s> ^name op_wait1)
-->
   (<s> ^propose-wait true)
}

sp {op_wait*elaborate*after-clause
   (state <s> ^name op_wait1
              ^task-operator.after-clause <after>)
-->
   (<s> ^after-clause <after>)
}

# duration 
sp {op_wait*elaborate*duration*after-clause
   (state <s> ^name op_wait1
              ^after-clause.predicate <pred>)
   (<pred> ^type duration)
-->
   (<s> ^duration <dur>)
   (<dur> ^info <pred>)
}

sp {op_wait*elaborate*duration*seconds
   (state <s> ^name op_wait1
              ^duration <dur>)
   (<dur> ^info <info>)
   (<info> ^unit seconds
           ^number <num>)
-->
   (<dur> ^seconds <num>)
}

sp {op_wait*elaborate*duration*minutes
   (state <s> ^name op_wait1
              ^duration <dur>)
   (<dur> ^info <info>)
   (<info> ^unit minutes
           ^number <num>)
-->
   (<dur> ^seconds (* <num> 60))
}

sp {op_wait*elaborate*duration*hours
   (state <s> ^name op_wait1
              ^duration <dur>)
   (<dur> ^info <info>)
   (<info> ^unit hours
           ^number <num>)
-->
   (<dur> ^seconds (* <num> 3600))
}

sp {op_wait*elaborate*duration*end-time
   (state <s> ^name op_wait1
              ^duration <dur>
              ^current-task-segment.start-time <t>)
   (<dur> ^seconds <secs>)
-->
   (<dur> ^end-time (+ <secs> <t>))
}

sp {op_wait*elaborate*duration*satisfied
   (state <s> ^name op_wait1
              ^duration <dur>
              ^io.input-link.time.total-secs > <t>)
   (<dur> ^end-time <t>)
-->
   (<dur> ^satisfied true)
}

sp {op_wait*elaborate*desired*satisfied*duration*met
   (state <s> ^name op_wait1
              ^duration.satisfied true)
-->
   (<s> ^desired.satisfied true)
}

# clocktime
sp {op_wait*elaborate*clocktime*after-clause
   (state <s> ^name op_wait1
              ^after-clause.predicate <pred>)
   (<pred> ^type clocktime)
-->
   (<s> ^clocktime <time>)
   (<time> ^info <pred>)
}

sp {op_wait*elaborate*clocktime*satisfied
   (state <s> ^name op_wait1
              ^clocktime <time>
              ^io.input-link.time <il-time>)
   (<time> ^info <info>)
   (<info> ^hour <hour>
           ^minute <min>)
   (<il-time> ^clock-hour >= <hour>
              ^clock-min >= <min>)
-->
   (<time> ^satisfied true)
}

sp {op_wait*elaborate*desired*satisfied*clocktime*met
   (state <s> ^name op_wait1
              ^clocktime.satisfied true)
-->
   (<s> ^desired.satisfied true)
}



