sp {primitive-pick-up*fetch*apply*complete-task*add*grabbed
   (state <s> ^name primitive-pick-up
              ^agent-params.domain fetch
              ^operator <o>
              ^target-object.predicates <preds>)
   (<o> ^name complete-task
        ^status success)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -
            ^arm-status not-grabbed -
            ^arm-status grabbed)
}

sp {primitive-pick-up*fetch*apply*complete-task*failure*remove*moving
   (state <s> ^name primitive-pick-up
              ^agent-params.domain fetch
              ^operator <o>
              ^target-object.predicates <preds>)
   (<o> ^name complete-task
        ^status failure)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -)
}

sp {primitive-pick-up*fetch*apply*complete-task*move*belief*to*under*table
   (state <s> ^name primitive-pick-up
              ^agent-params.domain fetch
              ^operator <o>
              ^target-object.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
   (<o> ^name complete-task
        ^status success)
-->
   (<cmd> ^set_transform <xform>)
   (<xform> ^id <id>
            ^position <pos>)
   (<pos> ^x 0.5 ^y 0.0 ^z 0.5) # under the table (hide it!)
}
