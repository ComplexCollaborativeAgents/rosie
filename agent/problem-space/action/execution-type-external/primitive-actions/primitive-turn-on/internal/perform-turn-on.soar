sp {op_turn-on1*internal*propose*perform-turn-on
   (state <s> ^name op_turn-on1
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^target-object.predicates.activation1 off2)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-turn-on)
}

sp {op_turn-on1*internal*apply*perform-turn-on
   (state <s> ^name op_turn-on1
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^operator.name perform-turn-on
              ^object-info.input-link-obj.property <il-prop>)
   (<il-prop> ^property-handle activation1
              ^values <vals>)
-->
   (<vals> ^off2 1.00 -
           ^on2 1.00)
}

sp {op_turn-on1*internal*apply*perform-turn-on*microwave*default*time
   (state <s> ^name op_turn-on1
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^operator.name perform-turn-on
              ^target-object.predicates.name microwave1
              ^task-operator <task-op>
              ^io.input-link.time.total-secs <secs>
              ^object-info.input-link-obj.property <il-prop>)
   (<task-op> -^until-clause.<pred-num>.type duration)
   (<il-prop> ^property-handle activation1)
-->
   (<il-prop> ^turn-off-time (+ <secs> 2500))
}

sp {op_turn-on1*internal*apply*perform-turn-on*microwave*time*seconds
   (state <s> ^name op_turn-on1
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^operator.name perform-turn-on
              ^target-object.predicates.name microwave1
              ^task-operator.until-clause.<pnum> <duration>
              ^io.input-link.time.total-secs <secs>
              ^object-info.input-link-obj.property <il-prop>)
   (<il-prop> ^property-handle activation1)
   (<duration> ^type duration
               ^number <num>
               ^unit seconds)
-->
   (<il-prop> ^turn-off-time (+ <secs> (* <num> 10)))
}

sp {op_turn-on1*internal*apply*perform-turn-on*microwave*make*hot
   (state <s> ^name op_turn-on1
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^operator.name perform-turn-on
              ^target-object <mic>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^world <w>)
   (<mic> ^predicates.name microwave1)
   (<w> ^objects.object <obj>
        ^predicates.predicate <pred>)
   (<pred> ^handle in1
           ^instance <i>)
   (<i> ^1 <obj> ^2 <mic>)
   (<obj-info> ^wm-obj <obj>
               ^input-link-obj.property <il-prop>)
   (<il-prop> ^property-handle temperature
              ^values <vals>)
   (<vals> ^{ <prop> <> hot1 } 1.00)
-->
   (<vals> ^<prop> 1.00 -
           ^hot1 1.00)
}

sp {op_turn-on1*internal*prefer*attend-to-scene-change*over*perform-turn-on
   (state <s> ^name op_turn-on1
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << push-task-segment pop-task-segment attend-to-scene-change >>)
   (<o2> ^name perform-turn-on)
-->
   (<s> ^operator <o1> > <o2>)
}

