
sp {handle-invalid-argument-failure*propose*terminate-current-action
   (state <s> ^name handle-invalid-argument-failure
              ^action-segment <a>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name terminate-current-action)
}

sp {handle-invalid-argument-failure*apply*terminate-current-action*add*failure*status
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-action
              ^action-segment <a>)
-->
   (<a> ^unrecoverable-failure invalid-argument-failure)
}

sp {handle-invalid-argument-failure*apply*terminate-current-action*send*message*invalid-argument-failure*missing-argument
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-action
             -^silent true
              ^action-segment.execution-operator.task-handle <action-h>
              ^missing-argument <arg-name>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type invalid-argument-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^subtype missing-argument
        ^task-handle <action-h>
        ^missing-argument <arg-name>)
}

sp {handle-invalid-argument-failure*apply*terminate-current-action*send*message*invalid-argument-failure*missing-property
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-action
             -^silent true
              ^action-segment.execution-operator.task-handle <action-h>
              ^missing-property <prop-name>
              ^invalid-argument.id <obj>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type invalid-argument-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^subtype missing-property
        ^task-handle <action-h>
        ^object <obj>
        ^missing-property <prop-name>)
}

sp {handle-invalid-argument-failure*apply*terminate-current-action*send*message*invalid-argument-failure*default
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-action
             -^silent true
              ^action-segment.invalid-argument-operator.task-handle <action-h>
              -^missing-property
              ^argument-name <arg-name>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type invalid-argument-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^subtype default
        ^task-handle <action-h>
        ^argument-name <arg-name>)
}
