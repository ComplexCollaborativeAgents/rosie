sp {subproblem*search*elaborate*subtask-arg-info
   (state <s> ^problem-space <ps>
              ^operator <o> +)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<o> ^item-type task-operator)
-->
   (<s> ^subtask-arg-info <info>)
   (<info> ^subtask-op <o>)
}

sp {subproblem*search*elaborate*subtask-arg-info*task-object*object*arg
   (state <s> ^problem-space <ps>
              ^subtask-arg-info <info>
              ^task-objects <objs>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<info> ^subtask-op.<arg-name> <arg>)
   (<arg> ^arg-type object
          ^id <obj>)
   (<objs> ^object <obj>)
-->
   (<info> ^task-object <obj>)
}

sp {subproblem*search*elaborate*subtask-arg-info*not-task-object*object*arg
   (state <s> ^problem-space <ps>
              ^subtask-arg-info <info>
              ^task-objects <objs>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<info> ^subtask-op.<arg-name> <arg>)
   (<arg> ^arg-type object
          ^id <obj>)
   (<objs> -^object <obj>)
-->
   (<info> ^not-task-object <obj>)
}

sp {subproblem*search*elaborate*subtask-arg-info*task-object*predicate*arg
   (state <s> ^problem-space <ps>
              ^subtask-arg-info <info>
              ^task-objects <objs>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<info> ^subtask-op.<arg-name> <arg>)
   (<arg> ^arg-type predicate
          ^2 <obj>)
   (<objs> ^object <obj>)
-->
   (<info> ^task-object <obj>)
}

sp {subproblem*search*elaborate*subtask-arg-info*not-task-object*predicate*arg
   (state <s> ^problem-space <ps>
              ^subtask-arg-info <info>
              ^task-objects <objs>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<info> ^subtask-op.<arg-name> <arg>)
   (<arg> ^arg-type predicate
          ^2 <obj>)
   (<objs> -^object <obj>)
-->
   (<info> ^not-task-object <obj>)
}

sp {subproblem*search*search-level*1*reject*subtask-operator*with*not-task-object
   (state <s> ^problem-space <ps>
              ^subtask-arg-info <info>
              ^operator <o> +)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true
         ^search-level 1)
   (<info> ^subtask-op <o>
           ^not-task-object <any>)
-->
   (<s> ^operator <o> -)
}

sp {subproblem*search*search-level*2*reject*subtask-operator*with*no*task-object
   (state <s> ^problem-space <ps>
              ^subtask-arg-info <info>
              ^operator <o> +)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true
         ^search-level 2)
   (<info> ^subtask-op <o>
          -^task-object <any>)
-->
   (<s> ^operator <o> -)
}
