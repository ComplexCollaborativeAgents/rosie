sp {subproblem*search*elaborate*task-objects
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
-->
   (<s> ^task-objects <objs>)
}

sp {subproblem*search*elaborate*task-objects*object*arg*task-operator
   (state <s> ^problem-space <ps>
              ^task-operator.<arg-name> <arg>
              ^task-objects <objs>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<arg> ^arg-type object
          ^id <obj>)
-->
   (<objs> ^object <obj>)
}

sp {subproblem*search*elaborate*task-objects*predicate*arg*task-operator
   (state <s> ^problem-space <ps>
              ^task-operator.<arg-name> <arg>
              ^task-objects <objs>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<arg> ^arg-type predicate
          ^2 <obj>)
-->
   (<objs> ^object <obj>)
}

sp {subproblem*search*elaborate*task-objects*goal*predicate
   (state <s> ^problem-space <ps>
              ^task-objects <objs>
              ^desired.<gp-num> <gp>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<gp> ^item-type goal-predicate
         ^<pnum> <pobj>)
   (<pobj> ^item-type object)
-->
   (<objs> ^object <pobj>)
}

