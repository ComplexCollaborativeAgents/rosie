# Elaborate all objects that are related to the current task 
# (Either on the task operator or goal)
sp {subproblem*search*elaborate*task-objects
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
-->
   (<s> ^task-objects <objs>)
}

sp {subproblem*search*elaborate*task-object*object*arg
   (state <s> ^problem-space <ps>
              ^task-objects <objs>
              ^task-operator.<arg-name> <arg>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<arg> ^arg-type object
          ^id <obj>)
-->
   (<objs> ^object <obj>)
}

sp {subproblem*search*elaborate*task-object*predicate*arg
   (state <s> ^problem-space <ps>
              ^task-objects <objs>
              ^task-operator.<arg-name> <arg>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
   (<arg> ^arg-type partial-predicate
          ^2 <obj>)
-->
   (<objs> ^object <obj>)
}

sp {subproblem*search*elaborate*task-objects*goal*predicate
   (state <s> ^problem-space <ps>
              ^task-objects <objs>
              ^desired.<gp-num>.{ << 1 2 3 4 5 >> } <obj>)
   (<ps> ^name action
         ^subproblem search
         ^propose-subtasks true)
-->
   (<objs> ^object <obj>)
}
