####################### predicate-handle ###########################

# procedural match - the predicate-handle matches one on the reference-task-op
sp {generalize-task-operator*propose*generalize-task-argument*predicate-handle*procedural
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>
             ^reference-tcn-proc.<arg-name> <parg>
             ^reference-task-op.<arg-name>.handle <handle>)
  (<arg> ^instantiation <i>
        -^general-arg-rep)
  (<i> ^arg-type predicate-handle
       ^handle <handle>)
  (<parg> ^arg-type predicate
          ^id <pred-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type procedural
       ^id <pred-slot>)
}

sp {generalize-task-operator*apply*generalize-task-argument*predicate-handle*procedural
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type procedural
       ^id <pred-slot>)
-->
  (<arg> ^general-arg-rep <gen>)
  (<gen> ^arg-type predicate-handle
         ^id <pred-slot>)
}

# default match - copy the predicates onto a default rep
sp {generalize-task-operator*propose*generalize-task-argument*predicate-handle*default
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^instantiation <i>
        -^general-arg-rep)
  (<i> ^arg-type predicate-handle
       ^handle <handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^predicate-handle <handle>)
}

sp {generalize-task-operator*apply*generalize-task-argument*predicate-handle*default
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^predicate-handle <handle>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^arg-type predicate-handle
         ^id <pred-slot>)
  (<pred-slot> ^default <def>)
  (<def> ^predicate-handle <handle>)
}
