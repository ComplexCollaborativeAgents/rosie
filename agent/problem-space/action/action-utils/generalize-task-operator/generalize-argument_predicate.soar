####################### predicate ###########################

### Expand the predicate to generalize its sub-components

sp {generalize-task-operator*generalize-task-argument*predicate*expand*state*predicate
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation <i>)
  (<i> ^type state
       ^handle <handle>
       ^1 <obj1>)
-->
  (<s> ^generalize-task-argument <gen-obj1> <gen-pred>)

  (<gen-pred> ^instantiation <pred-i>)
  (<pred-i> ^arg-type predicate-handle
            ^handle <handle>)

  (<gen-obj1> ^instantiation <obj1-i>)
  (<obj1-i> ^arg-type object
            ^id <obj1>)

  (<arg> ^generalize-handle <gen-pred>
         ^generalize-obj1 <gen-obj1>)
}


sp {generalize-task-operator*generalize-task-argument*predicate*expand*relation*predicate
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation <i>)
  (<i> ^type relation
       ^handle <handle>
       ^1 <obj1>
       ^2 <obj2>)
-->
  (<s> ^generalize-task-argument <gen-pred> <gen-obj1> <gen-obj2>)

  (<gen-pred> ^instantiation <pred-i>)
  (<pred-i> ^arg-type predicate-handle
            ^handle <handle>)

  (<gen-obj1> ^instantiation <obj1-i>)
  (<obj1-i> ^arg-type object
            ^id <obj1>)

  (<gen-obj2> ^instantiation <obj2-i>)
  (<obj2-i> ^arg-type object
            ^id <obj2>)

  (<arg> ^generalize-handle <gen-pred>
         ^generalize-obj1 <gen-obj1>
         ^generalize-obj2 <gen-obj2>)
}

sp {generalize-task-operator*generalize-task-argument*predicate*expand*predicate*arg-type
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^instantiation <i>)
  (<i> ^arg-type predicate
       ^handle <handle>
       ^2 <obj>)
-->
  (<s> ^generalize-task-argument <gen-obj> <gen-pred>)

  (<gen-pred> ^instantiation <pred-i>)
  (<pred-i> ^arg-type predicate-handle
            ^handle <handle>)

  (<gen-obj> ^instantiation <obj-i>)
  (<obj-i> ^arg-type object
           ^id <obj>)

  (<arg> ^generalize-handle <gen-pred>
         ^generalize-obj2 <gen-obj>)
}

sp {generalize-task-operator*generalize-task-argument*predicate*elaborate*object*reference-info
   (state <s> ^name generalize-task-operator
              ^generalize-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^generalize-obj2 <gen-obj1>
          ^instantiation.reference-info <info>)
-->
   (<gen-obj1> ^reference-info <info>)
}


### When all the subparts are generalized, generalize the whole predicate

# generalize state predicate
sp {generalize-task-operator*propose*generalize-task-argument*predicate*state
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation.type state
         ^generalize-handle.general-arg-rep.id <pred-slot>
         ^generalize-obj1.general-arg-rep.id <obj1-slot>
         -^general-arg-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type state
       ^id <pred-slot>
       ^1 <obj1-slot>)
}

sp {generalize-task-operator*apply*generalize-task-argument*predicate*state
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type state
       ^id <pred-slot>
       ^1 <obj1-slot>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^type state
         ^id <pred-slot>
         ^1 <obj1-slot>)
}


# generalize relation predicate
sp {generalize-task-operator*propose*generalize-task-argument*predicate*relation
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation.type relation
         ^generalize-handle.general-arg-rep.id <pred-slot>
         ^generalize-obj1.general-arg-rep.id <obj1-slot>
         ^generalize-obj2.general-arg-rep.id <obj2-slot>
         -^general-arg-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^id <pred-slot>
       ^1 <obj1-slot>
       ^2 <obj2-slot>)
}

sp {generalize-task-operator*apply*generalize-task-argument*predicate*relation
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^id <pred-slot>
       ^1 <obj1-slot>
       ^2 <obj2-slot>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^type relation
         ^id <pred-slot>
         ^1 <obj1-slot>
         ^2 <obj2-slot>)
}


# generalize partial predicate (from task operator)
sp {generalize-task-operator*propose*generalize-task-argument*predicate*predicate
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^instantiation.arg-type predicate
         ^generalize-handle.general-arg-rep.id <pred-slot>
         ^generalize-obj2.general-arg-rep.id <obj-slot>
        -^general-arg-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type partial
       ^id <pred-slot>
       ^2 <obj-slot>)
}

sp {generalize-task-operator*apply*generalize-task-argument*predicate*procedural
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type partial
       ^id <pred-slot>
       ^2 <obj-slot>)
-->
  (<arg> ^general-arg-rep <gen>)
  (<gen> ^arg-type predicate
         ^id <pred-slot>
         ^2 <obj-slot>)
}


