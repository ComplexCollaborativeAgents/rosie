######## match object arguments #######

sp {learn-matches-action-elaboration-rule*elaborate*candidate*matches-arg*object
   (state <s> ^name learn-matches-action-elaboration-rule
              ^action-info <info>
              ^candidate <cand>)
   (<info> ^arg-name <arg-name>
           ^<arg-name> <arg>)
   (<cand> ^action <ca>)
   (<arg> ^arg-type object
          ^id <obj>)
   (<ca> ^<arg-name> <carg>)
   (<carg> ^arg-type object
           ^id <obj>)
-->
   (<cand> ^matches-arg <arg-name>)
}

sp {learn-matches-action-elaboration-rule*elaborate*candidate*matches-arg*object*2*preds
   (state <s> ^name learn-matches-action-elaboration-rule
              ^action-info <info>
              ^candidate <cand>)
   (<info> ^arg-name <arg-name>
           ^<arg-name> <arg>)
   (<cand> ^action <ca>)
   (<arg> ^arg-type object
          ^arg-status abstract
          ^id.predicates <preds1>)
   (<ca> ^<arg-name> <carg>)
   (<carg> ^arg-type object
           ^id.predicates <preds2>)
   (<preds1> ^{ <prop1> << category modifier1 >> } { <pred1>           }
             ^{ <prop2> << category modifier1 >> } { <pred2> > <pred1> }
            -^{ <prop3> << category modifier1 >> } { <pred3> > <pred2> })
   (<preds2> ^<prop1> <pred1>
             ^<prop2> <pred2>)
-->
   (<cand> ^matches-arg <arg-name>)
}
