
#### copy until-clause argument

sp {task-utils*copy-task-operator*expand*until-clause*argument*state
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type until-clause
          ^source-arg.predicate <src>)
   (<src> ^type state
          ^handle <h>
          ^1 <obj1>)
-->
   (<arg> ^pred-type state
          ^pred-handle <h>
          ^obj1-copy <obj1-copy>)

   (<ws> ^copy-argument <obj1-copy>)
   (<obj1-copy> ^arg-type object
                ^source-arg.id <obj1>)
}

sp {task-utils*copy-task-operator*expand*until-clause*argument*relation
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type until-clause
          ^source-arg.predicate <src>
         -^arg-copy)
   (<src> ^type relation
          ^handle <h>
          ^1 <obj1>
          ^2 <obj2>)
-->
   (<arg> ^pred-type relation
          ^pred-handle <h>
          ^obj1-copy <obj1-copy>
          ^obj2-copy <obj2-copy>)

   (<ws> ^copy-argument <obj1-copy> <obj2-copy>)
   (<obj1-copy> ^arg-type object
                ^source-arg.id <obj1>)
   (<obj2-copy> ^arg-type object
                ^source-arg.id <obj2>)
}

sp {task-utils*copy-task-operator*propose*copy-task-argument*until-clause*state
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type until-clause
          ^pred-type state
          ^pred-handle <h>
          ^obj1-copy.arg-copy.id <obj1-copy>
         -^arg-copy)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^workspace <ws>
        ^argument <arg>
        ^arg-type until-clause
        ^pred-type state
        ^pred-handle <h>
        ^obj1 <obj1-copy>)
}

sp {task-utils*copy-task-operator*propose*copy-task-argument*until-clause*relation
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type until-clause
          ^pred-type relation
          ^pred-handle <h>
          ^obj1-copy.arg-copy.id <obj1-copy>
          ^obj2-copy.arg-copy.id <obj2-copy>
         -^arg-copy)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^workspace <ws>
        ^argument <arg>
        ^arg-type until-clause
        ^pred-type relation
        ^pred-handle <h>
        ^obj1 <obj1-copy>
        ^obj2 <obj2-copy>)
}

sp {task-utils*copy-task-operator*apply*copy-task-argument*until-clause*state
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type until-clause
        ^pred-handle <h>
        ^pred-type state
        ^obj1 <obj1>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^arg-type until-clause
           ^predicate <new-pred>)
   (<new-pred> ^type state
               ^handle <h>
               ^1 <obj1>)
}

sp {task-utils*copy-task-operator*apply*copy-task-argument*until-clause*relation
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type until-clause
        ^pred-handle <h>
        ^pred-type relation
        ^obj1 <obj1>
        ^obj2 <obj2>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^arg-type until-clause
           ^predicate <new-pred>)
   (<new-pred> ^type relation
               ^handle <h>
               ^1 <obj1>
               ^2 <obj2>)
}
