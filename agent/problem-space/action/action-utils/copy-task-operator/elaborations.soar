sp {anystate*copy-task-operator*elaborate*performed*info
   (state <s> ^copy-task-operator <ws>)
   (<ws> ^source-task-op.performed <sub>
         ^destination <dest>
         ^copy-name <name>)
   (<dest> ^<name> <copy>)
-->
   (<copy> ^performed <sub>)
}

sp {anystate*copy-task-operator*elaborate*current-subgoal
   (state <s> ^copy-task-operator <ws>)
   (<ws> ^source-task-op.current-subgoal <sub>
         ^destination <dest>
         ^copy-name <name>)
   (<dest> ^<name> <copy>)
-->
   (<copy> ^current-subgoal <sub>)
}

sp {state*no-change*copy-task-operator*elaborate*copy-task-op-workspace
   (state <s> ^superstate.copy-task-operator <sscto>
              ^impasse no-change
              ^attribute state)
-->
   (<s> ^copy-task-op-workspace <ws>)
   (<ws> ^ss-copy-task-op <sscto>)
}

sp {task-utils*copy-task-operator*elaborate*name*learn-copy-task-op-elaboration-rule
   (state <s> ^copy-task-op-workspace <ws>)
-->
   (<s> ^name learn-copy-task-op-elaboration-rule)
}

sp {task-utils*copy-task-operator*elaborate*learning-task-utils-rule
   (state <s> ^name learn-copy-task-op-elaboration-rule
              ^copy-task-op-workspace <ws>)
   (<ws> ^copy-name <name>
         ^destination <dest>)
   (<dest> -^<name> <copy>)
-->
   (<s> ^learning-task-utils-rule true)
}

sp {copy-task-operator*op-no-change*elaborate*ss-copy-task-op*superoperator
   (state <s> ^name copy-task-operator
              ^superstate.operator <o>)
-->
   (<s> ^copy-task-op-workspace <ws>)
   (<ws> ^ss-copy-task-op <o>)
}

sp {task-utils*copy-task-operator*elaborate*info
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^ss-copy-task-op <sscto>)
   (<sscto> ^source-task-op <src-top>
           ^destination <dest>
           ^copy-name <name>)
-->
   (<ws> ^source-task-op <src-top>
         ^destination <dest>
         ^copy-name <name>)
}

sp {learn-copy-task-op-elaborate-rule*elaborate*copy-task-operator*not-copied*true
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-name <name>
        -^destination.<name>)
-->
   (<ws> ^not-copied true)
}

sp {task-utils*copy-task-operator*elaborate*object-map*superoperator
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^ss-copy-task-op <sscto>)
   (<sscto> ^object-map <map>)
-->
   (<ws> ^object-map <map>)
}

sp {task-utils*copy-task-operator*elaborate*object-map*none
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^ss-copy-task-op <sscto>)
   (<sscto> -^object-map <map>)
-->
   (<ws> ^no-object-map true)
}

# elaborate argument information 
## ENUMERATE: 0-5 arguments
sp {task-utils*copy-task-operator*elaborate*no-args
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^source-task-op <taskop>)
   (<src-top> -^{ << arg1 arg2 arg3 arg4 arg5 until-clause >> } <arg>)
-->
   (<ws> ^num-args 0)
}

sp {task-utils*copy-task-operator*elaborate*arguments*1*arg
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^source-task-op <src-top>)
   (<src-top> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
             -^{ <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>)
   (<a1> ^arg-type <t1>)
--> 
   (<ws> ^num-args 1
        ^copy-argument <ca1>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
}

sp {task-utils*copy-task-operator*elaborate*arguments*2*args
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^source-task-op <src-top>)
   (<src-top> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
              ^{ <n2> > <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
             -^{ <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
-->
   (<ws> ^num-args 2
        ^copy-argument <ca1> <ca2>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
}

sp {task-utils*copy-task-operator*elaborate*arguments*3*args
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^source-task-op <src-top>)
   (<src-top> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
              ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
              ^{ <n3> > <n2>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
             -^{ <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
-->
   (<ws> ^num-args 3
        ^copy-argument <ca1> <ca2> <ca3>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
}

sp {task-utils*copy-task-operator*elaborate*arguments*4*args
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^source-task-op <src-top>)
   (<src-top> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
              ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
              ^{ <n3> > <n2> < <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
              ^{ <n4> > <n3>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
             -^{ <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
-->
   (<ws> ^num-args 4
        ^copy-argument <ca1> <ca2> <ca3> <ca4>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
   (<ca4> ^arg-type <t4>
          ^arg-name <n4>
          ^source-arg <a4>)
}


sp {task-utils*copy-task-operator*elaborate*arguments*5*args
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^source-task-op <src-top>)
   (<src-top> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
              ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
              ^{ <n3> > <n2> < <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
              ^{ <n4> > <n3> < <n5> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
              ^{ <n5> > <n4>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>
             -^{ <n6> <> <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a6>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
   (<a5> ^arg-type <t5>)
-->
   (<ws> ^num-args 5
        ^copy-argument <ca1> <ca2> <ca3> <ca4> <ca5>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
   (<ca4> ^arg-type <t4>
          ^arg-name <n4>
          ^source-arg <a4>)
   (<ca5> ^arg-type <t5>
          ^arg-name <n5>
          ^source-arg <a5>)
}
