####################################################
# predicate arguments

sp {task-utils*copy-task-operator*expand*predicate*argument*copy*obj2
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>)
   (<src> ^handle <h>
          ^2 <obj2>)
-->
   (<arg> ^obj2-copy <obj2-copy>
          ^pred-handle <h>)
   (<ws> ^copy-argument <obj2-copy>)
   (<obj2-copy> ^arg-type object
                ^source-arg.id <obj2>)
}

sp {task-utils*copy-task-operator*propose*copy-task-argument*predicate
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type predicate
          ^pred-handle <h>
          ^obj2-copy.arg-copy.id <obj2-copy>
         -^arg-copy)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^workspace <ws>
        ^argument <arg>
        ^arg-type predicate
        ^pred-handle <h>
        ^obj2 <obj2-copy>)
}

sp {task-utils*copy-task-operator*apply*copy-task-argument*predicate
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-handle <h>
        ^obj2 <obj2>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^arg-type predicate
           ^handle <h>
           ^2 <obj2>)
}
