###############################################################
#####                      concept                        #####

sp {instantiate-task-operator*propose*instantiate-task-argument*concept*matches*procedural
   (state <s> ^name instantiate-task-operator
              ^instantiate-task-argument <arg>
              ^reference-tcn-proc.<arg-name> <parg>
              ^reference-task-op.<arg-name> <op-arg>)
   (<arg> ^general-arg-rep <gen>
         -^instantiation)
   (<gen> ^arg-type concept
          ^id <conc-slot>)
   (<parg> ^arg-type concept
           ^id <conc-slot>)
   (<op-arg> ^arg-type concept
             ^handle <conc-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^handle <conc-handle>)
}

sp {instantiate-task-operator*propose*instantiate-task-argument*concept*matches*default
   (state <s> ^name instantiate-task-operator
              ^instantiate-task-argument <arg>)
   (<arg> ^general-arg-rep <gen>
         -^instantiation)
   (<gen> ^arg-type concept
          ^id.default.predicate-handle <conc-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^handle <conc-handle>)
}

sp {instantiate-task-operator*apply*instantiate-task-argument*concept*create*instantiation
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^arg-type concept
       ^argument <arg>
       ^handle <conc-handle>)
-->
  (<arg> ^instantiation <i>)
  (<i> ^arg-type concept
       ^handle <conc-handle>)
}
