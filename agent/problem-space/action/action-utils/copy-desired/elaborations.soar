sp {state*no-change*copy-desired*elaborate*copy-desired-workspace
   (state <s> ^superstate.copy-desired <des>
              ^impasse no-change
              ^attribute state)
-->
   (<s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
}

# NOTE: Don't test on this wme with any rules that will be chunked
# (If there are multiple workspaces, there will be problems)
# Instead, test copy-desired-workspace directly
sp {action-utils*copy-desired*elaborate*name*learn-copy-desired-elaboration-rule
   (state <s> ^copy-desired-workspace <ws>)
-->
   (<s> ^name learn-copy-desired-elaboration-rule)
}


sp {learn-copy-desired-elaborate-rule*elaborate*copy-desired*was-copied*true
   (state <s> ^copy-desired-workspace <ws>
              ^superstate.desired.duplicate-of <des>)
   (<ws> ^source-desired <des>)
-->
   (<ws> ^was-copied true)
}

sp {action-utils*copy-desired*elaborate*object-map*superoperator
   (state <s> ^copy-desired-workspace <ws>
              ^superstate.deep-copy-world.copy-objects-map <map>)
-->
   (<ws> ^object-map <map>)
}

sp {action-utils*copy-desired*elaborate*learning-action-utils-rule
   (state <s> ^name learn-copy-desired-elaboration-rule
              ^copy-desired-workspace <ws>)
   (<ws> -^was-copied true)
-->
   (<s> ^learning-action-utils-rule true)
}

# Elaborate subgoal information
sp {action-utils*copy-desired*elaborate*copy-desired-workspace*pred-count
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <src-des>)
   (<src-des> ^pred-count <c>)
-->
   (<ws> ^pred-count <c>)
}

sp {action-utils*copy-desired*elaborate*copy-predicate
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <src-des>)
   (<src-des> ^<pred-num> <src-pred>)
   (<src-pred> ^item-type goal-predicate)
-->
   (<ws> ^copy-predicate <pred>)
   (<pred> ^num <pred-num> ^source-pred <src-pred>)
}

## elaborate individual predicate information 
#sp {action-utils*copy-desired*elaborate*copy-subgoal*pred-count
#   (state <s> ^copy-desired-workspace.copy-subgoal <sub>)
#   (<sub> ^source-subgoal.pred-count <c>)
#-->
#   (<sub> ^pred-count <c>)
#}
#
#sp {action-utils*copy-desired*elaborate*copy-subgoal*copy-predicate
#   (state <s> ^copy-desired-workspace.copy-subgoal <sub>)
#   (<sub> ^source-subgoal <src-sub>)
#   (<src-sub> ^<pred-num> <src-pred>)
#   (<src-pred> ^item-type goal-predicate)
#-->
#   (<sub> ^copy-predicate <pred>)
#   (<pred> ^num <pred-num>
#           ^source-pred <src-pred>)
#}

