#################### generalize-argument object ########################
# Goes from a specific object instance to creating a generalized representation

# match-type procedural - the object matches one on the reference-action
sp {generalize-action*propose*generalize-argument*object*procedural*level1
  (state <s> ^name generalize-action
             ^generalize-argument <arg>
             ^reference-id.<arg-name> <parg>
             ^reference-action.<arg-name>.id <obj>)
  (<arg> ^instance <i>
        -^generalized-rep)
  (<i> ^arg-type object
       ^id <obj>)
  (<parg> ^arg-type object
          ^id <obj-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type procedural
       ^id <obj-slot>)
}

sp {generalize-action*propose*generalize-argument*object*procedural*level2
  (state <s> ^name generalize-action
             ^generalize-argument <arg>
             ^reference-id.<arg-name> <parg>
             ^reference-action.<arg-name>.{2} <obj>)
  (<arg> ^instance <i>
        -^generalized-rep)
  (<i> ^arg-type object
       ^id <obj>)
  (<parg> ^arg-type predicate
          ^2 <obj-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type procedural
       ^id <obj-slot>)
}

sp {generalize-action*apply*generalize-argument*object*procedural
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type procedural
       ^id <obj-slot>)
-->
  (<arg> ^generalized-rep <gen>)
  (<gen> ^arg-type object
         ^id <obj-slot>)
}

# match-type default - copy the predicates onto a default rep
sp {generalize-action*propose*generalize-argument*object*default
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^instance <i>
        -^generalized-rep)
  (<i> ^arg-type object
       ^id <obj>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^instance <obj>
       ^predicates <preds>)
}

sp {generalize-action*elaborate*generalize-argument*object*default*predicates*sentence
  (state <s> ^name generalize-action
             ^source-id.handle << say1 ask1 >>
             ^operator <o> +)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^instance.predicates <preds>
       ^predicates <def-preds>)
  (<preds> ^sentence <sent>)
-->
  (<def-preds> ^sentence <sent>)
}

sp {generalize-action*elaborate*generalize-argument*object*default*predicates
  (state <s> ^name generalize-action
             ^operator <o> +)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^instance.predicates <preds>
       ^predicates <def-preds>)
  (<preds> ^{ <prop-handle> << modifier1 color shape size category property spatial-shape >> } <pred-handle>)
  #(<preds> ^{ <prop-handle> << modifier1 color shape size category property spatial-shape >> } <pred-handle>)
-->
  (<def-preds> ^<prop-handle> <pred-handle>)
}

# ENUMERATE: 1-5 predicates

sp {generalize-action*apply*generalize-argument*object*default*1*predicate
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^predicates <def-preds>)
  (<def-preds> ^{ <prop1>             } <pred1>
               -^{ <prop2> <> <prop1> } <pred2>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^arg-type object
         ^id <obj-slot>)
  (<obj-slot> ^default <def>)
  (<def> ^<prop1> <pred1>)
}

sp {generalize-action*apply*generalize-argument*object*default*2*predicates
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^predicates <def-preds>)
  (<def-preds> ^{ <prop1>           } <pred1>
               ^{ <prop2> > <prop1> } <pred2>
               -^{ <prop3> <> <prop2> <> <prop1> } <pred3>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^arg-type object
         ^id <obj-slot>)
  (<obj-slot> ^default <def>)
  (<def> ^<prop1> <pred1>
         ^<prop2> <pred2>)
}

sp {generalize-action*apply*generalize-argument*object*default*3*predicates
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^predicates <def-preds>)
  (<def-preds> ^{ <prop1>           } <pred1>
               ^{ <prop2> > <prop1> } <pred2>
               ^{ <prop3> > <prop2> } <pred3>
               -^{ <prop4> <> <prop3> <> <prop2> <> <prop1> } <pred4>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^arg-type object
         ^id <obj-slot>)
  (<obj-slot> ^default <def>)
  (<def> ^<prop1> <pred1>
         ^<prop2> <pred2>
         ^<prop3> <pred3>)
}

sp {generalize-action*apply*generalize-argument*object*default*4*predicates
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^predicates <def-preds>)
  (<def-preds> ^{ <prop1>           } <pred1>
               ^{ <prop2> > <prop1> } <pred2>
               ^{ <prop3> > <prop2> } <pred3>
               ^{ <prop4> > <prop3> } <pred4>
               -^{ <prop5> <> <prop4> <> <prop3> <> <prop2> <> <prop1> } <pred5>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^arg-type object
         ^id <obj-slot>)
  (<obj-slot> ^default <def>)
  (<def> ^<prop1> <pred1>
         ^<prop2> <pred2>
         ^<prop3> <pred3>
         ^<prop4> <pred4>)
}

sp {generalize-action*apply*generalize-argument*object*default*5*predicates
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type object
       ^match-type default
       ^predicates <def-preds>)
  (<def-preds> ^{ <prop1>           } <pred1>
               ^{ <prop2> > <prop1> } <pred2>
               ^{ <prop3> > <prop2> } <pred3>
               ^{ <prop4> > <prop3> } <pred4>
               ^{ <prop5> > <prop4> } <pred5>
               -^{ <prop6> <> <prop5> <> <prop4> <> <prop3> <> <prop2> <> <prop1> } <pred6>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^arg-type object
         ^id <obj-slot>)
  (<obj-slot> ^default <def>)
  (<def> ^<prop1> <pred1>
         ^<prop2> <pred2>
         ^<prop3> <pred3>
         ^<prop4> <pred4>
         ^<prop5> <pred5>)
}

