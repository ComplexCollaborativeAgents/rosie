####################### predicate-set ###########################

sp {generalize-action*generalize-argument*expand*predicate-set
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^instance.predicate <pred>)
-->
  (<s> ^generalize-argument <gen-pred>)
  (<gen-pred> ^arg-type predicate
              ^instance <pred>)
  (<arg> ^generalize-pred <gen-pred>)
}

sp {generalize-action*elaborate*generalize-argument*predicate-set*ungeneralized-pred
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^generalize-pred <gen-pred>)
  (<gen-pred> -^generalized-rep)
-->
  (<arg> ^ungeneralized-preds true)
}

sp {generalize-action*propose*generalize-argument*predicate-set
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate-set
         -^generalized-rep
         -^ungeneralized-preds true)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-set)
}

# ENUMERATE: 1-3 predicates

sp {generalize-action*apply*generalize-argument*predicate-set*1
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-set)
  (<arg> ^generalize-pred { <pred1> }
         -^generalize-pred { <pred2> <> <pred1> })
  (<pred1> ^generalized-rep <rep1>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^predicate <rep1>)
}

sp {generalize-action*apply*generalize-argument*predicate-set*2
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-set)
  (<arg> ^generalize-pred { <pred1>           }
         ^generalize-pred { <pred2> > <pred1> }
         -^generalize-pred { <pred3> <> <pred2> <> <pred1> })
  (<pred1> ^generalized-rep <rep1>)
  (<pred2> ^generalized-rep <rep2>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^predicate <rep1> <rep2>)
}

sp {generalize-action*apply*generalize-argument*predicate-set*3
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate-set)
  (<arg> ^generalize-pred { <pred1>           }
         ^generalize-pred { <pred2> > <pred1> }
         ^generalize-pred { <pred3> > <pred2> }
         -^generalize-pred { <pred4> <> <pred3> <> <pred2> <> <pred1> })
  (<pred1> ^generalized-rep <rep1>)
  (<pred2> ^generalized-rep <rep2>)
  (<pred3> ^generalized-rep <rep3>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^predicate <rep1> <rep2> <rep3>)
}
             
