# Check if the subtask being pushed matches one we've already learned
sp {subproblem*execute*elaborate*match-task-operator*pushed-task-operator
   (state <s> ^problem-space.subproblem execute
              ^task-stack <stack>
              ^current-task-segment <seg>
              ^task-operator <task-op>)
   (<stack> ^bottom <seg>
            ^push-task-operator <subtask>)
   (<subtask> ^task-source << instruction search >>)
-->
   (<s> ^match-task-operator <mto>)
   (<mto> ^type pushed-subtask
          ^task-operator <subtask>
          ^candidate-tasks <cands>)
}

sp {subproblem*execute*elaborate*match-task-operator*candidate-task-operators
   (state <s> ^problem-space.subproblem execute
              ^match-task-operator <mto>
              ^operator <o> +)
   (<mto> ^type pushed-subtask
          ^candidate-tasks <cands>)
   (<o> ^item-type task-operator)
-->
   (<cands> ^candidate-task-operator <o>)
}

# If the subtask doesn't match an existing task-operator, 
#   add it to the TCN
sp {subproblem*execute*propose*add-subtask-to-tcn*task-source
   (state <s> ^problem-space.subproblem execute
              ^task-stack <stack>
              ^current-task-segment <seg>
              ^task-operator <task-op>
              ^match-task-operator <mto>)
   (<mto> ^type pushed-subtask
          ^task-operator <subtask>
          -^successful-match)
   (<stack> ^bottom <seg>
            ^push-task-operator <subtask>)
   (<subtask> ^task-source << instruction search >>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name add-subtask-to-tcn
        ^subtask <subtask>
        ^task-operator <task-op>)
}

# Once the created-subtask <sub-handle> has been added to the state, 
#   Reset the state and get mark that we need to learn-subtask-proposal <sub-handle>

sp {subproblem*execute*apply*add-subtask-to-tcn*finished*remove*task-source
   (state <s> ^problem-space.subproblem execute
              ^operator <o>
              ^current-task-segment <seg>
              ^created-subtask <sub-h>
              ^task-concept-network <tcn>)
   (<o> ^name add-subtask-to-tcn
        ^subtask <subtask>)
   (<subtask> ^task-source <task-src>)
-->
   (<subtask> ^subtask-handle <sub-h>
              ^task-source <task-src> -)
   (<s> ^created-subtask <sub-h> -)
   (<seg> ^assigned-subtask-handle true
          ^learn-subtask-proposal <sub-h>)
   (<s> ^task-concept-network <tcn> -) # We want to re-retrieve this because it changed in the substate
}
    
