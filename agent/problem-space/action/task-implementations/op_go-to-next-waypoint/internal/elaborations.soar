sp {op_go-to-next-waypoint1*elaborate*waypoint
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^task-operator.arg1 <arg1>)
   (<arg1> ^arg-type waypoint
           ^id <wp>)
-->
   (<s> ^waypoint <wp>)
}

sp {op_go-to-next-waypoint*elaborate*internal-world*top-state
   (state <s> ^name op_go-to-next-waypoint1
              ^top-state.internal-world <wi>)
-->
   (<s> ^internal-world <wi>)
}

sp {op_go-to-next-waypoint*elaborate*new-location
   (state <s> ^name op_go-to-next-waypoint1
              ^retrieved-location.handle <h>
              ^top-state.world.objects.object <loc>)
   (<loc> ^handle <h>)
-->
   (<s> ^new-location <loc>)
}

sp {op_go-to-next-waypoint1*elaborate*old-waypoint
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^waypoint <wp>
              ^top-state.world.robot.current-waypoint { <old-wp> <> <wp> })
-->
   (<s> ^old-waypoint <old-wp>)
}

sp {op_go-to-next-waypoint1*elaborate*old-location
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^waypoint.handle <wph>
              ^top-state.world.robot.current-location <old-loc>)
   (<old-loc> ^waypoint <> <wph>)
-->
   (<s> ^old-location <old-loc>)
}

sp {op_go-to-next-waypoint1*elaborate*old-object
   (state <s> ^name op_go-to-next-waypoint1
              ^old-waypoint.handle <wph>
              ^internal-world.locations.<objh> <wph>
              ^top-state.world.objects <objs>)
   (<objs> ^object <obj>)
   (<obj> ^handle <objh>
         -^task-object true
         -^predicates.is-grabbed1 grabbed1)
-->
   (<s> ^old-object <obj>)
}

#### CUSTOM GOAL HANDLING ####

sp {op_go-to-next-waypoint1*elaborate*desired
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^task-operator.current-goal go-to-next-waypoint1goal1
             -^copy-task-goal
              ^waypoint <wp>)
-->
   (<s> ^desired <des>)
   (<des> ^handle go-to-next-waypoint1goal1 ^pred-count 1 ^1 <p1>)
   (<p1> ^type status ^name current-waypoint ^1 <wp>)
}

