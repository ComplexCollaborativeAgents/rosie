
sp {primitive-turn-on*ai2thor*propose*send-turn-on-command
   (state <s> ^name primitive-turn-on
              ^agent-params.domain ai2thor
              ^problem-space.execution-type external
             -^current-task-segment.turn-on-command
              ^target-object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-turn-on-command
        ^object <obj>)
}

sp {primitive-turn-on*ai2thor*apply*send-turn-on-command*microwave*duration*secs
   (state <s> ^name primitive-turn-on
              ^agent-params.domain ai2thor
              ^operator <o>
              ^io.output-link <ol>
              ^task-operator.until-clause.<pnum> <dur>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-on-command
        ^object <obj>)
   (<obj> ^handle <h>
          ^predicates.name microwave1)
   (<dur> ^type duration
          ^number <num>
          ^unit seconds)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^name set-timer
         ^object <h>
         ^time <num>)
   (<seg> ^turn-on-command <pa>)
}

sp {primitive-turn-on*ai2thor*apply*send-turn-on-command*microwave*duration*min
   (state <s> ^name primitive-turn-on
              ^agent-params.domain ai2thor
              ^operator <o>
              ^io.output-link <ol>
              ^task-operator.until-clause.<pnum> <dur>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-on-command
        ^object <obj>)
   (<obj> ^handle <h>
          ^predicates.name microwave1)
   (<dur> ^type duration
          ^number <num>
          ^unit minutes)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^name set-timer
         ^object <h>
         ^time (* <num> 60))
   (<seg> ^turn-on-command <pa>)
}

sp {primitive-turn-on*ai2thor*apply*send-turn-on-command*microwave*default*secs
   (state <s> ^name primitive-turn-on
              ^agent-params.domain ai2thor
              ^operator <o>
              ^io.output-link <ol>
              ^task-operator <task-op>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-on-command
        ^object <obj>)
   (<obj> ^handle <h>
          ^predicates.name microwave1)
   (<task-op> -^until-clause.<pnum>.type duration)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^name set-timer
         ^object <h>
         ^time 30)
   (<seg> ^turn-on-command <pa>)
}

sp {primitive-turn-on*ai2thor*elaborate*propose-wait*true
   (state <s> ^name primitive-turn-on
              ^agent-params.domain ai2thor
              ^problem-space.execution-type external)
-->
   (<s> ^propose-wait true)
}
