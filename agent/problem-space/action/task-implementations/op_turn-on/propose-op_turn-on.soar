### propose op_turn-on1 during task planning ###

# propose if off2(arg1) 
sp {problem-space*action*propose-subtasks*propose*op_turn-on1
   (state <s> ^problem-space <ps>
              ^world <w>
             -^agent-params.strict-preconditions true)
   (<ps> ^name action
         ^propose-subtasks true)
   (<w> ^robot.arm.holding-object false
        ^objects.object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-confirmed1 confirmed1
            ^affordance1 activatable1
#          -^category lightswitch1
            ^is-activated1 not-activated1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_turn-on1 
         ^item-type task-operator
         ^task-handle turn-on1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

sp {problem-space*action*propose-subtasks*propose*op_turn-on1*strict-preconditions
   (state <s> ^problem-space <ps>
              ^world <w>
              ^agent-params.strict-preconditions true)
   (<ps> ^name action
         ^propose-subtasks true)
   (<w> ^robot.arm.holding-object false
        ^objects.object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^affordance1 activatable1
            ^is-activated1 not-activated1
            ^is-visible1 visible1
            ^is-reachable1 reachable1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_turn-on1 
         ^item-type task-operator
         ^task-handle turn-on1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}
