sp {execute*op_turn-on1*propose*simulate-turn-on-command
   (state <s> ^name op_turn-on1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^turn-on-command <cmd>)
   (<cmd> ^name << turn-on set-timer >>
         -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-turn-on-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {execute*op_turn-on1*apply*simulate-turn-on-command*mark*command*complete
   (state <s> ^name op_turn-on1
              ^operator <o>)
   (<o> ^name simulate-turn-on-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {execute*op_turn-on1*apply*simulate-turn-on-command*change*activation
   (state <s> ^name op_turn-on1
              ^operator.name simulate-turn-on-command
              ^target-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^property-handle activation1
              ^values <vals>)
-->
   (<vals> ^off2 1.00 -
           ^on2 1.00)
}

sp {execute*op_turn-on1*apply*simulate-turn-on-command*set-timer
   (state <s> ^name op_turn-on1
              ^operator <o>
              ^target-object <obj>
              ^io.input-link.time.total-secs <secs>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name simulate-turn-on-command
        ^command <cmd>)
   (<cmd> ^name set-timer
          ^time <t>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^property-handle activation1)
-->
   (<il-prop> ^turn-off-time (+ <secs> (* <t> 10)))
}

sp {execute*op_turn-on1*apply*simulate-turn-on*microwave*make*hot
   (state <s> ^name op_turn-on1
              ^operator.name simulate-turn-on
              ^target-object <mic>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^world <w>)
   (<mic> ^predicates.name microwave1)
   (<w> ^objects.object <obj>
        ^predicates.predicate <pred>)
   (<pred> ^handle in1
           ^instance <i>)
   (<i> ^1 <obj> ^2 <mic>)
   (<obj-info> ^wm-obj <obj>
               ^input-link-obj.property <il-prop>)
   (<il-prop> ^property-handle temperature
              ^values <vals>)
   (<vals> ^{ <prop> <> hot1 } 1.00)
-->
   (<vals> ^<prop> 1.00 -
           ^hot1 1.00)
}


