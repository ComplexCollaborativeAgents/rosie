# If we needed to construct a new world object for the answered object, 
#    Add it to the world via manage-world-state/change-world-state

sp {op_ask*propose*change-world-state*add*constructed-world-object
   (state <s> ^name op_ask1
              ^constructed-world-object <obj>
             -^answered-world-object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name change-world-state
        ^add-object-to-world.object <obj>)
}

# Add a link to the created object on the imagined object answer
sp {op_ask*apply*change-world-state*copy*world-object*onto*imagined-object
   (state <s> ^name op_ask1
              ^operator <o>
              ^current-task-segment.answer { <ans> <> <obj> })
   (<ans> ^predicates <preds>)
   (<o> ^name change-world-state
        ^add-object-to-world.object <obj>)
-->
   (<ans> ^world-object <obj>)
}

# Add the created object onto the task-stack's list of task objects
sp {op_ask*apply*change-world-state*add*world-object*task-object*list
   (state <s> ^name op_ask1
              ^operator <o>
              ^task-stack.task-objects <objs>)
   (<o> ^name change-world-state
        ^add-object-to-world.object <obj>)
-->
   (<objs> ^object <obj>)
}


