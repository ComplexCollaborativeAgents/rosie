### Internally modeling the view task ###

sp {problem-space*action*internal*elaborate*op_view1*dont-learn-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_view1)
-->
   (<o> ^dont-learn-action-model true)
}

# Change to visible
sp {problem-space*action*internal*apply*op_view1*change*visible
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <op>)
   (<op> ^name op_view1
         ^arg1.id.predicates <preds>)
   (<preds> ^is-visible1 not-visible1)

-->
   (<preds> ^is-visible1 not-visible1 -
            ^is-visible1 visible1)
}

#### Change other objects to not-visible

sp {problem-space*action*internal*apply*op_view1*make*others*not-visible
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^agent-params.extra-action-models true
              ^operator <op>
              ^world.objects.object <obj>)
   (<op> ^name op_view1
         ^arg1.id { <target> <> <obj> })
   (<obj> ^predicates <preds>)
   (<preds> ^category << object person >>
           -^affordance1 grabbable1
            ^is-visible1 visible1)
-->
   (<preds> ^is-visible1 not-visible1
            ^is-visible1 visible1 -)
}

# Only make a grabbable object not-visible if it is not currently grabbed
sp {problem-space*action*internal*apply*op_view1*make*grabbable*others*not-visible
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^agent-params.extra-action-models true
              ^operator <op>
              ^world.objects.object <obj>)
   (<op> ^name op_view1
         ^arg1.id { <target> <> <obj> })
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^affordance1 grabbable1
            ^is-grabbed1 not-grabbed1)
-->
   (<preds> ^is-visible1 not-visible1
            ^is-visible1 visible1 -)
}

#### Change other objects to not reachable

sp {problem-space*action*internal*apply*op_view1*make*non-grabbable*not-reachable
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^agent-params.extra-action-models true
              ^operator <op>
              ^world.objects.object <obj>)
   (<op> ^name op_view1
         ^arg1.id { <target> <> <obj> })
   (<obj> ^predicates <preds>)
   (<preds> ^is-reachable1 reachable1
           -^affordance1 grabbable1)
-->
   (<preds> ^is-reachable1 not-reachable1
            ^is-reachable1 reachable1 -)
}

sp {problem-space*action*internal*apply*op_view1*make*grabbable*not-reachable
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^agent-params.extra-action-models true
              ^operator <op>
              ^world.objects.object <obj>)
   (<op> ^name op_view1
         ^arg1.id { <target> <> <obj> })
   (<obj> ^predicates <preds>)
   (<preds> ^is-reachable1 reachable1
            ^affordance1 grabbable1
           -^is-grabbed1 grabbed1)
-->
   (<preds> ^is-reachable1 not-reachable1
            ^is-reachable1 reachable1 -)
}
