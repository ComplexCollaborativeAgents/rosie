### For the internal environment, simulate the turn-off command on the top-state world ###

sp {op_turn-off1*propose*simulate-turn-off-command
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^turn-off-command <cmd>)
   (<cmd> -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-turn-off-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_turn-off1*apply*simulate-turn-off-command*mark*command*complete
   (state <s> ^name op_turn-off1
              ^operator <o>)
   (<o> ^name simulate-turn-off-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}


### simulate-perception = false, change on the object directly ###

sp {op_turn-off1*apply*simulate-turn-off-command*turn*off
   (state <s> ^name op_turn-off1
              ^operator <o>
             -^agent-params.simulate-perception true)
   (<o> ^name simulate-turn-off-command
        ^command.object.predicates <preds>)
   (<preds> ^<prop> on2)
-->
   (<preds> ^<prop> on2 -
            ^<prop> off2)
}


### simulate-perception = true, change on the perception-monitor ###

sp {op_turn-off1*apply*simulate-turn-off-command*simulate-perception*turn*off
   (state <s> ^name op_turn-off1
              ^operator <o>
              ^agent-params.simulate-perception true
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name simulate-turn-off-command
        ^command.object <obj>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^world-obj <obj>)
   (<il-prop> ^predicate-handle on2)

-->
   (<il-prop> ^predicate-handle on2 -
              ^predicate-handle off2)
}


