sp {op_turn-off1*propose*send-turn-off-command
   (state <s> ^name op_turn-off1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^activation1 on2)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-turn-off-command
        ^item-type command-operator
        ^object <obj>)
}

sp {op_turn-off1*reject*send-turn-off-command*already*sent
   (state <s> ^name op_turn-off1
              ^operator <o> +
              ^current-task-segment.turn-off-command)
   (<o> ^name send-turn-off-command)
-->
   (<s> ^operator <o> -)
}

### model send-turn-off-command
sp {op_turn-off1*internal*apply*send-turn-off-command*change*activation
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-turn-off-command
        ^object.predicates <preds>)
   (<preds> ^activation1 on2)
-->
   (<preds> ^activation1 on2 -
            ^activation1 off2)
}

### ai2thor and internal
sp {op_turn-off1*external*apply*send-turn-off-command*ai2thor*internal
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain << ai2thor internal >>
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-off-command
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name turn-off
         ^object <h>)
   (<seg> ^turn-off-command <pa>)
}
