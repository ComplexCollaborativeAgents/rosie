### Send the low-level turn-off command when the target is visible and reachable ###

sp {op_turn-off1*propose*send-turn-off-command
   (state <s> ^name op_turn-off1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^<prop> on2)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-turn-off-command
        ^item-type command-operator
        ^command-name turn-off-command
        ^arg1.id <obj>
        ^property <prop>)
}

### ai2thor and internal ###

sp {op_turn-off1*external*apply*send-turn-off-command*ai2thor*internal
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain << ai2thor internal >>
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-off-command
        ^command-name <cmd-name>
        ^arg1.id.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name turn-off
         ^object <h>)
   (<seg> ^<cmd-name> <pa>)
}


### tabletop ###

sp {op_turn-off1*external*apply*send-turn-off-command*tabletop
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain tabletop
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-off-command
        ^command-name <cmd-name>
        ^arg1.id.handle <h>
        ^property <prop>)
-->
   (<ol> ^set-state <set>)
   (<set> ^object-handle <h>
          ^name <prop>
          ^value off2)
   (<seg> ^<cmd-name> <set>)
}
