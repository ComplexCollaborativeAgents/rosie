### For the internal environment, simulate the open command on the top-state world ###

sp {op_open1*propose*simulate-open-command
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^open-command <cmd>)
   (<cmd> -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-open-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_open1*apply*simulate-open-command*mark*command*complete
   (state <s> ^name op_open1
              ^operator <o>)
   (<o> ^name simulate-open-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}


### simulate-perception = false, change on the object directly ###

sp {op_open1*apply*simulate-open-command*add*open*predicate
   (state <s> ^name op_open1
              ^operator.name simulate-open-command
             -^agent-params.simulate-perception true
              ^target-object.predicates <preds>)
   (<preds> ^<prop> closed2)
-->
   (<preds> ^<prop> closed2 -
            ^<prop> open2)
}


### simulate-perception = true, change on the perception-monitor ###

sp {op_open1*apply*simulate-open-command*simulate-perception*change*predicate*to*open
   (state <s> ^name op_open1
              ^operator.name simulate-open-command
              ^agent-params.simulate-perception true
              ^target-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^values <vals>)
   (<vals> ^closed2 1.00)
-->
   (<vals> ^closed2 1.00 -
           ^open2 1.00)
}

