sp {execute*op_open1*propose*simulate-open-command
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^open-command <cmd>)
   (<cmd> ^name << open set-timer >>
         -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-open-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {execute*op_open1*apply*simulate-open-command*mark*command*complete
   (state <s> ^name op_open1
              ^operator <o>)
   (<o> ^name simulate-open-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {execute*op_open1*apply*simulate-open-command*change*door2
   (state <s> ^name op_open1
              ^operator.name simulate-open-command
              ^target-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^property-handle door2
              ^values <vals>)
-->
   (<vals> ^closed2 1.00 -
           ^open2 1.00)
}


