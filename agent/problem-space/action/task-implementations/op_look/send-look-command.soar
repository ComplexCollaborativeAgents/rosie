
sp {op_look1*propose*send-look-command
   (state <s> ^name op_look1
              ^problem-space.execution-type external)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-look-command
        ^item-type command-operator)
}

sp {op_look1*reject*send-look-command*already*sent
   (state <s> ^name op_look1
              ^operator <o> +
              ^current-task-segment.look-command)
   (<o> ^name send-look-command)
-->
   (<s> ^operator <o> -)
}

### Domain = ai2thor
sp {op_look1*apply*send-look-command*ai2thor
   (state <s> ^name op_look1
              ^operator.name send-look-command
              ^agent-params.domain ai2thor
              ^current-task-segment <seg>
              ^direction <dir>
              ^top-state.io.output-link <out>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name look
         ^direction <dir>)
   (<seg> ^look-command <pa>)
}

### Domain = cozmo
sp {op_look1*apply*send-look-command*cozmo*up
   (state <s> ^name op_look1
              ^operator.name send-look-command
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^direction up1
              ^top-state.io <io>)
   (<io> ^input-link.self.head-angle <angle>
         ^output-link <out>)
-->
   (<out> ^move-head <mh>)
   (<mh> ^item-type output-command
         ^angle (+ <angle> 0.4) )
   (<seg> ^look-command <mh>)
}

sp {op_look1*apply*send-look-command*cozmo*down
   (state <s> ^name op_look1
              ^operator.name send-look-command
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^direction down1
              ^top-state.io <io>)
   (<io> ^input-link.self.head-angle <angle>
         ^output-link <out>)
-->
   (<out> ^move-head <mh>)
   (<mh> ^item-type output-command
         ^angle (- <angle> 0.4))
   (<seg> ^look-command <mh>)
}


