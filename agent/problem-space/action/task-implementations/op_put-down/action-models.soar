###############################################
## put-down


# Mark the object as not-grabbed
sp {action*apply*op_put-down1*internal*do*action-model*arm-status
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
               ^operator <o>)
   (<o> ^name op_put-down1
        ^arg1.id.predicates <obj1-preds>)
   (<obj1-preds> ^arm-status grabbed)
-->
   (<obj1-preds> ^arm-status grabbed -)
   (<obj1-preds> ^arm-status not-grabbed)
}

# Add the expected relation from the arg2
sp {action*apply*op_put-down1*internal*do*action-model*on
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name op_put-down1
        ^arg1.id <obj1>
        ^arg2.{2} <obj2>
        ^arg2.handle <pred-handle>)
   (<pred> ^handle <pred-handle>)
   (<obj1> ^predicates.arm-status grabbed)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
}

sp {action*apply*op_put-down1*do*action-model*add*in*relation*current-location
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.robot.current-location <loc>
              ^world.predicates.predicate <pred>)
   (<o> ^name op_put-down1
        ^arg1.id <obj>
       -^arg2.{2} <loc>)
   (<pred> ^handle in1)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

sp {action*apply*op_put-down1*mark*tried-tied-operator
   (state <s> ^problem-space.name action
              ^operator <o>
              ^untried-tied-operator <o>)
   (<o> ^name op_put-down1)
-->
   (<s> ^untried-tied-operator <o> -
        ^tried-tied-operator <o>)
}

sp {action*apply*op_put-down1*update*current-evaluation-depth
   (state <s> ^problem-space.name action
              ^operator <o>
              ^untried-tied-operator <o>
              ^current-evaluation-depth <ced>)
   (<o> ^name op_put-down1)
-->
   (<s> ^current-evaluation-depth <ced> -
        ^current-evaluation-depth (- <ced> 1) )
}
