sp {op_put-down1*propose*send-put-down-command
   (state <s> ^name op_put-down1
              ^held-object <obj>
              ^destination <dest>)
   (<obj> ^predicates.arm-status grabbed)
   (<dest> ^predicates <dest-preds>)
   (<dest-preds> ^is-visible1 visible1
                 ^is-reachable1 reachable1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-put-down-command
        ^item-type command-operator
        ^command-name put-down-command
        ^object <obj>
        ^destination <dest>)
}

sp {op_put-down1*propose*send-put-down-command*current-location
   (state <s> ^name op_put-down1
              ^held-object <obj>
              ^destination <dest>
              ^world.robot.current-location <dest>)
   (<obj> ^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-put-down-command
        ^item-type command-operator
        ^command-name put-down-command
        ^object <obj>
        ^destination <dest>)
}

sp {op_put-down1*reject*send-put-down-command*already*sent
   (state <s> ^name op_put-down1
              ^operator <o> +
              ^current-task-segment.put-down-command)
   (<o> ^name send-put-down-command)
-->
   (<s> ^operator <o> -)
}

### model send-put-down-command ###

sp {op_put-down1*internal*best*pref*send-put-down-command
   (state <s> ^name op_put-down1
              ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name send-put-down-command)
-->
   (<s> ^operator <o> >)
}

sp {op_put-down1*internal*apply*send-put-down-command*change*arm-status
   (state <s> ^name op_put-down1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-put-down-command
        ^object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -
            ^arm-status not-grabbed)
}

sp {op_put-down1*internal*apply*send-put-down-command*add*predicate
   (state <s> ^name op_put-down1
              ^problem-space.execution-type internal
              ^predicate-handle <ph>
              ^operator <o>
              ^world <w>)
   (<o> ^name send-put-down-command
        ^object <obj>
        ^destination <dest>)
   (<w> ^predicates.predicate <pred>)
   (<pred> ^handle <ph>)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <dest>)
}

### ai2thor ###

sp {op_put-down1*external*apply*send-put-down-command*ai2thor
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-put-down-command
        ^object.handle <h>
        ^destination.handle <rh>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name put-down
         ^object <h>
         ^receptacle <rh>)
   (<seg> ^put-down-command <pa>)
}

sp {op_put-down1*external*apply*send-put-down-command*internal
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> ^name send-put-down-command
        ^object <obj>)
-->
   (<seg> ^put-down-command <cmd>)
   (<cmd> ^item-type output-command
          ^name put-down
          ^object <obj>)
}

sp {op_put-down1*external*apply*send-put-down-command*cozmo*current
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^agent-params.domain cozmo
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-put-down-command
        ^command-name <cmd-name>
        ^object <obj>
        ^destination.predicates.modifier1 current1)
-->
   (<ol> ^place-object-down <pdo>)
   (<pdo> ^item-type output-command)
   (<seg> ^<cmd-name> <pdo>)
}

### magicbot ###

sp {op_put-down1*external*apply*send-put-down-command*magicbot
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-put-down-command
        ^object.perception-id <perc-id>
        ^command-name <cmd-name>)
-->
   (<ol> ^do-control-law <cmd>)
   (<cmd> ^name put-down
          ^item-type output-command
          ^termination-condition <term>)
   (<term> ^name holding
           ^parameters <params>)
   (<params> ^object-id (int <perc-id>)
             ^negated true)

   (<seg> ^<cmd-name> <cmd>)
}

sp {execute*op_put-down1*apply*send-put-down-command*add*in*current-location
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^operator.name send-put-down-command
              ^held-object <obj>
              ^world.robot.current-location <loc>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle in1)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <obj> ^2 <loc>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

