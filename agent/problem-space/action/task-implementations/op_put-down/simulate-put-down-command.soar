sp {execute*op_put-down1*propose*simulate-put-down-command
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^put-down-command <cmd>)
   (<cmd> ^name put-down
         -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-put-down-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {execute*op_put-down1*apply*simulate-put-down-command*mark*command*complete
   (state <s> ^name op_put-down1
              ^operator <o>)
   (<o> ^name simulate-put-down-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {execute*op_put-down1*apply*simulate-put-down-command*remove*holding-object*arm
   (state <s> ^name op_put-down1
              ^agent-params.simulate-perception true
              ^operator.name simulate-put-down-command
              ^held-object.perception-id <perc-id>
              ^top-state.perception-monitor.input-link.self.arm <arm>)
   (<arm> ^holding-object <perc-id>)
-->
   (<arm> ^holding-object <perc-id> -)
}

sp {execute*op_put-down1*apply*simulate-put-down-command*add*predicate
   (state <s> ^name op_put-down1
              ^operator.name simulate-put-down-command
              ^held-object <obj>
              ^predicate-handle <pred-h>
              ^destination <dest>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle <pred-h>)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <obj> ^2 <dest>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <dest>)
}

sp {execute*op_put-down1*apply*simulate-put-down-command*on1*add*predicate*below
   (state <s> ^name op_put-down1
              ^operator.name simulate-put-down-command
              ^held-object <obj>
              ^predicate-handle on1
              ^destination <dest>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle below1)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <dest> ^2 <obj>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <dest> ^2 <obj>)
}

sp {execute*op_put-down1*apply*simulate-put-down-command*add*in*current-location
   (state <s> ^name op_put-down1
              ^operator.name simulate-put-down-command
              ^held-object <obj>
              ^world.robot.current-location <loc>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle in1)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <obj> ^2 <loc>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

sp {execute*op_put-down1*apply*simulate-put-down-command*add*is-grabbed1*not-grabbed
   (state <s> ^name op_put-down1
              ^operator.name simulate-put-down-command
             -^agent-params.simulate-perception true
              ^held-object.predicates <preds>)
   (<preds> ^is-grabbed1 grabbed1)
-->
   (<preds> ^is-grabbed1 grabbed1 -
            ^is-grabbed1 not-grabbed1)
}
