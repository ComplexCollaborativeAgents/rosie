# Simulate the pick-up-command (internal world)

sp {execute*op_pick-up1*propose*simulate-pick-up-command
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^pick-up-command <cmd>)
   (<cmd> ^name pick-up
          ^object <obj>
         -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-pick-up-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*mark*command*complete
   (state <s> ^name op_pick-up1
              ^operator <o>)
   (<o> ^name simulate-pick-up-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*add*arm*holding-object
   (state <s> ^name op_pick-up1
              ^agent-params.simulate-perception true
              ^operator.name simulate-pick-up-command
              ^target-object.perception-id <perc-id>
              ^top-state.perception-monitor.input-link.self.arm <arm>)
-->
   (<arm> ^holding-object <perc-id>)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*remove*predicates
   (state <s> ^name op_pick-up1
              ^operator.name simulate-pick-up-command
              ^agent-params.simulate-perception true
              ^target-object <obj>
              ^world.predicates.predicate <pred>)
   (<pred> ^instance <i>)
   (<i> ^<num> <obj>)
-->
   (<pred> ^instance <i> -)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*add*is-grabbed1*grabbed
   (state <s> ^name op_pick-up1
              ^operator.name simulate-pick-up-command
             -^agent-params.simulate-perception true
              ^target-object.predicates <preds>)
   (<preds> ^is-grabbed1 not-grabbed1)
-->
   (<preds> ^is-grabbed1 not-grabbed1 -
            ^is-grabbed1 grabbed1)
}
