### Send the low-level pick-up command when the object is visible + reachable ###

sp {op_pick-up1*propose*send-pick-up-command
   (state <s> ^name op_pick-up1
              ^target-object <obj>
              ^desired <d>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^is-grabbed1 not-grabbed1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-pick-up-command
        ^item-type command-operator
        ^command-name pick-up-command
        ^object <obj>)
}

### model send-pick-up-command ###

sp {op_pick-up1*internal*best*pref*send-pick-up-command
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name send-pick-up-command)
-->
   (<s> ^operator <o> >)
}

sp {op_pick-up1*internal*apply*send-pick-up-command*change*is-grabbed1
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-pick-up-command
        ^object.predicates <preds>)
   (<preds> ^is-grabbed1 not-grabbed1)
-->
   (<preds> ^is-grabbed1 not-grabbed1 -
            ^is-grabbed1 grabbed1)
}

sp {op_pick-up*internal*apply*send-pick-up-command*remove*predicates
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name send-pick-up-command
        ^object <obj>)
   (<pred> ^instance <i>)
   (<i> ^<pnum> <obj>)
-->
   (<pred> ^instance <i> -)
}

### internal domain ###

sp {op_pick-up1*external*apply*send-pick-up-command*internal
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
        ^command-name <cmd-name>
        ^object <obj>)
-->
   (<seg> ^<cmd-name> <cmd>)
   (<cmd> ^item-type output-command
          ^name pick-up
          ^object <obj>)
}

### ai2thor ###

sp {op_pick-up1*external*apply*send-pick-up-command*ai2thor
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
        ^command-name <cmd-name>
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name pick-up
         ^object <h>)
   (<seg> ^<cmd-name> <pa>)
}

### cozmo ###

sp {op_pick-up1*external*apply*send-pick-up-command*cozmo
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^agent-params.domain cozmo
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
        ^command-name <cmd-name>
        ^object.perception-id <id>)
-->
   (<ol> ^pick-up-object <pa>)
   (<pa> ^item-type output-command
         ^object-id <id>)
   (<seg> ^<cmd-name> <pa>)
}

### tabletop ###

sp {op_pick-up1*external*apply*send-pick-up-command*tabletop
   (state <s> ^name op_pick-up1
              ^agent-params.domain tabletop
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
        ^command-name <cmd-name>
        ^object.handle <obj-handle>)
-->
   (<ol> ^pick-up <cmd>)
   (<cmd> ^item-type output-command
          ^object-handle <obj-handle>)
   (<seg> ^<cmd-name> <cmd>)
}

### fetch ###

sp {op_pick-up1*external*apply*send-pick-up-command*fetch
   (state <s> ^name op_pick-up1
              ^agent-params.domain fetch
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
        ^command-name <cmd-name>
        ^object.handle <obj-handle>)
-->
   (<ol> ^pick-up <cmd>)
   (<cmd> ^item-type output-command
          ^object-handle <obj-handle>)
   (<seg> ^<cmd-name> <cmd>)
}

### magicbot ###

sp {op_pick-up1*external*apply*send-pick-up-command*magicbot
   (state <s> ^name op_pick-up1
              ^agent-params.domain magicbot
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
        ^command-name <cmd-name>
        ^object.perception-id <perc-id>)
-->
   (<ol> ^do-control-law <cmd>)
   (<cmd> ^name pick-up
          ^item-type output-command
          ^parameters.object-id (int <perc-id>)
          ^termination-condition <term>)
   (<term> ^name holding
           ^parameters.object-id (int <perc-id>))
   (<seg> ^<cmd-name> <cmd>)
}

