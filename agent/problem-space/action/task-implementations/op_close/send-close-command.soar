sp {op_close1*propose*send-close-command
   (state <s> ^name op_close1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^door2 open2)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-close-command
        ^item-type command-operator
        ^object <obj>)
}

sp {op_close1*reject*send-close-command*already*sent
   (state <s> ^name op_close1
              ^operator <o> +
              ^current-task-segment.close-command)
   (<o> ^name send-close-command)
-->
   (<s> ^operator <o> -)
}

### model send-close-command
sp {op_close1*internal*apply*send-close-command*change*door
   (state <s> ^name op_close1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-close-command
        ^object.predicates <preds>)
   (<preds> ^door2 open2)
-->
   (<preds> ^door2 open2 -
            ^door2 closed2)
}

### ai2thor and internal
sp {op_close1*external*apply*send-close-command*ai2thor*internal
   (state <s> ^name op_close1
              ^problem-space.execution-type external
              ^agent-params.domain << ai2thor internal >>
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-close-command
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name close
         ^object <h>)
   (<seg> ^close-command <pa>)
}
