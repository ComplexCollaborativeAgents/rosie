sp {execute*op_close1*propose*simulate-close-command
   (state <s> ^name op_close1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^close-command <cmd>)
   (<cmd> ^name close
         -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-close-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {execute*op_close1*apply*simulate-close-command*mark*command*complete
   (state <s> ^name op_close1
              ^operator <o>)
   (<o> ^name simulate-close-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {execute*op_close1*apply*simulate-close-command*change*door2
   (state <s> ^name op_close1
              ^operator.name simulate-close-command
              ^target-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^property-handle door2
              ^values <vals>)
-->
   (<vals> ^open2 1.00 -
           ^closed2 1.00)
}


