sp {op_remove1*propose*send-remove-command
   (state <s> ^name op_remove1
              ^target-object <obj>
              ^world.objects.object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-remove-command
        ^item-type command-operator
        ^object <obj>)
}

sp {op_remove1*reject*send-remove-command*already*sent
   (state <s> ^name op_remove1
              ^operator <o> +
              ^current-task-segment.remove-command)
   (<o> ^name send-remove-command)
-->
   (<s> ^operator <o> -)
}

### model send-remove-command
sp {op_remove1*internal*apply*send-remove-command*add*value
   (state <s> ^name op_remove1
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.objects <objs>)
   (<o> ^name send-remove-command
        ^object <obj>)
   (<objs> ^object <obj>)
-->
   (<objs> ^object <obj> -)
}

# external 
sp {op_remove1*external*apply*send-remove-command
   (state <s> ^name op_remove1
              ^problem-space.execution-type external
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-remove-command
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name remove
         ^object <h>)
   (<seg> ^remove-command <pa>)
}

