sp {op_remove1*propose*simulate-remove-command
   (state <s> ^name op_remove1
              ^problem-space.execution-type external
              ^current-task-segment <seg>)
   (<seg> ^remove-command <cmd>)
   (<cmd> ^name remove
         -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-remove-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_remove1*apply*simulate-remove-command*mark*command*complete
   (state <s> ^name op_remove1
              ^operator <o>)
   (<o> ^name simulate-remove-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {op_remove1*apply*simulate-remove-command*remove-object
   (state <s> ^name op_remove1
              ^operator.name simulate-remove-command
              ^top-state.world.objects <objs>
              ^target-object <obj>)
   (<objs> ^object <obj>)
-->
   (<objs> ^object <obj> -)
}

sp {op_remove1*apply*simulate-remove-command*remove-predicates
   (state <s> ^name op_remove1
              ^operator.name simulate-remove-command
              ^top-state.world.predicates.predicate <pp>
              ^target-object <obj>)
   (<pp> ^instance <in>)
   (<in> ^<< 1 2 3 >> <obj>)
-->
   (<pp> ^instance <in> -)
}