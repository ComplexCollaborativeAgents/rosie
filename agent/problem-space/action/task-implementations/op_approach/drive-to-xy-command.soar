
# If we were told to go to an object (^target-object <obj>)
# Then find the object's position and drive to a position 1.5 meteres in front of it

sp {op_approach*elaborate*svs-filter*node_position*object
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^target-object.svs-obj.id <obj-id>
              ^top-state.svs.command <cmd>)
-->
    (<cmd> ^extract_once <dist>)
    (<dist> ^type distance
           ^a <robot>
           ^b <obj>
           ^distance_type hull)
    (<robot> ^type node
             ^id robot_body)
    (<obj> ^type node
           ^id <obj-id>)

   (<s> ^distance_filter <dist>)
}

sp {op_approach*elaborate*object-distance*from*filter*result
   (state <s> ^name op_approach1
              ^distance_filter.result.record.value <d>)
-->
   (<s> ^target-object-distance <d>)
}

sp {op_approach*elaborate*calcuations*drive-x*drive-y
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^target-object-distance <d>
              ^io.input-link.self.pose <pose>)
   (<pose> ^x <x> ^y <y> ^yaw <theta>)
-->
   (<s> ^calculations <c>)
   (<c> ^drive-x (+ <x> (* <d> (cos <theta>) ) ) )
   (<c> ^drive-y (+ <y> (* <d> (sin <theta>) ) ) )
}

sp {op_approach1*propose*send-drive-to-xy-command
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^calculations <c>
              -^current-task-segment.drive-command)
  (<c> ^drive-x <x>
       ^drive-y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-drive-to-xy-command
        ^position <pos>)
   (<pos> ^x <x> ^y <y>)
}

sp {op_approach1*apply*send-drive-to-xy-command*send
   (state <s> ^name op_approach1
              ^operator <o>
              ^io.output-link <out>
              ^current-task-segment <seg>)
   (<o> ^name send-drive-to-xy-command
        ^position <pos>)
   (<pos> ^x <x> ^y <y>)
-->
    (<out> ^do-control-law <law>)
    (<law> ^name drive-xy
           ^parameters <params>
           ^termination-condition.name stabilized)
    (<params> ^x <x> ^y <y>)
   (<seg> ^drive-command <law>)
}
