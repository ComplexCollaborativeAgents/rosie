### For the magicbot, we break approach into face and drive subtasks

### First face the object
sp {op_approach1*propose*op_face1
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^goal-coord <coord>
             -^current-task-segment.face-command.status success)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_face1
        ^item-type task-operator
        ^task-handle face1
        ^arg1 <arg1>)
   (<arg1> ^arg-type coordinate
           ^coord <coord>)
}

sp {op_approach1*apply*pop-task-segment*op_face1*mark*finished
   (state <s> ^name op_approach1
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> ^name pop-task-segment
        ^segment <child-seg>)
   (<child-seg> ^status success
                ^task-operator.name op_face1)
-->
   (<seg> ^face-command.status success)
}

# Second, go to the goal coordinate
sp {op_approach1*propose*op_go-to-xy1
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^goal-coord <coord>
              ^current-task-segment.face-command.status success
             -^current-task-segment.drive-command.status success)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_go-to-xy1
        ^item-type task-operator
        ^task-handle go-to-xy1
        ^arg1 <arg1>)
   (<arg1> ^arg-type coordinate
           ^coord <coord>)
}

sp {op_approach1*apply*pop-task-segment*op_go-to-xy1*mark*finished
   (state <s> ^name op_approach1
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> ^name pop-task-segment
        ^segment <child-seg>)
   (<child-seg> ^status success
                ^task-operator.name op_go-to-xy1)
-->
   (<seg> ^drive-command.status success)
}
