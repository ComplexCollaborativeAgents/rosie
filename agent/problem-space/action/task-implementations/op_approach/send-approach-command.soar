
sp {op_approach1*propose*send-approach-command
   (state <s> ^name op_approach1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^arm-status not-grabbed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-approach-command
        ^item-type command-operator
        ^object <obj>)
}

sp {op_approach1*reject*send-approach-command*already*sent
   (state <s> ^name op_approach1
              ^operator <o> +
              ^current-task-segment.approach-command)
   (<o> ^name send-approach-command)
-->
   (<s> ^operator <o> -)
}

sp {op_approach1*internal*apply*send-approach-command*change*arm-status
   (state <s> ^name op_approach1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-approach-command
        ^object.predicates <preds>)
   (<preds> ^is-reachable1 not-reachable1)
-->
   (<preds> ^is-reachable1 not-reachable1 -
            ^is-reachable1 reachable1)
}

sp {op_approach1*external*apply*send-approach-command*ai2thor
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-approach-command
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name approach
         ^object <h>)
   (<seg> ^approach-command <pa>)
}

#sp {op_approach1*external*apply*send-approach-command*internal
#   (state <s> ^name op_approach1
#              ^problem-space.execution-type external
#              ^agent-params.domain internal
#              ^operator <o>
#              ^current-task-segment <seg>)
#   (<o> ^name send-approach-command
#        ^object <obj>)
#-->
#   (<seg> ^approach-command <cmd>)
#   (<cmd> ^item-type output-command
#          ^name approach
#          ^object <obj>)
#}

### Domain = cozmo
sp {op_approach1*external*apply*send-approach-command*cozmo
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^agent-params.domain cozmo
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-approach-command
        ^object.handle <h>)
-->
   (<ol> ^go-to-object <gto>)
   (<gto> ^item-type output-command
          ^object-id <h>)
   (<seg> ^approach-command <gto>)
}

