### Internally modeling the use task ###

sp {problem-space*action*internal*elaborate*op_use1*dont-learn-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_use1)
-->
   (<o> ^dont-learn-action-model true)
}

### Action Model: Using a kettle with something fillable will fill it with water
sp {problem-space*action*internal*apply*op_use1*kettle*fillable
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <op>)
   (<op> ^name op_use1
         ^arg1.id.predicates <p1>
         ^arg2.{2}.predicates <p2>)
   (<p1> ^category kettle1)
   (<p2> ^affordance1 fillable1
         ^is-filled1 empty1)
-->
   (<p2> ^is-filled1 empty1 -
         ^is-filled1 filled1
         ^liquid1 water1)
}

### Action Model: Using a filled object with a sink will empty the object
sp {problem-space*action*internal*apply*op_use1*fillable*sink
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <op>)
   (<op> ^name op_use1
         ^arg1.id.predicates <p1>
         ^arg2.{2}.predicates <p2>)
   (<p1> ^affordance1 fillable1
         ^is-filled1 filled1
         ^liquid1 <liq>)
   (<p2> ^category sink1)
-->
   (<p1> ^is-filled1 filled1 -
         ^is-filled1 empty1
         ^liquid1 <liq> -)
}
