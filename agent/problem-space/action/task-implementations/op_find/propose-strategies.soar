# Known location in svs, turn to face it
sp {op_find1*propose*op_face
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>)
   (<obj> ^svs-obj)
-->
   (<s> ^operator <face-op> + =)
   (<face-op> ^name op_face1
              ^item-type task-operator
              ^task-handle face1
              ^arg1 <obj-arg>)
   (<obj-arg> ^arg-type object
              ^id <obj>)
}

# Known location in the world, propose go-to that location
sp {op_find1*propose*op_go-to1*location
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>
              ^world.robot.current-location <cur-loc>
              ^world.objects.object { <loc> <> <cur-loc> }
              ^world.predicates.predicate <in-pred>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_go-to1
        ^item-type task-operator
        ^task-handle go-to1
         ^arg2 <arg2>)
   (<arg2> ^arg-type partial-predicate
           ^handle to1
           ^2 <loc>)
}

# Ask the instructor if they know where the object is 
sp {op_find1*propose*initiate-interaction*get-find-help
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>
             -^current-task-segment.stop-asking true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name initiate-interaction
        ^type get-find-help
        ^object <obj>)
}

# Scan the room to see if you can see it
sp {op_find1*propose*op_scan
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_scan1
        ^item-type task-operator
        ^task-handle scan1
        ^until-clause <until>)
   (<until> ^arg-type until-clause
            ^predicate <pred>)
   (<pred> ^type unary
           ^handle visible1
           ^property-handle is-visible1
           ^1 <obj>)
}

# Explore the building to try and find it
sp {op_find1*propose*op_explore
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_explore1
        ^item-type task-operator
        ^task-handle explore1
        ^until-clause <until>)
   (<until> ^arg-type until-clause
            ^predicate <pred>)
   (<pred> ^type unary
           ^handle visible1
           ^property-handle is-visible1
           ^1 <obj>)
}
