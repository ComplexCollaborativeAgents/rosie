### Internally modeling the find task ###

sp {problem-space*action*internal*elaborate*op_find1*known-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_find1)
-->
   (<o> ^known-action-model true)
}

# Change to visible
sp {problem-space*action*internal*apply*op_find1*change*visible
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <op>)
   (<op> ^name op_find1
         ^arg1.id.predicates <preds>)
   (<preds> ^is-visible1 not-visible1)

-->
   (<preds> ^is-visible1 not-visible1 -
            ^is-visible1 visible1)
}

sp {problem-space*action*internal*apply*op_find1*change*current-location
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world <w>)
   (<o> ^name op_find1
        ^arg1.id <obj>)
   (<w> ^robot <robot>
        ^objects <objs>
        ^predicates.predicate <in-pred>)
   (<robot> ^current-location <loc>)
   (<in-pred> ^handle in1)
   -{(<in-pred> ^instance <i>)
     (<i> ^1 <obj> ^2 <loc>)}
-->
   (<objs> ^object <new-loc>)
   (<new-loc> ^root-category location
              ^predicates <loc-preds>)
   (<loc-preds> ^category entity1 location)
   (<in-pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <new-loc>)
   (<robot> ^current-location <loc> -
            ^current-location <new-loc>)
}
