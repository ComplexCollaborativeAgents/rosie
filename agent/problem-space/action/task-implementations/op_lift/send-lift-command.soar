sp {op_lift1*propose*send-lift-command
   (state <s> ^name op_lift1
              ^problem-space.execution-type external)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-lift-command
        ^item-type command-operator)
}

sp {op_lift1*reject*send-lift-command*already*sent
   (state <s> ^name op_lift1
              ^operator <o> +
              ^current-task-segment.lift-command)
   (<o> ^name send-lift-command)
-->
   (<s> ^operator <o> -)
}

### Domain = cozmo
sp {op_lift1*apply*send-lift-command*cozmo*up
   (state <s> ^name op_lift1
              ^operator.name send-lift-command
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^direction up1
              ^top-state.io.output-link <out>)
-->
   (<out> ^move-lift <ml>)
   (<ml> ^item-type output-command
         ^height 90.0)
   (<seg> ^lift-command <ml>)
}

sp {op_lift1*apply*send-lift-command*cozmo*down
   (state <s> ^name op_lift1
              ^operator.name send-lift-command
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^direction down1
              ^top-state.io.output-link <out>)
-->
   (<out> ^move-lift <ml>)
   (<ml> ^item-type output-command
         ^height 0.0)
   (<seg> ^lift-command <ml>)
}


