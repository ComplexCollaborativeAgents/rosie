sp {replay-subaction*action-operator*propose*report-success
   (state <s> ^superstate.name replay-subaction
              ^superstate.operator <aop>
              ^action-operator <aop>
              ^desired <des>)
   (<des> ^satisfied true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {replay-subaction*action-operator*apply*report-success
   (state <s> ^superstate.name replay-subaction
              ^superstate.operator <aop>
              ^action-operator <aop>
              ^operator.name report-success
              ^superstate <ss>)
-->
   (<ss> ^retrospective-learning-result success)
}
             
sp {replay-subaction*action-operator*prefer*report-success*over*all*others
   (state <s> ^superstate.name replay-subaction
              ^superstate.operator <aop>
              ^action-operator <aop>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name report-success)
   (<o2> ^name <> report-success)
-->
   (<s> ^operator <o1> > <o2>)
}
