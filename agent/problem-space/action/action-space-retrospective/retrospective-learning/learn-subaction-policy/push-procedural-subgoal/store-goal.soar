sp {push-procedural-subgoal*propose*store-goal
   (state <s> ^name push-procedural-subgoal
              ^added-subgoal true
             -^stored-subgoal true)
-->
   (<s> ^operator <o> +)
   (<o> ^name store-goal)
}

sp {push-procedural-subgoal*apply*store-goal
   (state <s> ^name push-procedural-subgoal
              ^operator.name store-goal
              ^action-id <aid>
              ^smem.command <cmd>)
   (<aid> ^goal <g>)
   (<g> ^subgoal-count <c>
        ^<c> <subgoal>)
-->
   (<cmd> ^store <aid>
          ^store <g>
          ^store <subgoal>)
   (<s> ^stored-subgoal true)
}

sp {push-procedural-subgoal*apply*store-goal*subgoal*predicate
   (state <s> ^name push-procedural-subgoal
              ^operator.name store-goal
              ^action-id <aid>
              ^smem.command <cmd>)
   (<aid> ^goal <g>)
   (<g> ^subgoal-count <c>
        ^<c> <subgoal>)
   (<subgoal> ^<pred-num> <pred>)
   (<pred> ^item-type goal-predicate)
-->
   (<cmd> ^store <pred>)
}
