sp {push-procedural-subgoal*propose*add-subgoal-to-goal-id
   (state <s> ^name push-procedural-subgoal
              ^task-concept-network.goal <g>
              ^subaction-handle <sub-h>
              -^added-subgoal <sub>)
   (<g> ^subgoal-count <c>)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-subgoal-to-goal-id
        ^new-subgoal-count (+ <c> 1)
        ^subaction-handle <sub-h>)
}

sp {push-procedural-subgoal*apply*add-subgoal-to-goal-id*increment*other*goals
   (state <s> ^name push-procedural-subgoal
              ^operator.name add-subgoal-to-goal-id
              ^task-concept-network.goal <g>)
   (<g> ^<cur-num> <other-subgoal>)
   (<other-subgoal> ^item-type task-subgoal)
-->
   (<g> ^<cur-num> <other-subgoal> -
        ^(+ <cur-num> 1) <other-subgoal>)
}

sp {push-procedural-subgoal*apply*add-subgoal-to-goal-id
   (state <s> ^name push-procedural-subgoal
              ^operator <o>
              ^task-concept-network.goal <g>)
   (<o> ^name add-subgoal-to-goal-id
        ^new-subgoal-count <nc>
        ^subaction-handle <sub-h>)
   (<g> ^subgoal-count <old-c>)
-->
   (<g> ^subgoal-count <old-c> -
        ^subgoal-count <nc>
        ^1 <subgoal>)
   (<subgoal> ^item-type task-subgoal
              ^handle (make-constant-symbol |task-subgoal|)
              ^pred-count 1
              ^1 <pred1>)
   (<pred1> ^item-type goal-predicate
            ^type subaction
            ^subaction-handle <sub-h>)

   (<s> ^added-subgoal <subgoal>)
}

