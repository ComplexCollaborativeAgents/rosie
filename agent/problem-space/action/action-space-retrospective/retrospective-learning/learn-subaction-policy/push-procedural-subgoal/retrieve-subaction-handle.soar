sp {push-procedural-subgoal*propose*retrieve-subaction-handle
   (state <s> ^name push-procedural-subgoal
             -^subaction-handle)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-subaction-handle)
}

sp {push-procedural-subgoal*apply*retrieve-subaction-handle
   (state <s> ^name push-procedural-subgoal
              ^operator.name retrieve-subaction-handle
              ^action-operator.action-handle <action-handle>
              ^subaction-context <context> 
              ^superstate.superstate.subactions-stack <stack>
              ^epmem.command <cmd>)
   (<context> ^episode-id <min>
              ^end-episode-id <max>
              ^subaction-copy.action-handle <sub-handle>)
-->
   (<cmd> ^query <q>
          ^before <max>
          ^after <min>)
   (<q> ^action-stack.top <top>)
   (<top> ^execution-operator.action-handle <sub-handle>
          ^assigned-subaction-handle true
          ^prev.execution-operator.action-handle <action-handle>)
}


sp {push-procedural-subgoal*apply*retrieve-subaction-handle*success
   (state <s> ^name push-procedural-subgoal
              ^operator.name retrieve-subaction-handle
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^result.retrieved <episode>
            ^command <cmd>)
   (<res> ^success <q>
          ^retrieved <episode>
          ^memory-id <ep-id>
          ^normalized-match-score 1.0)
   (<cmd> ^query <q>)
   (<episode> ^action-stack.top.execution-operator.subaction-handle <sub-h>)
-->
   (<s> ^subaction-handle <sub-h>)
   (<cmd> ^query <q> -)
}

