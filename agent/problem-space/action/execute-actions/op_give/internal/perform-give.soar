sp {op_give1*internal*propose*perform-give
   (state <s> ^name op_give1
              ^problem-space.action.type execute
              ^target-object.predicates.arm-status grabbed)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-give)
}

sp {op_give1*internal*apply*perform-give*arm-status*not-grabbed
   (state <s> ^name op_give1
              ^problem-space.action.type execute
              ^operator.name perform-give
              ^target-object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -
            ^arm-status not-grabbed)
}

sp {op_give1*apply*report-success*add*holding*predicate
   (state <s> ^name op_give1
              ^operator.name report-success
              ^held-object <obj>
              ^person <person>
              ^top-state.world.predicates.predicate <in-pred>)
   (<in-pred> ^handle holding1)
   -{(<in-pred> ^instance <cur-i>)
     (<cur-i> ^1 <obj> ^2 <person>)}
-->
   (<in-pred> ^instance <i>)
   (<i> ^1 <person> ^2 <obj>)
}

sp {op_give1*apply*report-success*add*in*predicate
   (state <s> ^name op_give1
              ^operator.name report-success
              ^held-object <obj>
              ^top-state.world.predicates.predicate <in-pred>
              ^top-state.world.robot.current-location <loc>)
   (<in-pred> ^handle in1)
   -{(<in-pred> ^instance <cur-i>)
     (<cur-i> ^1 <obj> ^2 <loc>)}
-->
   (<in-pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}