#### Different failure cases that can arise during turn

# command-error: The turn command got an error status
sp {op_scan1*propose*report-failure*command-error
   (state <s> ^name op_scan1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.turn-command.status error)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type command-error
        ^failure-info <handle>)
}

# execution-failed: The turn command got a failure status
sp {op_scan1*propose*report-failure*execution-failed
   (state <s> ^name op_scan1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.turn-command.status failure)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type execution-failed
        ^failure-info <handle>)
}

# unsatisifed-until-clause: the turn command finished without the until clause being satisfied
sp {op_scan1*propose*report-failure*unsatisifed-until-clause*after*turn
   (state <s> ^name op_scan1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.turn-command.status success
              ^until-clause <until>)
   (<until> -^satisfied true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type unsatisfied-until-clause
        ^failure-info <handle>)
}

sp {op_scan1*apply*report-failure*unsatisfied*until-clause*remove*unseen*in*predicates
  (state <s> ^name op_scan1
             ^operator <o>
             ^world.robot.current-location <loc>
             ^world.predicates.predicate <in-pred>)
  (<o> ^name report-failure
       ^failure-type unsatisfied-until-clause)
  (<in-pred> ^handle in1
             ^instance <i>)
  (<i> ^1 <obj> ^2 <loc>)
  (<obj> -^predicates.object-status confirmed
         -^predicates.visible true)
-->
  (<in-pred> ^instance <i> -)
}

# unsatisifed-until-clause: the stop command finished without the until clause being satisfied
sp {op_scan1*propose*report-failure*unsatisifed-until-clause*after*stop
   (state <s> ^name op_scan1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.stop-command.status success
              ^until-clause <until>)
   (<until> -^satisfied true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type unsatisfied-until-clause
        ^failure-info <handle>)
}
