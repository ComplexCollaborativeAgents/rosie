#### Different failure cases that can arise during follow-wall

# direction-retrieval-failure: No LTI was found with the given direction-handle
sp {op_follow-wall1*propose*report-failure*direction-retrieval-failure
   (state <s> ^name op_follow-wall1
              ^problem-space.action.type execute
              ^smem-query.result failure
              ^direction-handle <handle>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type direction-retrieval-failure
        ^failure-info <handle>)
}

# invalid-direction-failure: The LTI found for the direction-handle is not a relative-direction
sp {op_follow-wall1*propose*report-failure*invalid-direction-failure
   (state <s> ^name op_follow-wall1
              ^problem-space.action.type execute
              ^direction-handle <handle>
              ^direction-id.property.handle <> relative-direction1)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type invalid-direction-failure
        ^failure-info <handle>)
}

# unsatisfied-until-clause: The follow-wall command finished, but the until-clause was not satisfied
sp {op_follow-wall1*propose*report-failure*unsatisfied-until-clause*after*follow-wall
   (state <s> ^name op_follow-wall1
              ^problem-space.action.type execute
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.follow-wall-command.status success
             -^current-action.stop-command
              ^until-clause <until>)
   (<until> -^satisfied true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type unsatisfied-until-clause
        ^failure-info <handle>)
}

# unsatisfied-until-clause: there was a stop command, but the until-clause is not satisfied
sp {op_follow-wall1*propose*report-failure*unsatisfied-until-clause*after*stop
   (state <s> ^name op_follow-wall1
              ^problem-space.action.type execute
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.stop-command.status success
              ^until-clause <until>)
   (<until> -^satisfied true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type unsatisfied-until-clause
        ^failure-info <handle>)
}

# command-error: The follow-wall command got an error status
sp {op_follow-wall1*propose*report-failure*command-error
   (state <s> ^name op_follow-wall1
              ^problem-space.action.type execute
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << follow-wall-command stop-command >> }.status error)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type command-error
        ^failure-info <handle>)
}

# execution-failed: The follow-wall command got a failure status
sp {op_follow-wall1*propose*report-failure*execution-failed
   (state <s> ^name op_follow-wall1
              ^problem-space.action.type execute
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << follow-wall-command stop-command >> }.status << failure interrupted >>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type execution-failed
        ^failure-info <handle>)
}
