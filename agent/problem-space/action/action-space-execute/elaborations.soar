# Elaborate: task-stack from superstate (link to top-state.task-stack)
sp {action-space*execute*elaborate*task-stack*from*superstate
   (state <s> ^problem-space.action-space execute
              ^superstate.task-stack <stack>)
-->
   (<s> ^task-stack <stack>)
}

# Elaborate: current-action, the segment from the task-stack for the task-operator
sp {action-space*execute*elaborate*current-action*from*superstate*child-action
   (state <s> ^problem-space.action-space execute
              ^task-operator <o>
              ^superstate.child-action <a>)
   (<a> ^task-operator <o>)
-->
   (<s> ^current-action <a>)
}

# Elaborate: parent-action, the previous action segment on the task-stack
sp {action-space*execute*elaborate*parent-action
   (state <s> ^problem-space.action-space execute
              ^current-action.prev { <parent> <> none })
-->
   (<s> ^parent-action <parent>)
}

# Elaborate: child-action, the next action segment on the task-stack 
sp {action-space*execute*elaborate*child-action
   (state <s> ^problem-space.action-space execute
              ^current-action.next { <child> <> none })
-->
   (<s> ^child-action <child>)
}


