sp {action-space*execute*propose*add-subaction-to-tcn*action-source*instruction
   (state <s> ^problem-space.action-space execute
              ^child-action <a>
              ^action-operator <action>)
   (<a> ^action-source instruction
        ^execution-operator <subaction>)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-subaction-to-tcn
        ^subaction <subaction>
        ^action-operator <action>)
}

sp {action-space*execute*apply*add-subaction-to-tcn*finished*remove*action-soruce
   (state <s> ^problem-space.action-space execute
              ^operator.name add-subaction-to-tcn
              ^child-action <a>
              ^current-action <ca>
              ^created-subaction <sub-h>)
   (<a> ^action-source instruction)
-->
   (<a> ^action-source instruction -)
   (<s> ^created-subaction <sub-h> -)
   (<ca> ^learn-subaction-proposal <sub-h>)
}
    
sp {action-space*execute*apply*add-subaction-to-tcn*finished*add*subtask-handle
   (state <s> ^problem-space.action-space execute
              ^operator.name add-subaction-to-tcn
              ^child-action <a>
              ^created-subaction <sub-h>)
   (<a> ^action-source instruction
        ^execution-operator <ex-op>)
-->
   (<ex-op> ^subtask-handle <sub-h>)
   (<a> ^assigned-subtask-handle true)
}
    
