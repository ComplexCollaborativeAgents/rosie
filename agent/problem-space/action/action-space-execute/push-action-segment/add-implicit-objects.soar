sp {problem-space*action*push-action-segment*elaborate*object-slots
   (state <s> ^name push-action-segment)
-->
   (<s> ^object-slots <slots>)
}

sp {problem-space*action*push-action-segment*elaborate*object-slots*from*goal
   (state <s> ^name push-action-segment
              ^object-slots <slots>
              ^task-concept-network.goal.<sg-num> <subgoal>)
   (<subgoal> ^item-type task-subgoal
              ^<pred-num> <pred>)
   (<pred> ^item-type goal-predicate
           ^{ << 1 2 3 4 5 >> } <obj-slot>)
-->
   (<slots> ^object-slot <obj-slot>)
}

sp {problem-space*action*push-action-segment*elaborate*object-slots*from*subtasks*object*arg
   (state <s> ^name push-action-segment
              ^object-slots <slots>
              ^task-concept-network.procedural.subtasks.subtask <sub>)
   (<sub> ^<arg-name> <arg>)
   (<arg> ^arg-type object
         -^arg-status abstract
          ^id <obj-slot>)
-->
   (<slots> ^object-slot <obj-slot>)
}

sp {problem-space*action*push-action-segment*elaborate*object-slots*from*subtasks*predicate*arg
   (state <s> ^name push-action-segment
              ^object-slots <slots>
              ^task-concept-network.procedural.subtasks.subtask <sub>)
   (<sub> ^<arg-name> <arg>)
   (<arg> ^arg-type predicate
         -^arg-status abstract
          ^2 <obj-slot>)
-->
   (<slots> ^object-slot <obj-slot>)
}

sp {problem-space*action*push-action-segment*elaborate*object-slot*explicit*matches*object*arg
   (state <s> ^name push-action-segment
              ^object-slots <slots>
              ^task-concept-network.procedural.<arg-name> <arg>
              ^segment-to-push.execution-operator.<arg-name> <ex-arg>)
   (<slots> ^object-slot <obj-slot>)
   (<arg> ^arg-type object
          ^id <obj-slot>)
   (<ex-arg> ^arg-type object)
-->
   (<slots> ^explicit <obj-slot>)
}

sp {problem-space*action*push-action-segment*elaborate*object-slot*explicit*matches*predicate*arg
   (state <s> ^name push-action-segment
              ^object-slots <slots>
              ^task-concept-network.procedural.<arg-name> <arg>
              ^segment-to-push.execution-operator.<arg-name> <ex-arg>)
   (<slots> ^object-slot <obj-slot>)
   (<arg> ^arg-type predicate
          ^id <obj-slot>)
   (<ex-arg> ^arg-type predicate)
-->
   (<slots> ^explicit <obj-slot>)
}

sp {problem-space*action*push-action-segment*elaborate*object-slot*implicit
   (state <s> ^name push-action-segment
              ^object-slots <slots>
              ^task-concept-network.procedural.<arg-name> <arg>)
   (<slots> ^object-slot <obj-slot>
            -^explicit <obj-slot>)
   (<obj-slot> ^default.<prop> <any>)
-->
   (<slots> ^implicit <obj-slot>)
}
