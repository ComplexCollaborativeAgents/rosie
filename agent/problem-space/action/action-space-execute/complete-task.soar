sp {action-space*execute*propose*complete-task*success*desired*satisfied*no*smem*goal
   (state <s> ^problem-space.action-space execute
              ^task-concept-network <tcn>
              ^desired <des>
              ^current-task-segment <seg>)
   (<tcn> -^goal <goal>)
   (<des> ^satisfied true)
   (<seg> -^status)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-task
        ^status success)
}

sp {action-space*execute*propose*complete-task*success*desired*satisfied*final*subgoal
   (state <s> ^problem-space.action-space execute
              ^task-concept-network.goal <g>
              ^desired <des>
              ^current-task-segment <seg>)
   (<g> ^subgoal-count <c>
        ^{<c>}.handle <subgoal-h>)
   (<des> ^handle <subgoal-h>
          ^satisfied true)
   (<seg> -^status)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-task
        ^status success)
}

sp {action-space*execute*propose*complete-task*unrecoverable-failure
   (state <s> ^problem-space.action-space execute
              ^current-task-segment <seg>)
   (<seg> ^unrecoverable-failure <name>
       -^status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete-task
        ^status failure
        ^failure-name <name>)
}

sp {action-space*execute*apply*complete-task*mark*status*success
   (state <s> ^problem-space.action-space execute
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> ^name complete-task
        ^status <status>)
-->
   (<seg> ^status <status>)
}

sp {action-space*execute*apply*complete-task*remove*output*commands
   (state <s> ^problem-space.action-space execute
              ^operator.name complete-task
              ^current-task-segment <seg>
              ^io.output-link <out>)
   (<seg> ^sent-command <cmd>)
   (<out> ^<out-name> <cmd>)
-->
   (<out> ^<out-name> <cmd> -)
}
