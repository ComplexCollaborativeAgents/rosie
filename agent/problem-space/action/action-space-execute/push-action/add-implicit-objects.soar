sp {manage-action-stack*push-action*elaborate*object-slots
   (state <s> ^name push-action)
-->
   (<s> ^object-slots <slots>)
}

sp {manage-action-stack*push-action*elaborate*object-slots*from*goal
   (state <s> ^name push-action
              ^object-slots <slots>
              ^action-id.goal.<sg-num> <subgoal>)
   (<subgoal> ^item-type task-subgoal
              ^<pred-num> <pred>)
   (<pred> ^item-type goal-predicate
           ^{ << 1 2 3 4 5 >> } <obj-slot>)
-->
   (<slots> ^object-slot <obj-slot>)
}

sp {manage-action-stack*push-action*elaborate*object-slots*from*subactions*object*arg
   (state <s> ^name push-action
              ^object-slots <slots>
              ^action-id.procedural.subactions.subaction <sub>)
   (<sub> ^<arg-name> <arg>)
   (<arg> ^arg-type object
         -^arg-status abstract
          ^id <obj-slot>)
-->
   (<slots> ^object-slot <obj-slot>)
}

sp {manage-action-stack*push-action*elaborate*object-slots*from*subactions*predicate*arg
   (state <s> ^name push-action
              ^object-slots <slots>
              ^action-id.procedural.subactions.subaction <sub>)
   (<sub> ^<arg-name> <arg>)
   (<arg> ^arg-type predicate
         -^arg-status abstract
          ^2 <obj-slot>)
-->
   (<slots> ^object-slot <obj-slot>)
}

sp {manage-action-stack*push-action*elaborate*object-slot*explicit*matches*object*arg
   (state <s> ^name push-action
              ^object-slots <slots>
              ^action-id.procedural.<arg-name> <arg>
              ^action-to-push.execution-operator.<arg-name> <ex-arg>)
   (<slots> ^object-slot <obj-slot>)
   (<arg> ^arg-type object
          ^id <obj-slot>)
   (<ex-arg> ^arg-type object)
-->
   (<slots> ^explicit <obj-slot>)
}

sp {manage-action-stack*push-action*elaborate*object-slot*explicit*matches*predicate*arg
   (state <s> ^name push-action
              ^object-slots <slots>
              ^action-id.procedural.<arg-name> <arg>
              ^action-to-push.execution-operator.<arg-name> <ex-arg>)
   (<slots> ^object-slot <obj-slot>)
   (<arg> ^arg-type predicate
          ^id <obj-slot>)
   (<ex-arg> ^arg-type predicate)
-->
   (<slots> ^explicit <obj-slot>)
}

sp {manage-action-stack*push-action*elaborate*object-slot*implicit
   (state <s> ^name push-action
              ^object-slots <slots>
              ^action-id.procedural.<arg-name> <arg>)
   (<slots> ^object-slot <obj-slot>
            -^explicit <obj-slot>)
   (<obj-slot> ^default.<prop> <any>)
-->
   (<slots> ^implicit <obj-slot>)
}
