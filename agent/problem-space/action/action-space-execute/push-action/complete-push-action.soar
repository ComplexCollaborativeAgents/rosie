# once everything is done, push the action onto the stack

sp {push-action*propose*complete-push-action
   (state <s> ^name push-action)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-push-action)
}

sp {push-action*apply*complete-push-action*add*start-of-execution*flag
   (state <s> ^name push-action
              ^operator.name complete-push-action
              ^action-to-push <a>)
-->
   (<a> ^start-of-execution true)
}

# push the action on to the stack

sp {push-action*apply*complete-push-action*push
   (state <s> ^name push-action
              ^operator.name complete-push-action
              ^action-stack <stack>
              ^action-to-push <action>)
   (<stack> ^top <top>)
   (<top> ^next none)
-->
   (<stack> ^top <top> -
            ^top <action>)
   (<action> ^next none
             ^prev <top>)
   (<top> ^next none -
          ^next <action>)
}

# if the stack was empty, update the top and bottom 
sp {push-action*apply*complete-push-action*push*empty
   (state <s> ^name push-action
              ^operator.name complete-push-action
              ^action-stack <stack>
              ^action-to-push <action>)
   (<stack> ^top none
            ^bottom none)
-->
   (<stack> ^top none -
            ^top <action>
            ^bottom none -
            ^bottom <action>)
   (<action> ^next none
             ^prev none)
}

# clear the push command from the changes list
sp {push-action*apply*complete-push-action*clear*changes
   (state <s> ^name push-action
              ^operator.name complete-push-action
              ^action-stack.changes <changes>
              ^action-to-push <action>)
   (<changes> ^push <action>)
-->
   (<changes> ^push <action> -)
}

# Report to the output link that the action was started
sp {push-action*apply*complete-push-action*report-to-output-link
   (state <s> ^name push-action
              ^operator.name complete-push-action
              ^action-to-push <a>
              ^top-state.io.output-link <ol>)
-->
   (<ol> ^started-action <a>)
}

sp {push-action*apply*complete-push-action*ask*remove*previous*answer
   (state <s> ^name push-action
              ^operator.name complete-push-action
              ^action-to-push.execution-operator.action-handle ask1
              ^top-state.world.objects.object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^modifier1 answer1)
-->
   (<preds> ^modifier1 answer1 -)
}


sp {push-action*apply*complete-push-action*print*action*segment
   (state <s> ^name push-action
              ^agent-params.debug-actions true
              ^operator.name complete-push-action
              ^action-to-push <a>)
   (<a> ^execution-operator <exop>)
-->
   (write |Pushing a new action segment: | (crlf))
   (write (cmd print <a> -d 1)  (crlf))
   (write |Action Operator: |  (crlf))
   (write (cmd print <exop> -d 5)  (crlf))
}

sp {push-action*apply*complete-push-action*print*action*1*obj
   (state <s> ^name push-action
              ^agent-params.print-actions true
              ^operator.name complete-push-action
              ^action-to-push <a>)
   (<a> ^execution-operator <exop>)
   (<exop> ^name <op-name>
           ^<arg1-name> <arg1>
          -^{ <arg2-name> <> <arg1-name> }.arg-type <t2>)
   (<arg1> ^arg-type object ^id.handle <obj>)
-->
   (write (crlf) |!ACTION! | <op-name> | | <obj> (crlf))
}

sp {push-action*apply*complete-push-action*print*action*1*pred
   (state <s> ^name push-action
              ^agent-params.print-actions true
              ^operator.name complete-push-action
              ^action-to-push <a>)
   (<a> ^execution-operator <exop>)
   (<exop> ^name <op-name>
           ^<arg1-name> <arg1>
          -^{ <arg2-name> <> <arg1-name> }.arg-type <t2>)
   (<arg1> ^arg-type predicate ^{2}.handle <obj>)
-->
   (write (crlf) |!ACTION! | <op-name> | | <obj> (crlf))
}

sp {push-action*apply*complete-push-action*print*action*1*obj*1*pred
   (state <s> ^name push-action
              ^agent-params.print-actions true
              ^operator.name complete-push-action
              ^action-to-push <a>)
   (<a> ^execution-operator <exop>)
   (<exop> ^name <op-name>
           ^<arg1-name> <arg1>
           ^<arg2-name> <arg2>)
   (<arg1> ^arg-type object ^id.handle <obj1>)
   (<arg2> ^arg-type predicate ^{2}.handle <obj2>)
-->
   (write (crlf) |!ACTION! | <op-name> | | <obj1> | | <obj2> (crlf))
}

        

        
