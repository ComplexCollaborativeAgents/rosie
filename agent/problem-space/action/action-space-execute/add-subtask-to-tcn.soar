sp {action-space*execute*propose*add-subtask-to-tcn*task-source*instruction
   (state <s> ^problem-space.action-space execute
              ^child-task-segment <a>
              ^task-operator <task-op>)
   (<a> ^task-source instruction
        ^task-operator <subtask>)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-subtask-to-tcn
        ^subtask <subtask>
        ^task-operator <task-op>)
}

sp {action-space*execute*apply*add-subtask-to-tcn*finished*remove*task-source
   (state <s> ^problem-space.action-space execute
              ^operator.name add-subtask-to-tcn
              ^child-task-segment <a>
              ^current-task-segment <seg>
              ^created-subtask <sub-h>)
   (<a> ^task-source instruction)
-->
   (<a> ^task-source instruction -)
   (<s> ^created-subtask <sub-h> -)
   (<seg> ^learn-subtask-proposal <sub-h>)
}
    
sp {action-space*execute*apply*add-subtask-to-tcn*finished*add*subtask-handle
   (state <s> ^problem-space.action-space execute
              ^operator.name add-subtask-to-tcn
              ^child-task-segment <a>
              ^created-subtask <sub-h>)
   (<a> ^task-source instruction
        ^task-operator <ex-op>)
-->
   (<ex-op> ^subtask-handle <sub-h>)
   (<a> ^assigned-subtask-handle true)
}
    
