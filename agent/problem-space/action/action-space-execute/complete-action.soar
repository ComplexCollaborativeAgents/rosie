sp {action-space*execute*propose*complete-action*success*desired*satisfied*no*smem*goal
   (state <s> ^problem-space.action-space execute
              ^action-id <aid>
              ^desired <des>
              ^current-action <a>)
   (<aid> -^goal <goal>)
   (<des> ^satisfied true)
   (<a> -^status)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-action
        ^status success)
}

sp {action-space*execute*propose*complete-action*success*desired*satisfied*final*subgoal
   (state <s> ^problem-space.action-space execute
              ^action-id.goal <g>
              ^desired <des>
              ^current-action <a>)
   (<g> ^subgoal-count <c>
        ^{<c>}.handle <subgoal-h>)
   (<des> ^handle <subgoal-h>
          ^satisfied true)
   (<a> -^status)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-action
        ^status success)
}

sp {action-space*execute*propose*complete-action*unrecoverable-failure
   (state <s> ^problem-space.action-space execute
              ^current-action <a>)
   (<a> ^unrecoverable-failure <name>
       -^status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete-action
        ^status failure
        ^failure-name <name>)
}

sp {action-space*execute*apply*complete-action*mark*status*success
   (state <s> ^problem-space.action-space execute
              ^operator <o>
              ^current-action <a>)
   (<o> ^name complete-action
        ^status <status>)
-->
   (<a> ^status <status>)
}

sp {action-space*execute*apply*complete-action*remove*output*commands
   (state <s> ^problem-space.action-space execute
              ^operator.name complete-action
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<out> ^<out-name> <cmd>)
-->
   (<out> ^<out-name> <cmd> -)
}
