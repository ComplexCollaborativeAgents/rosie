## Do an smem query to lookup the smem info about the action
sp {add-subaction-to-tcn*elaborate*smem-query*action*handle
   (state <s> ^name add-subaction-to-tcn
              ^action-operator.task-handle <task-handle>)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 10)
   (<cue> ^handle <task-handle>)
}

# Elaborate task-concept-network from the result of the smem query
sp {add-subaction-to-tcn*elaborate*task-concept-network*from*smem-query
   (state <s> ^name add-subaction-to-tcn
              ^action-operator.task-handle <task-handle>
              ^smem-query.result <res>)
   (<res> ^handle <task-handle>)
-->
   (<s> ^task-concept-network <res>)
}
## Do an smem query to lookup the smem info about the subaction
sp {add-subaction-to-tcn*elaborate*smem-query*subaction*handle
   (state <s> ^name add-subaction-to-tcn
              ^subaction.task-handle <task-handle>)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 10)
   (<cue> ^handle <task-handle>)
}

# Elaborate subaction-tcn from the result of the smem query
sp {add-subaction-to-tcn*elaborate*subaction-tcn*from*smem-query
   (state <s> ^name add-subaction-to-tcn
              ^subaction.task-handle <task-handle>
              ^smem-query.result <res>)
   (<res> ^handle <task-handle>)
-->
   (<s> ^subaction-tcn <res>)
}
