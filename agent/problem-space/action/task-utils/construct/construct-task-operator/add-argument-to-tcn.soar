# If the argument is not on the task-concept-network.procedural link, add it
sp {construct-task-operator*propose*add-argument-to-tcn
   (state <s> ^name construct-task-operator
              ^update-task-concept-network true
              ^task-operator <task-op>
              ^task-concept-network.procedural <proc>)
   (<task-op> ^<arg-name> <arg>)
   (<arg> ^arg-type { <arg-type> << object concept partial-predicate >> })
   (<proc> -^<arg-name> <parg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument <arg>)
}

# arg-type concept
sp {construct-task-operator*apply*add-argument-to-tcn*concept
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument.arg-type concept)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type concept
           ^id <new-conc-slot>)
   (<s> ^to-store <proc> <parg>)
}

# arg-type object
sp {construct-task-operator*apply*add-argument-to-tcn*object
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument.arg-type object)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type object
           ^id <new-obj-slot>)
   (<s> ^to-store <proc> <parg>)
}

# arg-type partial-predicate
sp {construct-task-operator*apply*add-argument-to-tcn*predicate
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument.arg-type partial-predicate)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type partial-predicate
           ^id <new-pred-slot>
           ^2 <new-obj-slot>)
   (<s> ^to-store <proc> <parg>)
}


   

