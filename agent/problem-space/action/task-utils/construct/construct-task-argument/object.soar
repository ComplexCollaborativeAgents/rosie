#### constructing object arguments

### First we sanitize the object to make sure it is in the proper world representation and added to the world

sp {task-utils*construct-task-argument*elaborate*sanitize-object-argument
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^source <obj>)
-->
   (<s> ^sanitize-object-argument <san>)
   (<san> ^object <obj>)
}

sp {task-utils*construct-task-argument*elaborate*sanitize-object-argument*result*as*world-object
   (state <s> ^construct-task-argument <arg>
              ^sanitize-object-argument <san>)
   (<arg> ^arg-type object
          ^source <obj>)
   (<san> ^object <obj>
          ^result <wobj>)
-->
   (<arg> ^world-object <wobj>)
}

### construct-task-argument object

sp {task-utils*construct-task-argument*propose*construct-task-argument*object
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^world-object <obj>
         -^created-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type object
        ^object <obj>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*object
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type object
        ^object <obj>)
-->
   (<arg> ^created-arg <carg>)
   (<carg> ^id <obj>)
}
