### construct state-predicate

sp {task-utils*construct-task-argument*retrieve*state-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source <src>)
   (<src> ^type state
          ^handle <handle>)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 2
            ^destination <arg>
            ^result-name smem-content)
   (<cue> ^handle <handle>)
}

sp {task-utils*construct-task-argument*expand*state-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source <src>)
   (<src> ^type state
          ^1 <obj1>)
-->
   (<arg> ^pred-type state)

   (<s> ^construct-task-argument <obj1-arg>)
   (<obj1-arg> ^arg-type object
               ^source <obj1>)
   (<arg> ^create-obj1 <obj1-arg>)
}
          

sp {task-utils*construct-task-argument*propose*construct-task-argument*state-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^pred-type state
          ^source.handle <handle>
          ^smem-content.property.handle <prop-h>
          ^create-obj1.created-arg.id <obj1>
         -^created-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type state
        ^handle <handle>
        ^property-handle <prop-h>
        ^1 <obj1>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*state-predicate
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type state
        ^handle <handle>
        ^property-handle <prop-h>
        ^1 <obj1>)
-->
   (<arg> ^created-arg <carg>)
   (<carg> ^type state
           ^handle <handle>
           ^property-handle <prop-h>
           ^1 <obj1>)
}

