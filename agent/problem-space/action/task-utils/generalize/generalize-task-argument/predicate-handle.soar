### GENERALIZE PREDICATE HANDLE

# procedural match - the predicate-handle matches one on the reference-task-op
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*predicate-handle*procedural
  (state <s> ^generalize-task-argument <arg>
             ^reference-tcn-proc.<arg-name> <parg>
             ^reference-task-op.<arg-name>.handle <handle>)
  (<arg> ^instantiation <i>
         ^arg-type concept
        -^general-arg-rep)
  (<i> ^arg-type predicate-handle)
  (<parg> ^arg-type predicate
          ^id <pred-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type procedural
       ^id <pred-slot>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*predicate-handle*procedural
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type procedural
       ^id <pred-slot>)
-->
  (<arg> ^general-arg-rep <gen>)
  (<gen> ^id <pred-slot>)
}

# default match - copy the predicates onto a default rep
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*predicate-handle*default
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type concept
         ^instantiation <i>
        -^general-arg-rep)
  (<i> ^handle <handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^predicate-handle <handle>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*predicate-handle*default
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^predicate-handle <handle>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^id <pred-slot>)
  (<pred-slot> ^default <def>)
  (<def> ^predicate-handle <handle>)
}
