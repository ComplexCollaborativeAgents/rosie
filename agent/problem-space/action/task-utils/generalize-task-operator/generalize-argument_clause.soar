#### generalize-argument clause (until-clause after-clause)

sp {generalize-task-operator*generalize-task-argument*elaborate*clause*as*predicate-set
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <ga>)
   (<ga> ^instantiation <i>)
   (<i> ^arg-type << until-clause after-clause >>
        ^predicate.type << state relation >>)
-->
  (<ga> ^arg-type predicate-set)
}

sp {generalize-task-operator*apply*generalize-task-operator*copy*clause-type
   (state <s> ^name generalize-task-operator
              ^generalize-task-argument <arg>
              ^operator.name complete-generalize-task-operator)
   (<arg> ^arg-type predicate-set
          ^instantiation.arg-type { <clause-type> << until-clause after-clause >> }
          ^general-arg-rep <gen-rep>)
-->
   (<gen-rep> ^arg-type <clause-type>)
}

### DURATION PREDICATE
sp {generalize-task-operator*propose*generalize-task-argument*clause*duration
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^instantiation <i>
        -^general-arg-rep)
   (<i> ^arg-type { <clause-type> << until-clause after-clause >> }
       ^predicate <pred>)
   (<pred> ^type duration 
           ^number <num>
           ^unit <unit>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type clause
       ^clause-type <clause-type>
       ^match-type procedural
       ^pred-type duration
       ^number <num>
       ^unit <unit>)
}

sp {generalize-task-operator*apply*generalize-task-argument*clause*duration
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type clause
       ^clause-type <clause-type>
       ^pred-type duration
       ^number <num>
       ^unit <unit>)
-->
  (<arg> ^general-arg-rep <gen>)
   (<gen> ^arg-type <clause-type>
         ^predicate <pred>)
   (<pred> ^type duration 
           ^number <num>
           ^unit <unit>)
}

### CLOCKTIME PREDICATE
sp {generalize-task-operator*propose*generalize-task-argument*clause*clocktime
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^instantiation <i>
        -^general-arg-rep)
   (<i> ^arg-type { <clause-type> << until-clause after-clause >> }
       ^predicate <pred>)
   (<pred> ^type clocktime 
           ^minute <min>
           ^hour <hour>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type clause
       ^clause-type <clause-type>
       ^match-type procedural
       ^pred-type clocktime
       ^minute <min>
       ^hour <hour>)
}

sp {generalize-task-operator*apply*generalize-task-argument*clause*clocktime
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type clause
       ^clause-type <clause-type>
       ^pred-type clocktime
       ^minute <min>
       ^hour <hour>)
-->
  (<arg> ^general-arg-rep <gen>)
   (<gen> ^arg-type <clause-type>
         ^predicate <pred>)
   (<pred> ^type clocktime 
           ^minute <min>
           ^hour <hour>)
}


