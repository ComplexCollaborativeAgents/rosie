####################### concept ###########################

# procedural match - the concept matches one on the reference-task-op
sp {generalize-task-operator*propose*generalize-task-argument*concept*procedural
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>
             ^reference-tcn-proc.<arg-name> <parg>
             ^reference-task-op.<arg-name>.handle <handle>)
  (<arg> ^instantiation <i>
        -^general-arg-rep)
  (<i> ^arg-type concept
       ^handle <handle>)
  (<parg> ^arg-type concept
          ^id <conc-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^id <conc-slot>)
}

sp {generalize-task-operator*apply*generalize-task-argument*concept*procedural
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^id <conc-slot>)
-->
  (<arg> ^general-arg-rep <gen>)
  (<gen> ^arg-type concept
         ^id <conc-slot>)
}

# default match - copy the predicates onto a default rep
sp {generalize-task-operator*propose*generalize-task-argument*concept*default
  (state <s> ^name generalize-task-operator
             ^generalize-task-argument <arg>)
  (<arg> ^instantiation <i>
        -^general-arg-rep)
  (<i> ^arg-type concept
       ^handle <handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^predicate-handle <handle>)
}

sp {generalize-task-operator*apply*generalize-task-argument*concept*default
  (state <s> ^name generalize-task-operator
             ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^predicate-handle <handle>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^arg-type concept
         ^id <conc-slot>)
  (<conc-slot> ^default <def>)
  (<def> ^predicate-handle <handle>)
}
