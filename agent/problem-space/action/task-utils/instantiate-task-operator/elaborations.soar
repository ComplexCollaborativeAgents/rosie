sp {instantiate-task-operator*elaborate*operator*info
   (state <s> ^name instantiate-task-operator
              ^superstate.operator <ssop>)
   (<ssop> ^name instantiate-task-operator
           ^destination <ito>
           ^general-task-rep <rep>     
           ^reference-task-op <task-op>   
           ^reference-tcn-proc <tcn>
           ^world <w>)
-->
   (<s> ^result-destination <ito>
        ^general-task-rep <rep>     
        ^reference-task-op <task-op>   
        ^reference-tcn-proc <tcn>
        ^world <w>)
}

sp {instantiate-task-operator*elaborate*perform*from*general-task-rep
   (state <s> ^name instantiate-task-operator
              ^general-task-rep.perform <perf>)
-->
   (<s> ^perform <perf>)
}

sp {instantiate-task-operator*elaborate*perform*default
   (state <s> ^name instantiate-task-operator
              ^general-task-rep <src-id>)
   (<src-id> -^perform)
-->
   (<s> ^perform multiple)
}

# elaborate argument information 
## ENUMERATE: 0-5 arguments
sp {instantiate-task-operator*elaborate*no-args
   (state <s> ^name instantiate-task-operator
              ^general-task-rep <rep>)
   (<rep> -^{ << arg1 arg2 arg3 arg4 arg5 until-clause >> } <arg>)
-->
   (<s> ^num-args 0)
}

sp {instantiate-task-operator*elaborate*arguments*1*arg
   (state <s> ^name instantiate-task-operator
              ^general-task-rep <rep>)
   (<rep> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
         -^{ <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>)
   (<a1> ^arg-type <t1>)
-->
   (<s> ^num-args 1
        ^instantiate-task-argument <ita1>)
   (<ita1> ^arg-type <t1>
          ^arg-name <n1>
          ^general-arg-rep <a1>)
}

sp {instantiate-task-operator*elaborate*arguments*2*args
   (state <s> ^name instantiate-task-operator
              ^general-task-rep <rep>)
   (<rep> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
          ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
         -^{ <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
-->
   (<s> ^num-args 2
        ^instantiate-task-argument <ita1> <ita2>)
   (<ita1> ^arg-type <t1>
          ^arg-name <n1>
          ^general-arg-rep <a1>)
   (<ita2> ^arg-type <t2>
          ^arg-name <n2>
          ^general-arg-rep <a2>)
}

sp {instantiate-task-operator*elaborate*arguments*3*args
   (state <s> ^name instantiate-task-operator
              ^general-task-rep <rep>)
   (<rep> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
          ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
          ^{ <n3> > <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
         -^{ <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
-->
   (<s> ^num-args 3
        ^instantiate-task-argument <ita1> <ita2> <ita3>)
   (<ita1> ^arg-type <t1>
          ^arg-name <n1>
          ^general-arg-rep <a1>)
   (<ita2> ^arg-type <t2>
          ^arg-name <n2>
          ^general-arg-rep <a2>)
   (<ita3> ^arg-type <t3>
          ^arg-name <n3>
          ^general-arg-rep <a3>)
}

sp {instantiate-task-operator*elaborate*arguments*4*args
   (state <s> ^name instantiate-task-operator
              ^general-task-rep <rep>)
   (<rep> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
          ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
          ^{ <n3> > <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
          ^{ <n4> > <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
         -^{ <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
-->
   (<s> ^num-args 4
        ^instantiate-task-argument <ita1> <ita2> <ita3> <ita4>)
   (<ita1> ^arg-type <t1>
          ^arg-name <n1>
          ^general-arg-rep <a1>)
   (<ita2> ^arg-type <t2>
          ^arg-name <n2>
          ^general-arg-rep <a2>)
   (<ita3> ^arg-type <t3>
          ^arg-name <n3>
          ^general-arg-rep <a3>)
   (<ita4> ^arg-type <t4>
          ^arg-name <n4>
          ^general-arg-rep <a4>)
}


sp {instantiate-task-operator*elaborate*arguments*5*args
   (state <s> ^name instantiate-task-operator
              ^general-task-rep <rep>)
   (<rep> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
          ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
          ^{ <n3> > <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
          ^{ <n4> > <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
          ^{ <n5> > <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>
         -^{ <n6> <> <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a6>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
   (<a5> ^arg-type <t5>)
-->
   (<s> ^num-args 5
        ^instantiate-task-argument <ita1> <ita2> <ita3> <ita4> <ita5>)
   (<ita1> ^arg-type <t1>
          ^arg-name <n1>
          ^general-arg-rep <a1>)
   (<ita2> ^arg-type <t2>
          ^arg-name <n2>
          ^general-arg-rep <a2>)
   (<ita3> ^arg-type <t3>
          ^arg-name <n3>
          ^general-arg-rep <a3>)
   (<ita4> ^arg-type <t4>
          ^arg-name <n4>
          ^general-arg-rep <a4>)
   (<ita5> ^arg-type <t5>
          ^arg-name <n5>
          ^general-arg-rep <a5>)
}
