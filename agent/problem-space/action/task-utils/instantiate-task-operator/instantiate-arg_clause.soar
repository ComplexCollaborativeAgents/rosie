sp {instantiate-task-operator*instantiate-task-argument*arg-type*clause*elaborate*arg-type*predicate-set
  (state <s> ^name instantiate-task-operator
             ^instantiate-task-argument <gnd>)
   (<gnd> ^general-arg-rep.arg-type << until-clause after-clause >>)
-->
  (<gnd> ^arg-type predicate-set)
}

### DURATION PREDICATE

sp {instantiate-task-operator*propose*instantiate-task-argument*clause*duration
   (state <s> ^name instantiate-task-operator
              ^instantiate-task-argument <arg>)
  (<arg> ^general-arg-rep <gen>
          -^instantiation)
   (<gen> ^arg-type { <clause-type> << until-clause after-clause >> }
         ^predicate <pred>)
  (<pred> ^type duration
          ^number <num>
          ^unit <unit>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^match-type procedural
       ^arg-type clause
       ^clause-type <clause-type>
       ^pred-type duration
       ^number <num>
       ^unit <unit>)
}

sp {instantiate-task-operator*apply*instantiate-task-argument*clause*duration
  (state <s> ^name instantiate-task-operator
             ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type clause
       ^clause-type <clause-type>
       ^pred-type duration
       ^number <num>
       ^unit <unit>)
-->
  (<arg> ^instantiation <i>)
   (<i> ^arg-type <clause-type>
       ^predicate <pred>)
  (<pred> ^type duration
          ^number <num>
          ^unit <unit>)
}

# clocktime (after 3:15)
sp {instantiate-task-operator*propose*instantiate-task-argument*clause*clocktime
   (state <s> ^name instantiate-task-operator
              ^instantiate-task-argument <arg>)
  (<arg> ^general-arg-rep <gen>
          -^instantiation)
   (<gen> ^arg-type { <clause-type> << until-clause after-clause >> }
         ^predicate <pred>)
  (<pred> ^type clocktime
          ^hour <hour>
          ^minute <minute>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^match-type procedural
       ^arg-type clause
       ^clause-type <clause-type>
       ^pred-type clocktime
       ^hour <hour>
       ^minute <minute>)
}

sp {instantiate-task-operator*apply*instantiate-task-argument*clause*clocktime
  (state <s> ^name instantiate-task-operator
             ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type clause
       ^clause-type <clause-type>
       ^pred-type clocktime
       ^hour <hour>
       ^minute <minute>)
-->
  (<arg> ^instantiation <i>)
   (<i> ^arg-type <clause-type>
       ^predicate <pred>)
  (<pred> ^type clocktime
          ^hour <hour>
          ^minute <minute>)
}
