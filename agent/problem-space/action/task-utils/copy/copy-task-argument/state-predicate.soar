### COPY STATE PREDICATE

sp {task-utils*copy-task-argument*expand*state*predicate
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>
          ^object-map <map>)
   (<src> ^type state
          ^1 <obj1>)
-->
   (<arg> ^obj1-copy <obj1-copy>)

   (<s> ^copy-task-argument <obj1-copy>)
   (<obj1-copy> ^arg-type object
                ^source-arg.id <obj1>
                ^object-map <map>)
}

sp {task-utils*copy-task-argument*propose*copy-task-argument*state*predicate
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>
          ^obj1-copy.arg-copy.id <obj1>
         -^arg-copy)
   (<src> ^type state
          ^handle <h>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type state
        ^pred-handle <h>
        ^obj1 <obj1>)
}

sp {task-utils*copy-task-argument*apply*copy-task-argument*state*predicate
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type state
        ^pred-handle <h>
        ^obj1 <obj1>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^type state
           ^handle <h>
           ^1 <obj1>)
}


