# Don't do copy-desired if a copy already exists
sp {task-utils*copy-desired*reject*complete-copy-desired*if*was-copied
   (state <s> ^name learn-copy-desired-elaboration-rule
              ^operator <o> +)
   (<o> ^name << complete-copy-desired copy-desired-predicate copy-desired-predicate-set >>
        ^workspace <ws>)
   (<ws> ^was-copied true)
-->
   (<s> ^operator <o> -)
}

# Prefer higher workspaces 
# (This probably won't occur, but if there are 2 copy-desired-workspaces, 
#  do all the work from 1 before doing the other)
sp {task-utils*copy-desired*prefer*copy-desired*with*higher*workspace
   (state <s> ^name learn-copy-desired-elaboration-rule
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << copy-desired-predicate copy-desired-subgoal complete-copy-desired >>
         ^workspace <ws1>)
   (<o2> ^name << copy-desired-predicate copy-desired-subgoal complete-copy-desired >>
         ^workspace < <ws1>)
-->
   (<s> ^operator <o1> < <o2>)
}

# Don't copy a subgoal until all the predicates are copied
sp {task-utils*copy-desired*reject*copy-desired-subgoal*if*uncopied*predicates
   (state <s> ^name learn-copy-desired-elaboration-rule
              ^operator <o> +)
   (<o> ^name copy-desired-subgoal
        ^subgoal <sub>)
   (<sub> ^copy-predicate <pred>)
   (<pred> -^pred-copy)
-->
   (<s> ^operator <o> -)
}

# Don't finish until all subgoals are copied
sp {task-utils*copy-desired*reject*complete-copy-desired*if*uncopied*subgoals
   (state <s> ^name learn-copy-desired-elaboration-rule
              ^operator <o> +)
   (<o> ^name complete-copy-desired
        ^workspace <ws>)
   (<ws> ^copy-subgoal <sub>)
   (<sub> -^subgoal-copy)
-->
   (<s> ^operator <o> -)
}

# Don't learn a rule for an empty goal
sp {task-utils*copy-desired*reject*complete-copy-desired*if*no*subgoals
   (state <s> ^name learn-copy-desired-elaboration-rule
              ^operator <o> +)
   (<o> ^name complete-copy-desired
        ^workspace.source-desired <des>)
   (<des> -^pred-count > 0)
-->
   (<s> ^operator <o> -)
}




