
#### copy after-clause argument

sp {task-utils*copy-task-operator*expand*after-clause*duration
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type after-clause
          ^source-arg.predicate <src>)
   (<src> ^type duration
          ^number <num>
          ^unit <unit>)
-->
   (<arg> ^pred-type duration
          ^number <num>
          ^unit <unit>)
}

sp {task-utils*copy-task-operator*propose*copy-task-argument*after-clause*duration
   (state <s> ^copy-task-op-workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^arg-type after-clause
          ^pred-type duration
          ^number <num>
          ^unit <unit>
         -^arg-copy)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^workspace <ws>
        ^argument <arg>
        ^arg-type after-clause
        ^pred-type duration
        ^number <num>
        ^unit <unit>)
}

sp {task-utils*copy-task-operator*apply*copy-task-argument*after-clause*duration
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type after-clause
        ^pred-type duration
        ^number <num>
        ^unit <unit>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^arg-type after-clause
           ^predicate <new-pred>)
   (<new-pred> ^type duration
               ^number <num>
               ^unit <unit>)
}

