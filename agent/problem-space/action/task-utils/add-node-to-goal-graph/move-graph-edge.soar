
# If told to ^insert-before-node <node-h>
#   then move any edges pointing to that node to point to the new node instead
sp {add-node-to-goal-graph*elaborate*move-graph-edge*from*previous-node
   (state <s> ^name add-node-to-goal-graph
              ^created-node <node>
              ^insert-before-node <next-h>
              ^following-node {@ <next>}
              ^goal-graph-nodes.goal <prev>)
   (<node> ^handle <node-h>)
   (<prev> ^handle <> <node-h>
           ^next <edge>)
   (<edge> ^goal <next>)
-->
   (<s> ^move-graph-edge <move>)
   (<move> ^head <prev> 
           ^edge <edge>
           ^from <next>
           ^to <node>)
}

### PROPOSE: 1 operator for each ^move-graph-edge elaborated on the state
sp {add-node-to-goal-graph*propose*move-graph-edge
   (state <s> ^name add-node-to-goal-graph
              ^move-graph-edge <move>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move-graph-edge
        ^info <move>)
}

## APPLY: Replace the next pointer goal
sp {add-node-to-goal-graph*apply*move-graph-edge
   (state <s> ^name add-node-to-goal-graph
              ^operator <o>)
   (<o> ^name move-graph-edge
        ^info <info>)
   (<info> ^edge <edge>
           ^from <old>
           ^to <new>)
   (<edge> ^goal <old>)
-->
   (<edge> ^goal <old> -
           ^goal <new>)

   (<s> ^store-concept <edge>)
}
