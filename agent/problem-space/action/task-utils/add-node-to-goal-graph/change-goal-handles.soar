# We need to change the handles of all previous goals so that their chunks will no longer fire
#   for select-next-goal (want to learn new ones to select the pushed subgoal instead)

sp {add-node-to-goal-graph*elaborate*change-goal-handles 
   (state <s> ^name add-node-to-goal-graph
              ^insert-before-node <node-h>)
-->
   (<s> ^change-goal-handles <handles>)
   (<handles> ^changed false)
}

# Add any goals that point to the created node
sp {add-node-to-goal-graph*elaborate*change-goal-handles*before*created*node
   (state <s> ^name add-node-to-goal-graph
              ^created-node <node>
              ^goal-graph-nodes.goal <prev>
              ^change-goal-handles <handles>)
   (<prev> ^next.goal <node>)
-->
   (<handles> ^goal <prev>)
}

# Recurisvely add goals that point to other goals in the list
sp {add-node-to-goal-graph*elaborate*change-goal-handles*recursive*before
   (state <s> ^name add-node-to-goal-graph
              ^created-node.handle <node-h>
              ^goal-graph-nodes.goal <prev>
              ^change-goal-handles <handles>)
   (<handles> ^goal.handle <goal-h>)
   (<prev> ^handle <> <node-h>
           ^next.goal.handle <goal-h>)
-->
   (<handles> ^goal <prev>)
}

# Propose change-goal-handles if not changed yet
sp {add-node-to-goal-graph*propose*change-goal-handles*not*changed
   (state <s> ^name add-node-to-goal-graph
              ^change-goal-handles <handles>)
   (<handles> ^changed false)
-->
   (<s> ^operator <o> + =)
   (<o> ^name change-goal-handles)
}

# Apply - set changed to true
sp {add-node-to-goal-graph*apply*change-goal-handles*mark*changed
   (state <s> ^name add-node-to-goal-graph
              ^operator.name change-goal-handles
              ^change-goal-handles <handles>)
   (<handles> ^changed false)
-->
   (<handles> ^changed false -
              ^changed true)
}

# Apply - change start goals
sp {add-node-to-goal-graph*apply*change-goal-handle*start
   (state <s> ^name add-node-to-goal-graph
              ^operator.name change-goal-handles
              ^change-goal-handles.goal <goal>
              ^task-handle <task-h>)
   (<goal> ^item-type start-goal
           ^handle <old-h>)
-->
   (<goal> ^handle <old-h> -
           ^handle (make-constant-symbol <task-h> |start|))
   (<s> ^store-concept <goal>)
}

# Apply - change task goals
sp {add-node-to-goal-graph*apply*change-goal-handle*non-start
   (state <s> ^name add-node-to-goal-graph
              ^operator.name change-goal-handles
              ^change-goal-handles.goal <goal>
              ^task-handle <task-h>)
   (<goal> ^item-type task-goal
           ^handle <old-h>)
-->
   (<goal> ^handle <old-h> -
           ^handle (make-constant-symbol <task-h> |goal|))
   (<s> ^store-concept <goal>)
}

# Apply - remove learned flags
sp {add-node-to-goal-graph*apply*change-goal-handle*remove*learned*flag
   (state <s> ^name add-node-to-goal-graph
              ^operator.name change-goal-handles
              ^change-goal-handles.goal.next <next>)
   (<next> ^learned true)
-->
   (<next> ^learned true -)
   (<s> ^store-concept <next>)
}


