# Sometimes we have to retrieve the entire task goal graph

# If inserting the node before an existing one, retrieve the entire goal graph
sp {add-node-to-goal-graph*elaborate*retrieve-entire-goal-graph*insert-before-node
   (state <s> ^name add-node-to-goal-graph
              ^insert-before-node <node-h>)
-->
   (<s> ^retrieve-entire-goal-graph true)
}

# Retrieve the TCN
sp {add-node-to-goal-graph*elabrate*smem-query*task-concept-network*insert-before-node
   (state <s> ^name add-node-to-goal-graph
              ^retrieve-entire-goal-graph true
              ^task-handle <task-h>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue.handle <task-h>
        ^depth 2
        ^result-name task-concept-network
        ^destination <s>)
}

# Recurisvely retrieve all the nodes in the graph and elaborate onto ^goal-graph-nodes
sp {add-node-to-goal-graph*elaborate*goal-graph-nodes
   (state <s> ^name add-node-to-goal-graph
              ^retrieve-entire-goal-graph true)
-->
   (<s> ^goal-graph-nodes <goals>)
}

sp {add-node-to-goal-graph*elaborate*goal-graph-nodes*start*handle
   (state <s> ^name add-node-to-goal-graph
              ^task-concept-network.goal-graph <start>
              ^goal-graph-nodes <goals>)
   (<start> ^handle <goal-h>)
-->
   (<goals> ^retrieve <goal-h>)
}

sp {add-node-to-goal-graph*elaborate*smem-query*goal-graph-node*from*handle
   (state <s> ^name add-node-to-goal-graph
             -^created-node.handle <goal-h>
             -^change-goal-handles.changed true
              ^goal-graph-nodes <goals>)
   (<goals> ^retrieve <goal-h>
           -^goal.handle <goal-h>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 6
        ^destination <goals>
        ^result-name goal)
   (<cue> ^handle <goal-h>)
}

sp {add-node-to-goal-graph*elaborate*goal-graph-nodes*created-node
   (state <s> ^name add-node-to-goal-graph
              ^created-node <node>
              ^goal-graph-nodes <goals>)
-->
   (<goals> ^goal <node>)
}

sp {add-node-to-goal-graph*elaborate*goal-graph-nodes*next*goal-handle
   (state <s> ^name add-node-to-goal-graph
              ^goal-graph-nodes <goals>)
   (<goals> ^goal.next.goal.handle <next-goal-h>)
-->
   (<goals> ^retrieve <next-goal-h>)
}

