
### Operator create-intermediate-node
# Proposed if node-type = intermediate
# Creates a node with ^item-type intermediate-goal and adds it directly to the TCN as ^goal-graph <intermediate-node>
sp {add-node-to-goal-graph*propose*create-intermediate-node
   (state <s> ^name add-node-to-goal-graph
              ^node-type intermediate
              ^task-handle <task-h>
             -^created-node)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-intermediate-node
        ^task-handle <task-h>
        ^result-name created-node)
}

### Also proposed if ^add-else-link true flag is present
sp {add-node-to-goal-graph*propose*create-intermediate-node*add-else-link
   (state <s> ^name add-node-to-goal-graph
              ^add-else-link true
              ^task-handle <task-h>
             -^created-intermediate-node)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-intermediate-node
        ^task-handle <task-h>
        ^result-name created-intermediate-node)
}

sp {add-node-to-goal-graph*apply*create-intermediate-node
   (state <s> ^name add-node-to-goal-graph
              ^operator <o>)
   (<o> ^name create-intermediate-node
        ^task-handle <task-h>
        ^result-name <res-name>)
-->
   (<s> ^<res-name> <node>
        ^store-node <node>)
   (<node> ^handle (make-constant-symbol (concat <task-h> |int|)) ^item-type intermediate-goal)
}
