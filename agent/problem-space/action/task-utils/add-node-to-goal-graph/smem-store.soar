### Here we recursively expand the ^store-node and ^store-edge wmes
# and elaborate a bunch of ^store-concept <id> wmes
# to store the entire structure

sp {add-node-to-goal-graph*elaborate*store-node*from*created-node
   (state <s> ^name add-node-to-goal-graph
              ^created-node <node>)
-->
   (<s> ^store-node <node>)
}

##### EXPAND store-node

# store the node root
sp {add-node-to-goal-graph*store-node*elaborate*store-concept*root
   (state <s> ^name add-node-to-goal-graph
              ^store-node <node>)
-->
   (<s> ^store-concept <node>)
}

# store any node modifiers
sp {add-node-to-goal-graph*store-node*elaborate*store-concept*modifiers
   (state <s> ^name add-node-to-goal-graph
              ^store-node.modifiers <mods>)
-->
   (<s> ^store-concept <mods>)
}

# store each goal predicate
sp {add-node-to-goal-graph*store-node*elaborate*store-predicate
   (state <s> ^name add-node-to-goal-graph
              ^store-node.{ <n> << 1 2 3 4 5 6 7 8 9 >> } <pred>)
-->
   (<s> ^store-predicate <pred>)
}

##### EXPAND store-edge

# store the head + next pointer of an edge
sp {add-node-to-goal-graph*store-edge*elaborate*store-concept*edge
   (state <s> ^name add-node-to-goal-graph
              ^store-edge <se>)
   (<se> ^head <head>
         ^edge <edge>)
-->
   (<s> ^store-concept <head> <edge>)
}

# store any conditions on the edge
sp {add-node-to-goal-graph*store-edge*elaborate*store-concept*conditions
   (state <s> ^name add-node-to-goal-graph
              ^store-edge.edge.conditions <conds>)
-->
   (<s> ^store-concept <conds>)
}

# store each edge condition predicate
sp {add-node-to-goal-graph*store-edge*conditions*elaborate*store-predicate
   (state <s> ^name add-node-to-goal-graph
              ^store-edge.conditions.{ <n> << 1 2 3 4 5 6 7 8 9 >> } <pred>)
-->
   (<s> ^store-predicate <pred>)
}

##### EXPAND store-predicate

# store the predicate root
sp {add-node-to-goal-graph*store-predicate*elaborate*store-concept*root
   (state <s> ^name add-node-to-goal-graph
              ^store-predicate <pred>)
-->
   (<s> ^store-concept <pred>)
}

# store any predicate arguments
sp {add-node-to-goal-graph*store-predicate*elaborate*store-concept*argument
   (state <s> ^name add-node-to-goal-graph
              ^store-predicate.{ << id 1 2 3 4 5 >> } <val>)
-->
   (<s> ^store-concept <val>)
}

# store any predicate argument default values
sp {add-node-to-goal-graph*store-predicate*elaborate*store-concept*argument*default
   (state <s> ^name add-node-to-goal-graph
              ^store-predicate.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <val>)
-->
   (<s> ^store-concept <val>)
}


