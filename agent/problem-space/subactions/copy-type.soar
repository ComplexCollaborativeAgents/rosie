# problem-space.subactions.copy-type << deep shallow >>
#		Whether the subactions copy is shallow (elaborates the same identifier)
#			or deep (all substructure is deep copied)
#
# For deep copy implementation, see deep-copy-subactions.soar

# Copy-type shallow: just elaborate the same identifier
sp {problem-space*subactions*copy-type*shallow*elaborate*subactions
   (state <s> ^problem-space.subactions <subs>)
   (<subs> ^copy-type shallow
           ^source <source>)
-->
   (<s> ^subactions <source>)
}

# Copy-type deep: use the deep-copy-subactions structure to facilitate

# Note: For a deep copy of subactions, you must also have a deep copy of the world
sp {problem-space*subactions*copy-type*deep*elaborate*deep-copy-subactions
   (state <s> ^problem-space.subactions <subs>)
   (<subs> ^copy-type deep
           ^source <source>)
-->
   (<s> ^deep-copy-subactions <dcs>)
   (<dcs> ^source <source>)
}

# copy-type deep: copy the result onto the state from the deep-copy-subactions structure
sp {problem-space*subactions*copy-type*deep*elaborate*subactions*copy*from*deep-copy-subactions           
   (state <s> ^problem-space.subactions <subs>
              ^deep-copy-subactions <dcs>)
   (<subs> ^copy-type deep
           ^source <source>)
   (<dcs> ^source <source>
          ^copy <copy>)
-->
   (<s> ^subactions <copy>)
}
