sp {handle-new-whenever-task*elaborate*generalize-task-argument*conditional*predicates
   (state <s> ^name handle-new-whenever-task
              ^task-concept-network <tcn>)
   (<tcn> ^handle <task-handle>
          ^procedural <tcn-proc>)
   (<tcn-proc> ^op_name <op-name>)
-->
   (<s> ^reference-task-op <task-op>
        ^reference-tcn-proc <tcn-proc>)
   (<task-op> ^name <op-name>
              ^item-type task-operator
              ^task-handle <task-handle>)
}

sp {handle-new-whenever-task*elaborate*generalize-task-argument*object*reference-info
   (state <s> ^name handle-new-whenever-task
              ^generalize-task-argument <arg>
              ^reference-info <info>)
   (<arg> ^arg-type object
          ^instantiation <i>)
   (<i> ^id <obj>)
   (<info> ^referent <obj>)
-->
   (<i> ^reference-info <info>)
}

sp {handle-new-whenever-task*elaborate*generalize-task-argument*predicate-set*1*condition
   (state <s> ^name handle-new-whenever-task
              ^whenever-segment.task-operator.conditions <conds>)
-->
   (<s> ^generalize-task-argument <gen-arg>)
   (<gen-arg> ^arg-type predicate-set
              ^type goal-conditions
              ^instantiation <conds>)
}

sp {handle-new-whenever-task*elaborate*general-conditions
   (state <s> ^name handle-new-whenever-task
              ^generalize-task-argument <gen-arg>)
   (<gen-arg> ^arg-type predicate-set
              ^type goal-conditions
              ^general-arg-rep <conds>)
-->
   (<s> ^general-conditions <conds>)
}
