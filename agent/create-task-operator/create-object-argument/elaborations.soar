sp {create-object-argument*elaborate*world*from*superstate
   (state <s> ^name create-object-argument
              ^superstate.world <w>)
-->
   (<s> ^world <w>)
}

sp {create-object-argument*elaborate*command-message*from*superstate
   (state <s> ^name create-object-argument
              ^superstate.command-message <msg>)
-->
   (<s> ^command-message <msg>)
}

sp {create-object-argument*elaborate*task-concept-network*from*superstate
   (state <s> ^name create-object-argument
              ^superstate.task-concept-network <tcn>)
-->
   (<s> ^task-concept-network <tcn>)
}

###################### argument-info #############################

sp {create-object-argument*elaborate*argument-info*from*superoperator
   (state <s> ^name create-object-argument
              ^superstate.operator.argument-info <info>)
-->
   (<s> ^argument-info <info>)
}

### procedural <parg> - from procedural.<arg-name>
sp {create-object-argument*elaborate*argument-info*procedural
   (state <s> ^name create-object-argument
              ^argument-info <arg-info>
              ^task-concept-network.procedural.<arg-name> <parg>)
   (<arg-info> ^arg-name <arg-name>)
-->
   (<arg-info> ^procedural <parg>)
}

### arg-status - copy from procedural
sp {create-object-argument*elaborate*argument-info*arg-status*from*procedural
   (state <s> ^name create-object-argument
              ^argument-info <arg-info>)
   (<arg-info> ^procedural.arg-status <status>)
-->
   (<arg-info> ^arg-status <status>)
}

# object arg-status: default = real
sp {create-object-argument*elaborate*argument-info*arg-status*default*real
   (state <s> ^name create-object-argument
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type object
              -^procedural.arg-status)
-->
   (<arg-info> ^arg-status real)
}

### object: The object being added as an argument
sp {create-object-argument*elaborate*argument-info*object
   (sta


## argument-info for messages (say or ask commands)
sp {create-object-argument*elaborate*argument-info*message
   (state <s> ^name create-object-argument
              ^command-message <msg>)
   (<msg> ^action.handle << say1 ask1 >>
          ^arg1.sentence <sentence>)
-->
  (<s> ^argument-info <arg-info>)
  (<arg-info> ^arg-name arg1
              ^arg-type object
              ^id <new-obj>)
   (<new-obj> ^handle (make-constant-symbol |obj-hand|)
   	          ^predicates <obj-preds>)
   (<obj-preds> ^category message
                ^sentence <sentence>)
}

## argument-info for objects 
sp {create-object-argument*elaborate*argument-info*object
   (state <s> ^name create-object-argument
              ^command-message <msg>)
  (<msg> ^<arg-name> <arg>)
  (<arg> ^item-type object
         -^linked-to)
-->
  (<s> ^argument-info <arg-info>)
  (<arg-info> ^arg-name <arg-name>
              ^arg-type object
              ^id <arg>)
}

# linked objects
sp {create-object-argument*elaborate*argument-info*object*linked
   (state <s> ^name create-object-argument
              ^command-message <msg>)
  (<msg> ^<arg-name> <arg>)
  (<arg> ^item-type object
         ^linked-to <other>)
-->
  (<s> ^argument-info <arg-info>)
  (<arg-info> ^arg-name <arg-name>
              ^arg-type object
              ^id <other>)
}
