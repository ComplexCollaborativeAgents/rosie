

sp {attend-to-world-change*apply*attend-to-change-object-predicate*add*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-world-change
        ^change <change>)
   (<change> ^type change-object-predicate
             ^object-info.wm-obj.predicates <obj-preds>
             ^property-handle <prop>
             ^predicate-handle <pred>)
-->
   (<obj-preds> ^<prop> <pred>)
}

sp {attend-to-world-change*apply*attend-to-change-object-predicate*remove*old*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-world-change
        ^change <change>)
   (<change> ^type change-object-predicate
             ^object-info.wm-obj.predicates <obj-preds>
             ^property-handle <prop>
             ^predicate-handle <pred>)
   (<obj-preds> ^<prop> { <old-pred> <> <pred> })
-->
   (<obj-preds> ^<prop> <old-pred> -)
}

# if print-perception is on, print a message
sp {attend-to-world-change*apply*attend-to-change-object-predicate*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-world-change
        ^change <change>)
   (<change> ^type change-object-predicate
             ^object-info.object-handle <handle>
             ^property-handle <prop>
             ^predicate-handle <pred>)
-->
   (write |attend-to-world-change(change-object-predicate): Object | <handle> | - changing predicate | <prop> | to | <pred> (crlf))
}
