sp {construct-world-object*propose*initialize-object
   (state <s> ^name construct-world-object
             -^new-object-rep.handle <h>)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialize-object)
}

sp {construct-world-object*apply*initialize-object*create*new*rep
   (state <s> ^name construct-world-object
              ^operator.name initialize-object)
-->
   (<s> ^new-object-rep <rep>)
   (<rep> ^item-type object
          ^predicates <preds>)
}

sp {construct-world-object*apply*initialize-object*copy*handle
   (state <s> ^name construct-world-object
              ^operator.name initialize-object
              ^new-object-rep <rep>
              ^object-handle <h>)
-->
   (<rep> ^handle <h>)
}

sp {construct-world-object*apply*initialize-object*mark*in-smem
   (state <s> ^name construct-world-object
              ^operator.name initialize-object
              ^smem-obj <smem>
              ^new-object-rep <rep>)
-->
   (<rep> ^in-smem true)
}

sp {construct-world-object*apply*initialize-object*mark*arm-status*not-grabbed
   (state <s> ^name construct-world-object
              ^operator.name initialize-object
              ^agent-params.domain <> ai2thor
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^arm-status not-grabbed)
}

sp {construct-world-object*apply*initialize-object*mark*arm-status*not-grabbed*ai2thor
   (state <s> ^name construct-world-object
              ^operator.name initialize-object
              ^agent-params.domain ai2thor
              ^new-object-rep.predicates <preds>
              ^object-info.properties.property-info.predicate-handle grabbable)
-->
   (<preds> ^arm-status not-grabbed)
}

sp {construct-world-object*apply*initialize-object*mark*object-status*confirmed
   (state <s> ^name construct-world-object
              ^operator.name initialize-object
              ^source-type perception-obj
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^object-status confirmed
            ^visible true)
}

sp {construct-world-object*apply*initialize-object*mark*object-status*unconfirmed
   (state <s> ^name construct-world-object
              ^operator.name initialize-object
              ^source-type <> perception-obj
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^object-status unconfirmed
            ^visible false)
}

sp {construct-world-object*apply*initialize-object*internal*mark*inreach
   (state <s> ^name construct-world-object
              ^operator.name initialize-object
              ^agent-params.domain internal
              ^source-type perception-obj
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^inreach inreach)
}

