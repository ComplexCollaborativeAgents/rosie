#########################################################
#
# Discrepancy: new-perception-object
#   There is an object in perception that is not in belief
# Params: 
#   perception-obj <obj> - The perception object (from input-link)
# Handling:
#   attend-to-perception: new-perception-object
#
#########################################################

sp {perception-monitor*detect-discrepancies*new-perception-object
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^input-link.objects.object <il-obj>
               ^object-monitor <obj-mon>
               ^discrepancies <discs>)
   (<obj-mon> -^object-info.perception-obj <il-obj>)
-->
   (<discs> ^new-perception-object <disc>)
   (<disc> ^perception-obj <il-obj>)
}

sp {detect-discrepancies*new-perception-object*elaborate*ignore*if*stability*timer*unsatisfied
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>
              ^io.input-link.time.seconds <t>)
   (<perc-mon> ^object-monitor.new-object-info <obj-info>
               ^discrepancies.new-perception-object <disc>)
   (<disc> ^perception-obj <perc-obj>)
   (<obj-info> ^perception-obj <perc-obj>
               ^check-stability-time > <t>)
-->
   (<disc> ^ignore true)
}

