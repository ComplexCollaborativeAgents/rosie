#########################################################
#
# Discrepancy: different-arm-status
#   The arm-status predicate on the object doesn't match the input-link
# Handling: update the predicate on the object via world-change=change-object-predicate
#
#########################################################

sp {detect-discrepancies*different-arm-status*elaborate*perception-monitor*world-changes*change-object-predicate*grabbed
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^world-changes <changes>
               ^input-link.self.arm.holding-object <id>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^perception-id <id>
              -^wm-obj.predicates.arm-status grabbed)
-->
   (<changes> ^change-object-predicate <cop>)
   (<cop> ^object-info <obj-info>
          ^property-handle arm-status
          ^predicate-handle grabbed)
}

sp {detect-discrepancies*different-arm-status*elaborate*perception-monitor*world-changes*change-object-predicate*not-grabbed
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^world-changes <changes>
               ^input-link.self.arm <arm>
               ^object-monitor.object-info <obj-info>)
   (<arm> -^holding-object <id>)
   (<obj-info> ^perception-id <id>
               ^wm-obj.predicates <preds>)
   (<preds> ^is-grabbable1 grabbable1 
            ^arm-status grabbed)
-->
   (<changes> ^change-object-predicate <cop>)
   (<cop> ^object-info <obj-info>
          ^property-handle arm-status
          ^predicate-handle not-grabbed)
}

