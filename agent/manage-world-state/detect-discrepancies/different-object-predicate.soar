#########################################################
#
# Discrepancy: new-object-predicate
#   The object-predicate predicate on the object doesn't match the input-link
# Handling: attend-to-perception/new-object-predicate
#
#########################################################

# For visual properties, we make sure the object is not occluded
sp {perception-monitor*detect-discrepancies*new-object-predicate*visual
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^discrepancies <discs>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^properties.property-info <prop-info>
               ^wm-obj.predicates <wm-preds>
               ^status.is-occluded false)
   (<prop-info> ^property-handle <prop>
                ^type visual
                ^predicate-handle <pred>)
   (<wm-preds> -^<prop> <pred>)
-->
   (<discs> ^new-object-predicate <disc>)
   (<disc> ^object-info <obj-info>
           ^property-handle <prop>
           ^predicate-handle <pred>)
}

sp {perception-monitor*detect-discrepancies*new-object-predicate*non-visual
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^discrepancies <discs>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^properties.property-info <prop-info>
               ^wm-obj.predicates <wm-preds>)
   (<prop-info> ^property-handle <prop>
               -^type visual
                ^predicate-handle <pred>)
   (<wm-preds> -^<prop> <pred>)
-->
   (<discs> ^new-object-predicate <disc>)
   (<disc> ^object-info <obj-info>
           ^property-handle <prop>
           ^predicate-handle <pred>)
}

