#########################################################
#
# Discrepancy: object-visibility
#   The visibilitiy predicate on the object doesn't match perception
# Solution: update the predicate on the object via world-change=change-object-predicate
#
#########################################################

sp {top-state*perception-monitor*discrepancy*object-visibility*elaborate*change*change-object-predicate*visible
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^world-changes <changes>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^status.is-visible1 visible1
              -^wm-obj.predicates.is-visible1 visible1)
-->
   (<changes> ^change <change>)
   (<change> ^type change-object-predicate
             ^object-info <obj-info>
             ^property-handle is-visible1
             ^predicate-handle visible1)
}

sp {top-state*perception-monitor*discrepancy*object-visibility*elaborate*change*change-object-predicate*not-visible
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^world-changes <changes>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^status.is-visible1 not-visible1
              -^wm-obj.predicates.is-visible1 not-visible1)
-->
   (<changes> ^change <change>)
   (<change> ^type change-object-predicate
             ^object-info <obj-info>
             ^property-handle is-visible1
             ^predicate-handle not-visible1)
}

