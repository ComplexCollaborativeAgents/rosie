######## retrieve-smem-object ############
sp {delete-object*propose*retrieve-smem-object*from*epmem
   (state <s> ^name delete-object
              ^object-info.object-handle <obj-handle>
              ^object-info.wm-obj.in-smem true
             -^smem-obj)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
   (<cue> ^handle <obj-handle>)
}

sp {delete-object*apply*retrieve-smem-object*create*query
   (state <s> ^name delete-object
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
-->
   (<cmd> ^query <cue>)
}

sp {delete-object*apply*retrieve-smem-object*success*copy*object
   (state <s> ^name delete-object
              ^operator <o>
              ^smem.result <res>
              ^smem.command <cmd>)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
   (<res> ^success <cue>
          ^retrieved <obj-lti>)
-->
   (<s> ^smem-obj <obj-lti>)
   (<cmd> ^query <cue> -)
}
        
sp {delete-object*apply*retrieve-smem-object*failure*copy*object
   (state <s> ^name delete-object
              ^operator <o>
              ^smem.result.failure <cue>
              ^smem.command <cmd>)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
-->
   (<s> ^smem-obj none)
   (<cmd> ^query <cue> -)
}

#############3 update-smem-object ################
sp {delete-object*propose*update-smem-object
   (state <s> ^name delete-object
              ^smem-obj <> none
             -^updated-smem-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-smem-object)
}

sp {delete-object*elaborate*state*object-rep-info
   (state <s> ^name delete-object)
-->
   (<s> ^object-rep-info <rep>)
   (<rep> ^copy-level1 waypoint
          ^ignore-level1 handle item-type object-instance predicates in-smem room1 storage1
          ^ignore-level2 arm-status is-confirmed1 is-visible1 is-reachable1 dialog-object handle modifier1)
}

sp {delete-object*apply*update-smem-object*store*smem
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem.command <cmd>
              ^smem-obj <obj>)
-->
   (<cmd> ^store <obj>)
}

sp {delete-object*apply*update-smem-object*mark*updated
   (state <s> ^name delete-object
              ^operator.name update-smem-object)
-->
   (<s> ^updated-smem-object true)
}

# HARDCODED

# Copy everything under wm-obj listed in object-rep-info.copy-level1
sp {delete-object*apply*update-smem-object*add*level1*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
              ^object-info.wm-obj.<prop> <pred>
              ^object-rep-info <obj-rep>)
   (<obj-rep> ^copy-level1 <prop>)
-->
   (<obj> ^<prop> <pred>)
}

# Copy everything under wm-obj.predicates not listed in object-rep-info.ignore-level2
sp {delete-object*apply*update-smem-object*add*level2*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
              ^object-info.wm-obj.predicates.<prop> <pred>
              ^object-rep-info <obj-rep>)
   (<obj-rep> -^ignore-level2 <prop>)
-->
   (<obj> ^<prop> <pred>)
}

# Remove from smem object if not on wm-obj and listed in object-rep-info.copy-level1
sp {delete-object*apply*update-smem-object*remove*level1*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
             -^object-info.wm-obj.<prop> <pred>
              ^object-rep-info <obj-rep>)
   (<obj> ^<prop> <pred>)
   (<obj-rep> ^copy-level1 <prop>)
-->
   (<obj> ^<prop> <pred> -)
}

# Remove from smem object if not on wm-obj.predicates and not on ignore/copy list
sp {delete-object*apply*update-smem-object*remove*level2*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
             -^object-info.wm-obj.predicates.<prop> <pred>
              ^object-rep-info <obj-rep>)
   (<obj> ^<prop> <pred>)
   (<obj-rep> -^copy-level1 <prop>
              -^ignore-level1 <prop> 
              -^ignore-level2 <prop>)
-->
   (<obj> ^<prop> <pred> -)
}
        
