#### change-object-predicate
# Change the predicate on the object

sp {anystate*apply*change-world-state*change-object-predicate*add*predicate
   (state <s> ^operator <o>)
   (<o> ^name change-world-state
        ^change-object-predicate <chg>)
   (<chg> ^object-info.wm-obj.predicates <obj-preds>
          ^property-handle <prop>
          ^predicate-handle <pred>)
-->
   (<obj-preds> ^<prop> <pred>)
}

sp {anystate*apply*change-world-state*change-object-predicate*remove*old*predicate
   (state <s> ^operator <o>)
   (<o> ^name change-world-state
        ^change-object-predicate <chg>)
   (<chg> ^object-info.wm-obj.predicates <obj-preds>
          ^property-handle <prop>
          ^predicate-handle <pred>)
   (<obj-preds> ^<prop> { <old-pred> <> <pred> })
-->
   (<obj-preds> ^<prop> <old-pred> -)
}

# If print-perception is on, print a message
sp {anystate*apply*change-world-state*change-object-predicate*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name change-world-state
        ^change-object-predicate <chg>)
   (<chg> ^object-info.object-handle <obj-handle>
          ^property-handle <prop>
          ^predicate-handle <pred>)
-->
   (write |change-world-state: change-object-predicate on object | <obj-handle> | to | <prop> |=| <pred> (crlf))
}
   

