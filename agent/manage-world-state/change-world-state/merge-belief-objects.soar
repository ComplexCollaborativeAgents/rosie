### merge-belief-objects
# Tell percetion to merge the given world objects 
# Tell belief to merge the given objects via a modify-scene command on the output-link

sp {anystate*change-world-state*merge-belief-objects*elaborate*delete-object*merging-object
   (state <s> ^operator <o> +)
   (<o> ^name change-world-state
        ^merge-belief-objects <merge>)
   (<merge> ^merging-object-info <obj-info>)
-->
   (<o> ^delete-object <obj-info>)
}

sp {anystate*apply*change-world-state*merge-belief-objects*copy*obj*predicates
   (state <s> ^operator <o>)
   (<o> ^name change-world-state
        ^merge-belief-objects <merge>)
   (<merge> ^merging-object-info.wm-obj.predicates.<prop> <pred>
            ^core-object-info.wm-obj.predicates <core-preds>)
-->
   (<core-preds> ^<prop> <pred>)
}

sp {anystate*apply*change-world-state*merge-belief-objects*replace*predicate*info
   (state <s> ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name change-world-state
        ^merge-belief-objects <merge>)
   (<merge> ^merging-object-info.wm-obj <obj>
            ^core-object-info.wm-obj <core>)
   (<pred> ^instance <i>)
   (<i> ^<num> <obj>)
-->
   (<pred> ^instance <i>)
   (<i> ^<num> <obj> -
        ^<num> <core>)
}

sp {anystate*apply*change-world-state*merge-belief-objects*mark*merged-into
   (state <s> ^operator <o>)
   (<o> ^name change-world-state
        ^merge-belief-objects <merge>)
   (<merge> ^merging-object-info.wm-obj <obj>
            ^core-object-info.wm-obj <core>)
-->
   (<obj> ^merged-into <core>)
}

