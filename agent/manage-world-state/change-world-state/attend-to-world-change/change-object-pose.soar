##### update-object-pose
# Change the pose of the belief object in SVS to that of the perception version



## There is no perception-obj, so remove that 
#sp {attend-to-world-change*update-object-pose*remove*update*flag*no*perception
#   (state <s> ^operator <o>
#              ^top-state.svs.command <cmd>)
#   (<o> ^name attend-to-world-change
#        ^change-type update-object-pose
#        ^change-info.object-info <obj-info>)
#   (<obj-info> -^perception-obj <any>
#               -^wm-obj.predicates.arm-status grabbed
#                ^update-pose true)
#-->
#   (<obj-info> ^update-pose true -)
#}


# Send command to svs (not grabbed)
sp {attend-to-world-change*update-object-pose*svs*copy_node
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-world-change
        ^change-type update-object-pose
        ^change-info.object-info <obj-info>)
   (<obj-info> ^perception-obj.id <perc-id>
               ^belief-obj.id <bel-id>
               -^wm-obj.predicates.arm-status grabbed)
-->
   (<cmd> ^copy_transform <copy>)
   (<copy> ^source <perc-id>
           ^destination <bel-id>
           ^position yes
           ^rotation yes
           ^scale yes)
   (<obj-info> ^copy-command <copy>)
}

# Send the command to svs
sp {attend-to-world-change*update-object-pose*svs*copy_node*grabbed
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-world-change
        ^change-type update-object-pose
        ^change-info.object-info <obj-info>)
   (<obj-info> ^belief-obj.id <bel-id>
               ^wm-obj.predicates.arm-status grabbed)
-->
   (<cmd> ^copy_transform <copy>)
   (<copy> ^source robot
           ^destination <bel-id>
           ^position yes
           ^rotation no
           ^scale no)
   (<obj-info> ^copy-command <copy>)
}

# Once the command is finished, clean up the flags
sp {attend-to-world-change*update-object-pose*cleanup*flag
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-world-change
        ^change-type update-object-pose
        ^change-info.object-info <obj-info>)
   (<obj-info> ^update-pose true
               ^copy-command.status success)
-->
   (<obj-info> ^update-pose true -)
}

# Once the command is finished, clean up the flags
sp {attend-to-world-change*update-object-pose*cleanup*command
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-world-change
        ^change-type update-object-pose
        ^change-info.object-info <obj-info>)
   (<obj-info> ^copy-command <copy>)
   (<copy> ^status success)
-->
   (<obj-info> ^copy-command <copy> -)
   (<cmd> ^copy_transform <copy> -)
}
   
# If print-perception is on, print a message
sp {attend-to-world-change*update-object-pose*print-perception*print
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-world-change
        ^change-type update-object-pose
        ^change-info.object-info.object-handle <obj-handle>)
-->
   (write |Object | <obj-handle> |: updated pose| (crlf))
}
   
