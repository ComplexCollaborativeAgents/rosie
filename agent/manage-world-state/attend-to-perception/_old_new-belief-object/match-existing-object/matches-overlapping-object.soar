# Propose matches-overlapping-object for each object overlapping the perception object
sp {matches-known-object*propose*matches-overlapping-object
   (state <s> ^name match-known-object
              ^overlap-results.result <res>)
   (<res> ^overlapper-info <obj-info>
          ^overlap <val>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name matches-overlapping-object
        ^overlapping-object <obj-info>
        ^overlap <val>)
}

# If matching a visible object, the overlap must exceed the contained-threshold to count
sp {match-existing-object*reject*matches-overlapping-object*not*enough*overlap
   (state <s> ^name match-existing-object
              ^operator <o> +
              ^agent-params.contained-threshold <thresh>)
   (<o> ^name matches-overlapping-object
        ^overlapping-object.status.is-visible1 visible1
        ^overlap < <thresh>)
-->
   (<s> ^operator <o> -)
}

# Prefer more overlap when matching
sp {match-existing-object*prefer*matches-overlapping-object*with*more*overlap
   (state <s> ^name match-existing-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name matches-overlapping-object
         ^overlap <val1>)
   (<o2> ^name matches-overlapping-object
         ^overlap {<val2> < <val1>})
-->
   (<s> ^operator <o1> > <o2>)
}

# Report the match to the superstate
sp {matches-known-object*apply*matches-overlapping-object
   (state <s> ^name match-known-object
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name matches-overlapping-object
        ^overlapping-object <obj-info>)
-->
   (<ss> ^matches-existing-object <obj-info>)
}


