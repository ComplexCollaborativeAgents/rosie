sp {attend-to-perception*shrunken-object*propose*link-objects
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^found-contained-fragment true
              ^object-info.object-handle <dest-handle>)
-->
   (<s> ^operator <o> +)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^type link
          ^destination-handle <dest-handle>)
}

sp {attend-to-perception*shrunken-object*elaborate*link-objects*source-handle
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^operator <o> +
              ^contained-fragment-id <src-handle>)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
-->
   (<cmd> ^source-handle <src-handle>)
}

# Need to do all at once, otherwise only 1 fragment will be linked
sp {attend-to-perception*shrunken-object*apply*link-objects*modify-scene
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
-->
   (<ol> ^modify-scene <cmd>)
}

sp {attend-to-perception*shrunken-object*apply*link-objects*print-perception
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^agent-params.print-perception true
              ^operator <o>)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^source-handle <source>
          ^destination-handle <dest>)
-->
   (write |Telling perception to merge | <source> | with existing object | <dest> (crlf))
}

# It's possible this link will not remove the discrepancy
#   therefore we will remove the filter results and try again

sp {attend-to-perception*shrunken-object*apply*link-objects*reset*substate
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^operator <o>
              ^intersect-command <int-cmd>
              ^overlap-results <over-res>)
   (<o> ^name link-objects
        ^modify-scene-command.status complete)  
-->
   (<s> ^intersect-command <int-cmd> -
        ^overlap-results <over-res> -
        ^overlap-results <new-over-res>)
} 
 

        
