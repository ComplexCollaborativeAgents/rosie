# new-object-predicate
#   We simply add the predicate on the object

sp {anystate*apply*attend-to-perception*new-object-predicate*add*new*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-perception
        ^discrepancy-type new-object-predicate
        ^discrepancy-info <info>)
   (<info> ^object-info.wm-obj.predicates <obj-preds>
           ^property-handle <prop>
           ^predicate-handle <pred>)
-->
   (<obj-preds> ^<prop> <pred>)
}

sp {anystate*apply*attend-to-perception*new-object-predicate*remove*old*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-perception
        ^discrepancy-type new-object-predicate
        ^discrepancy-info <info>)
   (<info> ^object-info.wm-obj.predicates <obj-preds>
           ^property-handle <prop>
           ^predicate-handle <pred>)
   (<obj-preds> ^<prop> { <old-pred> <> <pred> })
-->
   (<obj-preds> ^<prop> <old-pred> -)
}

# if print-perception is on, print a message
sp {anystate*apply*attend-to-perception*new-object-predicate*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-perception
        ^discrepancy-type new-object-predicate
        ^discrepancy-info <info>)
   (<info> ^object-info.object-handle <handle>
           ^property-handle <prop>
           ^predicate-handle <pred>)
-->
   (write |attend-to-perception(new-object-predicate): On object | <handle> |: | <prop> |=| <pred> (crlf))
}
