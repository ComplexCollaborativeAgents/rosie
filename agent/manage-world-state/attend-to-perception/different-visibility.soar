# different-visibility
#   We simply update the is-visible1 predicate on the object

sp {anystate*apply*attend-to-perception*different-visibility*add*new*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-visibility
        ^discrepancy-info <info>)
   (<info> ^object-info.wm-obj.predicates <obj-preds>
           ^is-visible1 <vis>)
-->
   (<obj-preds> ^is-visible1 <vis>)
}

sp {anystate*apply*attend-to-perception*different-visibility*remove*old*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-visibility
        ^discrepancy-info <info>)
   (<info> ^object-info.wm-obj.predicates <obj-preds>
           ^is-visible1 <vis>)
   (<obj-preds> ^is-visible1 { <old-vis> <> <vis> })
-->
   (<obj-preds> ^is-visible1 <old-vis> -)
}

# if print-perception is on, print a message
sp {anystate*apply*attend-to-perception*different-visibility*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-visibility
        ^discrepancy-info <info>)
   (<info> ^object-info.object-handle <handle>
           ^is-visible1 <vis>)
-->
   (write |attend-to-perception(different-visibility): Object | <handle> | is now | <vis> (crlf))
}
