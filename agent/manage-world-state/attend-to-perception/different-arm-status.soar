# different-arm-status
#   We simply update the arm-status predicate on the object

sp {anystate*apply*attend-to-perception*different-arm-status*add*new*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-arm-status
        ^discrepancy-info <info>)
   (<info> ^object-info.wm-obj.predicates <obj-preds>
           ^arm-status <status>)
-->
   (<obj-preds> ^arm-status <status>)
}

sp {anystate*apply*attend-to-perception*different-arm-status*remove*old*predicate
   (state <s> ^operator <o>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-arm-status
        ^discrepancy-info <info>)
   (<info> ^object-info.wm-obj.predicates <obj-preds>
           ^arm-status <status>)
   (<obj-preds> ^arm-status { <old-status> <> <status> })
-->
   (<obj-preds> ^arm-status <old-status> -)
}

# if print-perception is on, print a message
sp {anystate*apply*attend-to-perception*different-arm-status*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-arm-status
        ^discrepancy-info <info>)
   (<info> ^object-info.object-handle <handle>
           ^arm-status <status>)
-->
   (write |attend-to-perception(different-arm-status): Object | <handle> | is now | <status> (crlf))
}
