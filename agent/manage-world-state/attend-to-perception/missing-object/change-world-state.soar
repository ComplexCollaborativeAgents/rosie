### If the object is contained by another perception-object
# Then set its perception-id to that of the containing object
sp {attend-to-perception*missing-object*propose*change-world-state*change-perception-id
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^object-info <obj-info>
              ^contained-by.perception-id <perc-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name change-world-state
        ^change-perception-id <chg>)
   (<chg> ^object-info <obj-info>
          ^perception-id <perc-id>)
}


# If we cannot determine a good reason why the object is missing, remove it from the world
# (implemented in manage-world-state/change-world-state)
sp {attend-to-perception*missing-object*propose*change-world-state*delete-object
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^object-info <obj-info>
              ^object-occluded false)
-->
   (<s> ^operator <o> +)
   (<o> ^name change-world-state
        ^delete-object <obj-info>)
}


