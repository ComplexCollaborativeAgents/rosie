              
# If we are doing segmentation reasoning, link multiple fragments
# All new perceptual objects that the missing object contains are linked to it
sp {attend-to-perception*missing-object*propose*link-objects
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^found-contained-fragment true
              ^object-info.object-handle <dest-handle>)
-->
   (<s> ^operator <o> +)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^type link
          ^destination-handle <dest-handle>)
}

sp {attend-to-perception*missing-object*elaborate*link-objects*src-handle
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^operator <o> +
              ^contained-fragment-id <src-handle>)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
-->
   (<cmd> ^source-handle <src-handle>)
}

# Need to do all at once, otherwise only 1 fragment will be linked
sp {attend-to-perception*missing-object*apply*link-objects
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
-->
   (<ol> ^modify-scene <cmd>)
}

sp {attend-to-perception*missing-object*apply*link-objects*monitor*perception
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^source-handle <src>
          ^destination-handle <dest>)
-->
   (write |Telling perception to merge | <src> | with existing object | <dest> (crlf))
}

# It's possible this link will not remove the discrepancy
#   therefore we will remove the filter results and try again

sp {attend-to-perception*missing-object*apply*link-objects*reset*substate
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^operator <o>
              ^intersect-command <int-cmd>
              ^overlap-results <over-res>)
   (<o> ^name link-objects
        ^modify-scene-command.status complete)  
-->
   (<s> ^intersect-command <int-cmd> -
        ^overlap-results <over-res> -
        ^overlap-results <new-over-res>)
} 
 
