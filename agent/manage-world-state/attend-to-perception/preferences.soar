# Reject an attend-to-perception operator if attending to an object being moved

sp {anystate*reject*attend-to-perception*moving*object
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^change <chg>)
   (<chg> ^info.category object
          ^object-info.wm-obj.predicates.arm-status << moving grabbed >>
          ^type <> newly-visible-object <> newly-not-visible-object <> changed-arm-status)
-->
   (<s> ^operator <o> -)
}


### Ordering of attend-to-perception operators

sp {anystate*attend-to-perception*prefer*higher*preference-order
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-perception
         ^preference-order <pref1>)
   (<o2> ^name attend-to-perception
         ^preference-order < <pref1>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {anystate*attend-to-perception*prefer*equal*same*preference-order
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-perception
         ^preference-order <pref>)
   (<o2> ^name attend-to-perception
         ^preference-order <pref>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {anystate*attend-to-perception*elaborate*default*preference-order
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
       -^preference-order > 0)
-->
   (<o> ^preference-order 0)
}


# Preference Orders:

# 190 new-perception-object
# 180 different-robot-status
# 160 different-waypoint 
# 120 moved-object
# 100 different-arm-status
# 100 different-visibility
# 100 new-object-predicate
#  80 missing-object
#  70 shrunken-object
#  60 grown-object

# 190 new-perception-object, new-object
sp {anystate*scene-change*attend*preference*190
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type new-perception-object)
-->
   (<o> ^preference-order 190)
}

# 180 different-robot-status
sp {anystate*scene-change*attend*preference*180
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-robot-status)
-->
   (<o> ^preference-order 180)
}

# 160 different-waypoint
sp {anystate*scene-change*attend*preference*160
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-waypoint)
-->
   (<o> ^preference-order 160)
}

# 120 moved-object
sp {anystate*scene-change*attend*preference*120
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type << moved-object >>)
-->
   (<o> ^preference-order 120)
}

# 100 new-object-predicate
# 100 different-visibility 
# 100 different-arm-status
sp {anystate*scene-change*attend*preference*100
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type << new-object-predicate different-visibility different-arm-status >>)
-->
   (<o> ^preference-order 100)
}

#  80 missing-object
sp {anystate*scene-change*attend*preference*80
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type << missing-object >>)
-->
   (<o> ^preference-order 80)
}

#  70 shrunken-object
sp {anystate*scene-change*attend*preference*70
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type << shrunken-object >>)
-->
   (<o> ^preference-order 70)
}

#  60 grown-object
sp {anystate*scene-change*attend*preference*60
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-perception
        ^discrepancy-type << grown-object >>)
-->
   (<o> ^preference-order 60)
}
