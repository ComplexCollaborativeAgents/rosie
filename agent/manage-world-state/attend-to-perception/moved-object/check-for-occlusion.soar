### check-for-occlusion
# If the object was moved, we need to see if we can trust the new position
#   by checking to see whether it is occluded

# For some domains, occlusion doesn't affect positions
sp {attend-to-perception*moved-object*elaborate*object-occluded*false*simulated*domains
   (state <s> ^name attend-to-perception
              ^discrepancy-type moved-object
              ^agent-params.domain << magicbot cozmo >>)
-->
   (<s> ^object-occluded false)
}

# Otherwise, we check for occlusion (agent/svs-command-operators)
sp {attend-to-perception*moved-object*propose*check-for-occlusion
   (state <s> ^name attend-to-perception
              ^discrepancy-type moved-object
              ^object-info <obj-info>
             -^object-occluded <flag>)
-->
   (<s> ^operator <o> +)
   (<o> ^name check-for-occlusion
        ^object-info <obj-info>
        ^report-to-superstate true
        ^report-to-top-state true)
}
