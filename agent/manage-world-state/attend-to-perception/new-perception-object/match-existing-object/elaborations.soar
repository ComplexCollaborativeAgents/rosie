
# Elaborate the source object information from the superoperator
sp {match-existing-object*elaborate*source*info*from*superoperator
   (state <s> ^name match-existing-object
              ^superstate.operator <ssop>)
   (<ssop> ^source-type <type>
           ^source-obj <obj>)
-->
   (<s> ^source-type <type>
        ^source-obj <obj>)
}

sp {match-existing-object*elaborate*match-missing-object*superstate
   (state <s> ^name match-existing-object
              ^superstate.candidate-for-missing-object <obj>)
-->
   (<s> ^match-missing-object <obj>)
}

sp {match-existing-object*elaborate*object-handle*from*source-obj
   (state <s> ^name match-existing-object
              ^source-obj.{ << handle object-handle >> } <h>)
-->
   (<s> ^object-handle <h>)
}

# Elaborate the object-monitor wme from the top-state
sp {match-existing-object*elaborate*object-monitor*from*top-state
   (state <s> ^name match-existing-object
              ^top-state.perception-monitor.object-monitor <omon>)
-->
   (<s> ^object-monitor <omon>)
}


### Elaborate the predicates describing the object to match

sp {match-existing-object*elaborate*new-object-predicates
   (state <s> ^name match-existing-object)
-->
   (<s> ^new-object-predicates <preds>)
}

sp {match-existing-object*elaborate*new-object-predicates*predicate*perception
   (state <s> ^name match-existing-object
              ^source-type perception
              ^source-obj.property <prop>
              ^new-object-predicates <new-preds>)
   (<prop> ^property-handle <prop-h>
           ^predicate-handle <pred-h>) 
-->
   (<new-preds> ^<prop-h> <pred-h>)
}

sp {match-existing-object*elaborate*new-object-predicates*predicate*belief
   (state <s> ^name match-existing-object
              ^source-type belief
              ^source-obj.predicates.<prop-h> <pred-h>
              ^new-object-predicates <new-preds>)
-->
   (<new-preds> ^<prop-h> <pred-h>)
}

