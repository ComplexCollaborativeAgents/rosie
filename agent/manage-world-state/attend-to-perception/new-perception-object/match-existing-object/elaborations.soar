# Elaborate the source object information from the superoperator
sp {match-existing-object*elaborate*source*info*from*superoperator
   (state <s> ^name match-existing-object
              ^superstate.operator <ssop>)
   (<ssop> ^perception-obj <perc-obj>)
-->
   (<s> ^perception-obj <perc-obj>)
}

sp {match-existing-object*elaborate*match-missing-object*superstate
   (state <s> ^name match-existing-object
              ^superstate.candidate-for-missing-object <obj>)
-->
   (<s> ^match-missing-object <obj>)
}

# Elaborate the object-monitor wme from the top-state
sp {match-existing-object*elaborate*object-monitor*from*top-state
   (state <s> ^name match-existing-object
              ^top-state.perception-monitor.object-monitor <omon>)
-->
   (<s> ^object-monitor <omon>)
}


### Elaborate the new-perception-obj predicates that it is confident about
# (Confidence is above 0.1 and there is no other predicate within 20% confidence)

sp {match-existing-object*elaborate*new-object-predicates
   (state <s> ^name match-existing-object)
-->
   (<s> ^new-object-predicates <preds>)
}

sp {match-existing-object*elaborate*predicate-info*input-link
   (state <s> ^name match-existing-object
              ^perception-obj.property <prop>)
   (<prop> ^property-handle <prop-handle>
           ^values <vals>)
-->
   (<s> ^predicate-info <pred-info>)
   (<pred-info> ^property-handle <prop-handle>
                ^values <vals>)
}

sp {match-existing-object*elaborate*predicate-info*hypothesis
   (state <s> ^name match-existing-object
              ^predicate-info <pred-info>)
   (<pred-info> ^values.<pred-handle> { <conf> > 0.1 })
-->
   (<pred-info> ^hypothesis <h>)
   (<h> ^predicate-handle <pred-handle>
        ^confidence <conf>)
}

sp {match-existing-object*elaborate*predicate-info*hypothesis*lower-threshold
   (state <s> ^name match-existing-object
              ^predicate-info.hypothesis <h>)
   (<h> ^confidence <conf>)
-->
   (<h> ^lower-threshold (* <conf> .8))
}

sp {match-existing-object*elaborate*new-object-predicates*predicate
   (state <s> ^name match-existing-object
              ^predicate-info <pred-info>
              ^new-object-predicates <preds>)
   (<pred-info> ^property-handle <prop-handle>
                ^hypothesis <best-h>)
   (<best-h> ^predicate-handle <pred-handle>
             ^lower-threshold <thresh>)
   -{(<pred-info> ^hypothesis { <other-h> <> <best-h> })
     (<other-h> ^confidence > <thresh>)}
-->
   (<preds> ^<prop-handle> <pred-handle>)
}
