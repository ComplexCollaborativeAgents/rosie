### matches-object-predicates
# match an existing object based on the predicates they share

### Case 1: Always try to match any missing-objects specified by superstate
# (just match on predicates, don't worry about overlapping)

sp {match-existing-object*propose*matches-object-predicates*missing-object
   (state <s> ^name match-existing-object
              ^match-missing-object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name matches-object-predicates
        ^object-info <obj>
        ^matching-counts <mc>)
}

### Case 2: Match any objects without belief objects

sp {match-existing-object*propose*matches-object-predicates*no*belief-obj
   (state <s> ^name match-existing-object
              ^perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> -^belief-obj <bel>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name matches-object-predicates
        ^object-info <obj>
        ^matching-counts <mc>)
}

### Elaborate matching predicates

sp {match-existing-object*elaborate*matches-object-predicates*matching-predicate
   (state <s> ^name match-existing-object
              ^operator <o> +
              ^new-object-predicates.<prop> <pred>)
   (<o> ^name matches-object-predicates
        ^object-info.wm-obj.predicates.<prop> <pred>)
-->
   (<o> ^matching-predicate <pred>)
}

# Count the number of matching predicates
# start with the lowest predicate as 1
sp {match-existing-object*elaborate*matches-object-predicates*matching-counts*initial
   (state <s> ^name match-existing-object
              ^operator <o> +)
   (<o> ^name matches-object-predicates
        ^matching-predicate <min-pred>
       -^matching-predicate < <min-pred>
        ^matching-counts <counts>)
-->
   (<counts> ^<min-pred> 1)
}

# add a +1 count for each 'next' predicate (no predicate between it and previous)
sp {matching-existing-object*elaborate*matching-object-predicates*matching-counts*next
   (state <s> ^name match-existing-object
              ^operator <o> +)
   (<o> ^name matches-object-predicates
        ^matching-predicate { <next-pred> > <prev-pred> }
       -^matching-predicate { > <prev-pred> < <next-pred> }
        ^matching-counts <counts>)
   (<counts> ^<prev-pred> <prev-count>)
-->
   (<counts> ^<next-pred> (+ <prev-count> 1))
}

# elaborate the max count as the number of matching predicates
sp {matching-existing-object*elaborate*matching-object-predicates*number-matching-predicates
   (state <s> ^name match-existing-object
              ^operator <o> +
              ^new-object-predicates.<prop> <pred>)
   (<o> ^name matches-object-predicates
        ^matching-counts <counts>)
   (<counts> ^<max-pred> <max-count>
            -^<better-pred> > <max-count>)
-->
   (<o> ^number-matching-predicates <max-count>)
}

### Elaborate conflicting predicates

sp {match-existing-object*elaborate*matches-object-predicates*conflicting-predicate
   (state <s> ^name match-existing-object
              ^operator <o> +
              ^new-object-predicates.<prop> <pred>)
   (<o> ^name matches-object-predicates
        ^object-info.wm-obj.predicates.<prop> { <> <pred> })
-->
   (<o> ^conflicting-predicate <pred>)
}


### Preferences

# Prefer matches with more matching predicates
sp {match-existing-object*prefer*matches-object-predicates*higher*matching-predicate*count
   (state <s> ^name match-existing-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name matches-object-predicates
         ^matching-predicate-count <c>)
   (<o2> ^name matches-object-predicates
         ^matching-predicate-count < <c>)
-->
   (<s> ^operator <o1> > <o2>)
}

# Reject if no matching predicates
sp {match-existing-object*reject*matches-object-predicates*no*matching-predicate
   (state <s> ^match-existing-object
              ^operator <o> +)
   (<o> ^name matches-object-predicates
        -^matching-predicate)
-->
   (<s> ^operator <o> -)
}

# Reject if any conflicting predicate
sp {match-existing-object*reject*matches-object-predicates*conflicting-predicate
   (state <s> ^match-existing-object
              ^operator <o> +)
   (<o> ^name matches-object-predicates
        ^conflicting-predicate)
-->
   (<s> ^operator <o> -)
}

### Application

# copy the match to the superstate
sp {match-existing-object*apply*matches-object-predicates*report*to*superstate
   (state <s> ^name match-existing-object
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name matches-object-predicates
        ^object-info <obj-info>)
-->
   (<ss> ^matches-existing-object <obj-info>)
}

# If print-perception is on, print a message about the match
sp {match-existing-object*apply*matches-object-predicates*print-perception
   (state <s> ^name match-existing-object
              ^operator <o>
              ^input-link-obj.object-handle <new-obj>
              ^agent-params.print-perception true)
   (<o> ^name matches-object-predicates
        ^object-info.object-handle <matching-obj>)
-->
   (write |match-existing-object: Object | <new-obj> | matches | <matching-obj> | via predicates|(crlf))
   (write (cmd print <o>) (crlf))
}
