##!
# @file
#
# @created plind 20170518
# File: simulate-comprehension.soar
# Author: Peter Lindes
# Created: 18 May 2017
#
# Desc: This is for debugging purposes only.
#       When the comprehend operator does nothing and gets
#       to a state no-change, this operator should
#       run and either produce a canned result for the
#       given sentence or a failure message.
#

#   Propose an operator to comprehend a segment of language
sp {comprehend-state-no-change*propose*simulate-comprehension
   (state <s> ^attribute state
              ^impasse no-change
              ^superstate <ss>)
   (<ss> ^name comprehend
         ^attribute operator
         ^impasse no-change
         ^superstate <sss>)
   (<sss> ^name dialog-event
          ^purpose <p>
          ^top-state.agent-params.language-mode lucia)
   (<p> ^type process-sentence
        ^parameters.sentence <sentence>)
   (<sentence> ^complete-sentence <text>)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-comprehension
        ^sentence-text <text>
        ^debug-info (concat |Pretend to comprehend '| <text> |'.|))
}

#   Apply rules for canned results
sp {comprehend-state-no-change*apply*simulate-comprehension*pick-up-the-green-sphere
   (state <s> ^attribute state
              ^impasse no-change
              ^superstate <ss>
              ^operator <o>)
   (<ss> ^name comprehend
         ^attribute operator
         ^impasse no-change
         ^superstate <sss>)
   (<o> ^name simulate-comprehension
        ^sentence-text |Pick up the green sphere.|)
-->
   #    Put a canned message on the comprehend state
   (<sss> ^message <m>)
   (<m> ^message-type command
        ^action <action>
        ^arg1 <arg1>)
   (<action> ^item-type action
             ^handle pick-up1)
   (<arg1> ^item-type object
           ^handle large-green-sphere1
           ^predicates <properties>)
   (<properties> ^category object
                 ^color green1
                 ^shape sphere1)
}

#   Apply rules for canned results
sp {comprehend-state-no-change*apply*simulate-comprehension*put-it-on-the-stove
   (state <s> ^attribute state
              ^impasse no-change
              ^superstate <ss>
              ^operator <o>)
   (<ss> ^name comprehend
         ^attribute operator
         ^impasse no-change
         ^superstate <sss>)
   (<o> ^name simulate-comprehension
        ^sentence-text |Put it on the stove.|)
-->
   #    Put a canned message on the comprehend state
   (<sss> ^message <m>)
   (<m> ^message-type command
        ^action <action>
        ^arg1 <arg1>)
   (<action> ^item-type action
             ^handle put-down1)
   (<arg1> ^item-type object
           ^handle large-green-sphere1
           ^predicates <properties>)
   (<properties> ^category object
                 ^color green1
                 ^shape sphere1)
}

