##!
# @file
#
# @created plindes 20150623
# File: interpret-done.soar
# Author: Peter Lindes
# Created: 23 June 2015
#
# Desc: This operator is proposed in the interpret substate,
#       but it is given a worst preference.
#       Thus it will not be selected until after all other
#       operators needed in the interpret state
#       have completed.
#

#    Propose an operator to finish up the interpret state
sp {interpret*propose*interpret-done
   (state <s> ^top-state <ts>
              ^problem-space interpret-for-rosie)
-->
   (<s> ^operator <o> + <)
   (<o> ^name interpret-done
        ^debug-info (concat |All decision making is done.| ))
}

#   This one fires when an action was decided on
#	It may fire several times
sp {interpret*apply*interpret-done*action-chosen
   (state <s> ^top-state <ts>
              ^problem-space interpret-for-rosie
              ^operator <o>
              ^message <message>)
   (<o> ^name interpret-done)
   (<ts> ^interaction.status <status>
         ^comprehension <c>)
#   (<ts> ^io.output-link <output>)
-->
   #    Put the message on the interaction status
   #    along with the event it should satisfy.
   (<status> ^performed <performed>
             ^new-message <message>)
   (<performed> ^dialog-event <dialog-event>)
   (<dialog-event> ^originator agent
                   ^type process-sentence)
   #    Delete the comprehension from the top state
   (<ts> ^comprehension <c> -)
   #   Report the decision to the outside world
#   (<output> ^report-decision <a>)
}

#   This one fires when NO action was interpreted
sp {interpret*apply*interpret-done*no-action
   (state <s> ^top-state <ts>
              ^superstate <ss>
              ^problem-space interpret-for-rosie
              ^operator <o>
             -^message <msg>)
   (<o> ^name interpret-done)
   (<ts> ^io.output-link <output>)
-->
   #   Post a NONE message to the superstate to complete interpret
   (<ss> ^message NONE)
   #   Report the interpretation to the outside world
#   (<output> ^report-decision NONE)
}

