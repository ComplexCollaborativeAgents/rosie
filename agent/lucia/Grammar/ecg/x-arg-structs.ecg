//  Various kinds of motion clauses
construction ActInDirectionForDistance
   subcase of Imperative
      constructional
         constituents
              action: ActInDirection
            distance: NumberOfUnits
   meaning: MoveDistance
      constraints
         self.m.action <--> action.m
         self.m.direction <--> action.m.direction
         self.m.distance <--> distance.m


//  Add adverbs to modify verbs
construction ModifiedDriveVerb
   subcase of DriveVerb
   constructional
      constituents
         verb: DriveVerb
         modifier: Adverb
   meaning
      constraints
         self.m <--> verb.m
         self.m.modifier <--> modifier.m

//  Situation clauses
general construction SituationClause
   subcase of Declarative
   meaning: SituationDescriptor

construction TransitiveSituationClause
   subcase of SituationClause
   constructional
      constituents
         subject: RefExpr
         verb: TransitiveSituationVerb
         object: RefExpr
   meaning
      constraints
         self.m.subject <--> subject.m
         self.m.action <--> verb.m
         self.m.object <--> object.m

// Conditional commands
construction UntilDeclarativeClause
   subcase of UntilClause
   constructional
      constituents
         until: UNTIL
         declarative: Declarative
   meaning: UntilDeclarative
      constraints
         self.m.assertion <--> declarative.m

construction ActAlongDirectionUntil
   constructional
      constituents
         action: ActAlongDirection
         until: UntilClause
   meaning: TerminatedAction
      constraints
         self.m.action <--> action.m
         self.m.terminator <--> until.m
