//	The following constructions were originally in the Common grammar
general construction Declarative
   meaning: Assertion

construction RefIsProperty
   subcase of Declarative
   constructional
      constituents
         ref : RefExpr
         tobe : FiniteToBe
         prop : Property
   meaning: PropertyApplication
      constraints
         self.m.property <--> prop.m
         self.m.target <--> ref.m

construction RefIsPrepPhrase
   subcase of Declarative
   constructional
      constituents
         ref : RefExpr
         tobe : FiniteToBe
         phrase : PrepPhrase
   meaning: PrepPhraseAssertion
      constraints
         self.m.preprel <--> phrase.m
         self.m.target <--> ref.m

//  Take the name of this construction with a grain of salt
construction ThisIsAThat
   subcase of Declarative
   constructional
      constituents
         this : DeicticPronoun
         tobe : FiniteToBe
         that : PropertySet
   meaning: ThisIsAThatAssertion
      constraints
         self.m.this <--> this.m
         self.m.that <--> that.m

construction ThisIsTheThat
   subcase of Declarative
   constructional
      constituents
         this : DeicticPronoun
         tobe : FiniteToBe
         that : RefExpr
   meaning: ThisIsTheThatAssertion
      constraints
         self.m.this <--> this.m
         self.m.that <--> that.m

construction PrepPhrase
   constructional
      constituents
         prep : Preposition
         object : RefExpr
   meaning: PrepRelation
      constraints
         self.m.prep <--> prep.m.name
         self.m.object <--> object.m

construction DirectionalPrepPhrase
   constructional
      constituents
         prep : DirectionalPreposition
         object : RefExpr
   meaning: PrepRelation
      constraints
         self.m.prep <--> prep.m.name
         self.m.object <--> object.m

general construction VerbWithArguments

construction ImperativeWithLocation
   subcase of VerbWithArguments
   constructional
      constituents
         command: Imperative
         location: PrepPhrase
   meaning: Action
      constraints
         self.m.action <--> command.m
         self.m.location <--> location.m
         self.m.object <--> command.object

general construction Imperative
   subcase of VerbWithArguments
   meaning#: Command

construction TransitiveCommandParticle
   subcase of Imperative
   constructional
      constituents
         command: TransitiveCommand
         part: Particle
   meaning: ActOnIt
      constraints
         self.m <--> command.m

construction TransitiveCommand
   subcase of Imperative
   constructional
      constituents
         verb: ActionVerb
         object: RefExpr
   meaning: ActOnIt
      constraints
         self.m.action <--> verb.m
         self.m.object <--> object.m

construction DoUntil
   constructional
      constituents
         verb: ActionVerb
         until: UntilClause
   meaning: TerminatedAction
      constraints
         self.m.action <--> verb.m
         self.m.terminator <--> until.m

construction MoveOnObjectUntil
   constructional
      constituents
         command: MotionOnObject
         until: UntilClause
   meaning: TerminatedActionOnObject
      constraints
         self.m.action <--> command.verb.m
         self.m.object <--> command.object.m
         self.m.terminator <--> until.m

// Conditional commands
general construction UntilClause

construction UntilThereIsClause
   subcase of UntilClause
   constructional
      constituents
         until: UNTIL
         thereis: THERE-IS
         object: RefExpr
   meaning: UntilThereIs
      constraints
         self.m.object <--> object.m

general construction Conditional

construction IfThenCommandCxn
   subcase of Conditional
   constructional
      constituents
         condition: ConditionClause
         then: ThenCommand
   meaning: IfThenCommand
      constraints
         self.m.condition <--> condition.m
         self.m.command <--> then.command.m

construction IfCommandCxn
   subcase of Conditional
   constructional
      constituents
         condition: ConditionClause
         command: Imperative
   meaning: IfThenCommand
      constraints
         self.m.condition <--> condition.m
         self.m.command <--> command.m

construction ConditionClause
   subcase of Conditional
   constructional
      constituents
         if: IF
         condition: Declarative
   meaning: IfCondition
      constraints
         self.m.condition <--> condition.m

construction ThenCommand
   subcase of Conditional
   constructional
      constituents
         then: THEN
         command: Imperative
   meaning: ThenResult
      constraints
         self.m.command <--> command.m

// Verb structures
general construction ActionVerb

general construction ActionVerbNeedsTarget
   subcase of ActionVerb
   //	This construction serves as a flag to help
   //	with PP attachment.
   //	An actual target slot does not work here,
   //	that must be added at a higher level.

// Questions
general construction Question

general construction WhQuestion
   subcase of Question

general construction YesNoQuestion
   subcase of Question

construction WhatIsObject
   subcase of WhQuestion
   constructional
      constituents
         what: WHAT
         tobe: FiniteToBe
         object: RefExpr
   meaning: WhatIsObjectQuestion
      constraints
         self.m.object <--> object.m

construction WhatIsPrepPhrase
   subcase of WhQuestion
   constructional
      constituents
         what: WHAT
         tobe: FiniteToBe
         phrase: PrepPhrase
   meaning: WhatIsRelation
      constraints
         self.m.predicate <--> phrase.m

construction WheresWaldo
   subcase of WhQuestion
   constructional
      constituents
         where: WHERE
         tobe: FiniteToBe
         object: RefExpr
   meaning: WhereIsObject
      constraints
         self.m.object <--> object.m

construction WhatClassIsObjectQ
   subcase of WhQuestion
   constructional
      constituents
         what: WHAT
         class: PropertySet
         tobe: FiniteToBe
         object: RefExpr
   meaning: WhatClassIsObjectQuestion
      constraints
         self.m.class <--> class.m
         self.m.object <--> object.m

construction IsObjectClassQ
   subcase of YesNoQuestion
   constructional
      constituents
         tobe: FiniteToBe
         object: RefExpr
         class: Property
   meaning: ObjectIsClassQuestion
      constraints
         self.m.object <--> object.m
         self.m.class <--> class.m

construction IsObjectPropSetQ
   subcase of YesNoQuestion
   constructional
      constituents
         tobe: FiniteToBe
         object: RefExpr
         class: PropertySet
   meaning: ObjectIsClassQuestion
      constraints
         self.m.object <--> object.m
         self.m.class <--> class.m

//  The following construction does not seem
//  to make any sense in English, and causes problems.
//  Could this be left over here from Spanish?
//construction ObjectIsClassQ
//   subcase of YesNoQuestion
//   constructional
//      constituents
//         object: RefExpr
//         tobe: FiniteToBe
//         class: PropertySet
//   meaning: ObjectIsClassQuestion
//      constraints
//         self.m.object <--> object.m
//         self.m.class <--> class.m

construction IsObjectRelation
   subcase of YesNoQuestion
   constructional
      constituents
         tobe: FiniteToBe
         object: RefExpr
         relation: PrepPhrase
   meaning: ObjectIsRelationQuestion
      constraints
         self.m.object <--> object.m
         self.m.relation <--> relation.m

//	Various kinds of motion clauses
construction DriveVerbToTarget
   subcase of Imperative
      constructional
         constituents
		      verb: DriveVerb
            target: PrepPhrase
   meaning: MoveToIt
      constraints
         self.m.action <--> verb.m
         self.m.location <--> target.m
	  
general construction DriveVerb
   subcase of SimpleMotionVerb
   meaning: ActionDescriptor
      constraints
         self.m.name <-- "go1"
 
construction DriveInDirection
   subcase of Imperative
      constructional
         constituents
              verb: DriveVerb
            direction: Direction
   meaning: Action
      constraints
         self.m.action <--> verb.m
         self.m.direction <--> direction.m
 
construction ActInDirection
   subcase of Imperative
      constructional
         constituents
              action: SimpleAction
            direction: Direction
   meaning: Action
      constraints
         self.m.action <--> action.verb.m
         self.m.direction <--> direction.m
 
construction ActAlongDirection
   subcase of Imperative
      constructional
         constituents
              action: SimpleAction
            direction: DirectionalPrepPhrase
   meaning: ActInDirection
      constraints
         self.m.action <--> action.verb.m
         self.m.direction <--> direction.m.prep
         self.m.object <--> direction.m.object

construction SimpleAction
   subcase of Imperative
      constructional
         constituents
		      verb: SimpleMotionVerb
   meaning: Action
      constraints
         self.m.action <--> verb.m
		
general construction SimpleMotionVerb
   subcase of ActionVerb
   meaning: ActionDescriptor
      constraints
         self.m.class <-- @action
 
construction TurnDirection
   subcase of Imperative
      constructional
         constituents
		      verb: TurnVerb
            direction: Direction
   meaning: TurnToIt
      constraints
         self.m.action <--> verb.m
         self.m.direction <--> direction.m
 
construction TurnToDirection
   subcase of Imperative
      constructional
         constituents
		      verb: TurnVerb
            to: TO
            direction: Direction
   meaning: TurnToIt
      constraints
         self.m.action <--> verb.m
         self.m.direction <--> direction.m
 
construction TurnToTheDirection
   subcase of Imperative
      constructional
         constituents
		      verb: TurnVerb
            to: TO
            the: THE
            direction: Direction
   meaning: TurnToIt
      constraints
         self.m.action <--> verb.m
         self.m.direction <--> direction.m
		
general construction MotionVerb
   meaning: ActionDescriptor
      constraints
         self.m.class <-- @action
	  
general construction TurnVerb
  subcase of MotionVerb
 
construction MotionOnObject
   subcase of Imperative
      constructional
         constituents
		      verb: MotionVerb
            object: RefExpr
   meaning: ActOnIt
      constraints
         self.m.action <--> verb.m
         self.m.object <--> object.m
