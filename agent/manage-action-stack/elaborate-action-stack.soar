# Elaborate bottom action onto set of actions
sp {top-state*elaborate*actions*bottom*action
   (state <s> ^superstate nil
              ^action-stack <stack>)
   (<stack> ^bottom { <action> <> none }
            ^actions <a>)
-->
   (<a> ^action <action>)
}

# Elaborate other actions onto the set
sp {top-state*elaborate*actions*next*action
   (state <s> ^superstate nil
              ^action-stack.actions <a>)
   (<a> ^action <action>)
   (<action> ^next { <next> <> none })
-->
   (<a> ^action <next>)
}

sp {top-state*elaborate*action-stack*task-objects
  (state <s> ^superstate nil
             ^action-stack <stack>)
-->
  (<stack> ^task-objects <objs>)
}

sp {top-state*elaborate*action-stack*task-object*mark*object
  (state <s> ^superstate nil
             ^action-stack.task-objects.object <obj>)
-->
  (<obj> ^task-object true)
}

sp {top-state*elaborate*action-stack*bottom*depth*1
   (state <s> ^superstate nil
              ^action-stack.bottom { <bottom> <> none })
-->
   (<bottom> ^depth 1)
}

sp {top-state*elaborate*action-stack*depth*increment
   (state <s> ^superstate nil
              ^action-stack.actions.action <a>)
   (<a> ^depth <d>
        ^next { <n> <> none })
-->
   (<n> ^depth (+ <d> 1))
}
