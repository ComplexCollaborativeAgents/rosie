# Elaborate bottom action onto set of actions
sp {top-state*elaborate*actions*bottom*action
   (state <s> ^superstate nil
              ^action-stack <stack>)
   (<stack> ^bottom { <action> <> none }
            ^actions <a>)
-->
   (<a> ^action <action>)
}

# Elaborate other actions onto the set
sp {top-state*elaborate*actions*next*action
   (state <s> ^superstate nil
              ^action-stack.actions <a>)
   (<a> ^action <action>)
   (<action> ^next { <next> <> none })
-->
   (<a> ^action <next>)
}

# Elaborate prev links between actions
sp {top-state*elaborate*action-stack*prev*links
   (state <s> ^superstate nil
              ^action-stack.actions.action <a>)
   (<a> ^next { <next> <> none })
-->
   (<next> ^prev <a>)
}

# Elaborate prev link on bottom action
sp {top-state*elaborate*action-stack*bottom*prev
   (state <s> ^superstate nil
              ^action-stack.bottom <bot>)
-->
   (<bot> ^prev none)
}

sp {top-state*elaborate*action-stack*task-objects
  (state <s> ^superstate nil
             ^action-stack <stack>)
-->
  (<stack> ^task-objects <objs>)
}

sp {top-state*elaborate*action-stack*task-object*mark*object
  (state <s> ^superstate nil
             ^action-stack.task-objects.object <obj>)
-->
  (<obj> ^task-object true)
}


