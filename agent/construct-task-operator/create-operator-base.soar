##### operator create-operator-base
# creates the basic structure for the operator as task-operator

# propose create-operator-base
sp {construct-task-operator*propose*create-operator-base
   (state <s> ^name construct-task-operator
              ^task-concept-network <tcn>
             -^task-operator.name)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-operator-base
        ^task-concept-network <tcn>)
} 

# create the task-operator strucutre with task-handle
sp {construct-task-operator*apply*create-operator-base
   (state <s> ^name construct-task-operator
              ^operator <o>)
   (<o> ^name create-operator-base
        ^task-concept-network <tcn>)
   (<tcn> ^handle <task-handle>)
-->
   (<s> ^task-operator <task-op>)
   (<task-op> ^item-type task-operator
              ^task-handle <task-handle>)
}

# @TODO: Do we need this? 
sp {construct-task-operator*apply*create-operator-base*create*subtask-handle*none
   (state <s> ^name construct-task-operator
              ^operator.name create-operator-base
              ^top-state.task-stack.top none
              ^task-operator <task-op>)
-->
   (<task-op> ^subtask-handle none)
}

# Copy the name of the operator
sp {construct-task-operator*apply*create-operator-base*copy*operator*name
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name create-operator-base
        ^task-concept-network.procedural.op_name <name>)
-->
   (<task-op> ^name <name>)
}

# @TODO: Change to perform-once true/false? 
# Copy the perform flag
sp {construct-task-operator*apply*create-operator-base*copy*perform
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name create-operator-base
        ^task-concept-network.perform <perf>)
-->
   (<task-op> ^perform <perf>)
}

sp {construct-task-operator*apply*create-operator-base*copy*perform*default
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name create-operator-base
        ^task-concept-network <tcn>)
   (<tcn> -^perform <perf>)
-->
   (<task-op> ^perform multiple)
}

# @DEL: remove learn-proposal flag
#sp {construct-task-operator*apply*create-operator-base*mark*learn-proposal
#   (state <s> ^name construct-task-operator
#              ^operator <o>
#              ^task-operator <task-op>)
#   (<o> ^name create-operator-base
#        ^task-concept-network.learn-proposal <bool>)
#-->
#  (<task-op> ^learn-proposal <bool>)
#}
