# If there is an argument on the task-concept-network marked required
#   And it doesn't exist on the command-message, 
# Then mark it as a ^missing-argument <arg-name> on the task-operator

sp {construct-task-operator*propose*mark-argument-missing*required
   (state <s> ^name construct-task-operator
              ^task-concept-network.procedural.<arg-name>.required true
             -^argument-info.arg-name <arg-name>
             ^task-operator <task-op>)
   (<task-op> -^missing-argument <arg-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-argument-missing
        ^arg-name <arg-name>)
}

sp {construct-task-operator*apply*mark-argument-missing*mark*missing
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name mark-argument-missing
        ^arg-name <arg-name>)
-->
   (<task-op> ^missing-argument <arg-name>)
}
