
# If the argument is not on the task-concept-network.procedural link, add it
sp {construct-task-argument*propose*add-argument-to-tcn
   (state <s> ^name construct-task-argument
              ^argument-info <info>
              ^task-concept-network.procedural <proc>)
   (<info> ^arg-name { <arg-name> <> until-clause })
   (<proc> -^arg-name <arg-name>)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-argument-to-tcn
        ^argument-info <info>)
}

# arg-type concept
sp {construct-task-argument*apply*add-argument-to-tcn*concept
   (state <s> ^name construct-task-argument
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type concept)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type concept
           ^id <new-conc-slot>)
   (<s> ^to-store <proc> <parg>)
}

# arg-type object
sp {construct-task-argument*apply*add-argument-to-tcn*object
   (state <s> ^name construct-task-argument
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type object)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type object
           ^id <new-obj-slot>)
   (<s> ^to-store <proc> <parg>)
}

# arg-type predicate
sp {construct-task-argument*apply*add-argument-to-tcn*predicate
   (state <s> ^name construct-task-argument
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type predicate)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type predicate
           ^id <new-pred-slot>
           ^2 <new-obj-slot>)
   (<s> ^to-store <proc> <parg>)
}


   

