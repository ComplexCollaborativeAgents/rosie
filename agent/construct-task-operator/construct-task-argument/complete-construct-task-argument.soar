# When everything else is finished, copy the constructed task-argument
#    to the task-operator on the superstate

sp {construct-task-argument*propose*complete-construct-task-argument
   (state <s> ^name construct-task-argument
              ^task-argument <arg>
              ^argument-info.arg-name <arg-name>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-construct-task-argument
        ^task-argument <arg>
        ^arg-name <arg-name>)
}

sp {construct-task-argument*apply*complete-construct-task-argument*copy*to*superstate*task-operator
   (state <s> ^name construct-task-argument
              ^operator <o>
              ^superstate.task-operator <task-op>)
   (<o> ^name complete-construct-task-argument
        ^task-argument <arg>
        ^arg-name <arg-name>)
-->
   (<task-op> ^<arg-name> <arg>)
}
