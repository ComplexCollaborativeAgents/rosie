# If any ^to-store <lti> wmes exist, store them in smem

# elaborate ^concepts-to-store true flag if any ^to-store <lti> exist
sp {construct-task-argument*elaborate*concepts-to-store
   (state <s> ^name construct-task-argument
              ^to-store <lti>)
-->
   (<s> ^concepts-to-store true)
}

# propose store-smem-concepts if ^concepts-to-store true is present
sp {construct-task-argument*propose*store-smem-concepts
   (state <s> ^name construct-task-argument
              ^concepts-to-store true)
-->
   (<s> ^operator <o> +)
   (<o> ^name store-smem-concepts)
}

# create store command on smem.command link and remove ^to-store wmes
sp {construct-task-argument*apply*store-smem-concepts*store
   (state <s> ^name construct-task-argument
              ^operator.name store-smem-concepts
              ^smem.command <cmd>
              ^to-store <lti>)
-->
   (<cmd> ^store <lti>)
   (<s> ^to-store <lti> -)
}

# if a retrieve command is there, remove it so the stores can work
sp {construct-task-argument*apply*store-smem-concepts*remove*retrieve
   (state <s> ^name construct-task-argument
              ^operator.name store-smem-concepts
              ^smem.command <cmd>)
   (<cmd> ^retrieve <ret>)
-->
   (<cmd> ^retrieve <ret> -)
}
              
