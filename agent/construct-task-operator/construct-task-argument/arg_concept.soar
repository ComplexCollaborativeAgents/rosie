############################### argument-info ##############################

## argument-info for concepts
sp {generate-action-operator*elaborate*argument-info*concept
   (state <s> ^name generate-action-operator
              ^command-message <msg>)
  (<msg> ^<arg-name> <arg>)
  (<arg> ^item-type concept
         ^handle <handle>)
-->
  (<s> ^argument-info <arg-info>)
  (<arg-info> ^arg-name <arg-name>
              ^arg-type concept
              ^handle <handle>)
}

### arg-status
# Information about the status of the argument

# in general - copy from procedural
sp {generate-action-operator*elaborate*argument-info*object*arg-status*from*procedural
   (state <s> ^name generate-action-operator
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type <any>
               ^procedural.arg-status <status>)
-->
   (<arg-info> ^arg-status <status>)
}

# concept arg-status: default = abstract
sp {generate-action-operator*elaborate*argument-info*concept*arg-status*default*real
   (state <s> ^name generate-action-operator
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type concept
              -^procedural.arg-status)
-->
   (<arg-info> ^arg-status abstract)
}



######################### create-procedural-argument ####################

# If there is an argument-info for an argument not on task-concept-network.procedural,
#   then create it and mark it to be stored

# propose when the argument name isn't on the procedural info
sp {generate-action-operator*propose*create-procedural-argument
   (state <s> ^name generate-action-operator
              ^argument-info <arg-info>
              ^task-concept-network.procedural <proc>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type << object concept predicate >>)
   (<proc> -^<arg-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-procedural-argument
        ^argument-info <arg-info>)
}

# arg-type concept
sp {generate-action-operator*apply*create-procedural-argument*concept
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name create-procedural-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type concept
               ^arg-status <status>)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type concept
           ^arg-status <status>
           ^importance unknown
           ^id <new-conc-slot>)
   (<arg-info> ^created-procedural-argument true)
   (<s> ^to-store <proc> <parg>)
}



######################### create-operator-argument ####################33

##### Creates the proper representation of each argument on to the action-operator

# propose if the argument isn't on the action-operator yet
sp {generate-action-operator*propose*create-operator-argument
   (state <s> ^name generate-action-operator
              ^action-operator <op>
              ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
              -^created <arg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
}

sp {generate-action-operator*apply*create-operator-argument*copy*concept*arg
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type concept
               ^arg-status <status>
               ^handle <concept-handle>)
-->
   (<op> ^<arg-name> <arg>)
   (<arg> ^arg-type concept
          ^arg-status <status>
          ^handle <concept-handle>)
   (<arg-info> ^created <arg>)
}
