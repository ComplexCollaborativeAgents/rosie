sp {construct-task-argument*propose*create-task-argument
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>
             -^task-argument <task-arg>)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
}

# arg-type = concept
sp {construct-task-argument*apply*create-task-argument*concept
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type concept
               ^handle <concept-handle>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type concept
          ^handle <concept-handle>)
}

# arg-type = object
sp {construct-task-argument*apply*create-task-argument*object
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type object
               ^create-obj.object-rep <obj-rep>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type object
          ^id <obj-rep>)
}

# arg-type = partial-predicate
sp {construct-task-argument*apply*create-task-argument*predicate
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type partial-predicate
               ^handle <pred-handle>
               ^create-obj2.object-rep <obj2>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^handle <pred-handle>
          ^2 <obj2>)
}

# arg-type = until-clause/after-clause

# state predicate
sp {construct-task-argument*apply*create-task-argument*clause*state*pred
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type <arg-type>
               ^predicate <pred>)
   (<pred> ^type state
           ^handle { <handle> <> visible1 }
           ^create-obj1.object-rep <obj1>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type <arg-type>
          ^predicate <new-pred>)
   (<new-pred> ^type state
               ^handle <handle>
               ^1 <obj1>)
}

# state predicate - special case for visible
sp {construct-task-argument*apply*create-task-argument*clause*state*pred*visible
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type <arg-type>
               ^predicate <pred>)
   (<pred> ^type state
           ^handle visible1
           ^create-obj1.object-rep <obj1>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type <arg-type>
          ^predicate <new-pred>)
   (<new-pred> ^type state
               ^handle visible1
               ^1 <obj1>)
}

# relation predicate
sp {construct-task-argument*apply*create-task-argument*clause*relation*pred
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type <arg-type>
               ^predicate <pred>)
   (<pred> ^type relation
           ^handle <handle>
           ^create-obj1.object-rep <obj1>
           ^create-obj2.object-rep <obj2>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type <arg-type>
          ^predicate <new-pred>)
   (<new-pred> ^type relation
               ^handle <handle>
               ^1 <obj1>
               ^2 <obj2>)
}

# duration 
sp {construct-task-argument*apply*create-task-argument*clause*duration
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type <arg-type>
               ^predicate <pred>)
   (<pred> ^type duration
           ^number <num>
           ^unit <unit>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type <arg-type>
          ^predicate <new-pred>)
   (<new-pred> ^type duration
               ^number <num>
               ^unit <unit>)
}

# clocktime
sp {construct-task-argument*apply*create-task-argument*clause*clocktime
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type <arg-type>
               ^predicate <pred>)
   (<pred> ^type clocktime
           ^hour <hour>
           ^minute <min>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type <arg-type>
          ^predicate <new-pred>)
   (<new-pred> ^type clocktime
               ^hour <hour>
               ^minute <min>)
}

# arg-type conditions
sp {construct-task-argument*apply*create-task-argument*conditions*state*predicate
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type conditions
               ^conditions.<num> <cpred>)
   (<cpred> ^type state
            ^handle <handle>
            ^create-obj1.object-rep <obj1>)
-->
   (<cpred> ^created-predicate <new-pred>)
   (<new-pred> ^type state
               ^handle <handle>
               ^1 <obj1>)
}

sp {construct-task-argument*apply*create-task-argument*conditions*relation*predicate
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type conditions
               ^conditions.<num> <cpred>)
   (<cpred> ^type relation
            ^handle <handle>
            ^create-obj1.object-rep <obj1>
            ^create-obj2.object-rep <obj2>)
-->
   (<cpred> ^created-predicate <new-pred>)
   (<new-pred> ^type relation
               ^handle <handle>
               ^1 <obj1>
               ^2 <obj2>)
}

sp {construct-task-argument*apply*create-task-argument*conditions*1*predicate
   (state <s> ^name construct-task-argument
              ^operator <o>)
   (<o> ^name create-task-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type conditions
               ^conditions <conds>)
   (<conds> ^condition-count 1
            ^{1}.created-predicate <cp1>)
-->
   (<s> ^task-argument <arg>)
   (<arg> ^arg-type conditions
          ^condition-count 1
          ^1 <cp1>)
}
