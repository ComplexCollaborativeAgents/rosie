######################### create-operator-argument ####################33
## argument-info for until-clause

sp {generate-action-operator*elaborate*argument-info*until-clause*predicate*visible
   (state <s> ^name generate-action-operator
              ^command-message.<arg-name>.until-clause <until>)
   (<until> ^action.handle sense1
            ^agent.handle rosie
            ^arg1 <obj>)
-->
   (<s> ^argument-info <arg-info>)
   (<arg-info> ^arg-name until-clause
               ^arg-type until-clause
               ^predicate <pred>)
   (<pred> ^type state
           ^handle visible1
           ^1 <obj>)
}

##### Creates the proper representation of each argument on to the action-operator

# propose if the argument isn't on the action-operator yet
sp {generate-action-operator*propose*create-operator-argument
   (state <s> ^name generate-action-operator
              ^action-operator <op>
              ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
              -^created <arg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
}



sp {generate-action-operator*apply*create-operator-argument*arg-type*until-clause*state*predicate
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause
               ^predicate <pred>)
   (<pred> ^type state
           ^handle { <handle> <> visible1 }
           ^1 <obj1>)
-->
   (<op> ^until-clause <new-clause>)
   (<new-clause> ^arg-type until-clause
                 ^predicate <new-pred>)
   (<new-pred> ^type state
               ^handle <handle>
               ^1 <obj1>)
   (<arg-info> ^created <new-clause>)
}
          

sp {generate-action-operator*apply*create-operator-argument*arg-type*until-clause*state*predicate*visible
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause
               ^predicate <pred>)
   (<pred> ^type state
           ^handle visible1
           ^1 <obj1>)
-->
   (<op> ^until-clause <new-clause>)
   (<new-clause> ^arg-type until-clause
                 ^predicate <new-pred>)
   (<new-pred> ^type state
               ^handle visible1
               ^1 <obj1>)
  (<arg-info> ^created <new-clause>)
}
