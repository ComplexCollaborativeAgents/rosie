sp {construct-task-argument*elaborate*task-concept-network*from*superstate
   (state <s> ^name construct-task-argument
              ^superstate.task-concept-network <tcn>)
-->
   (<s> ^task-concept-network <tcn>)
}

sp {construct-task-argument*elaborate*command-message*from*superstate
   (state <s> ^name construct-task-argument
              ^superstate.command-message <msg>)
-->
   (<s> ^command-message <msg>)
}

### argument-info

### arg-name and arg-type
sp {construct-task-argument*elaborate*argument-info*superoperator
   (state <s> ^name construct-task-argument
              ^superstate.operator <ss-op>)
   (<ss-op> ^arg-name <arg-name>
            ^arg-type <arg-type>)
-->
   (<s> ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type <arg-type>)
}

### other attributes from superoperator
sp {construct-task-argument*elaborate*argument-info*other*items*superoperator
   (state <s> ^name construct-task-argument
              ^superstate.operator <ss-op>
              ^argument-info <arg-info>)
   (<ss-op> ^{ <att> << handle predicate object 1 2 conditions >> } <val>)
-->
   (<arg-info> ^<att> <val>)
}

### tcn-info
sp {construct-task-argument*elaborate*argument-info*tcn-info
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>
              ^task-concept-network.tcn-info.<arg-name> <tcn-arg>)
   (<arg-info> ^arg-name <arg-name>)
-->
   (<arg-info> ^tcn-info <tcn-arg>)
}

########## create-object-rep

# arg-type object
sp {construct-task-argument*elaborate*create-object-rep*arg-type*object
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type object
               ^object <obj>)
-->
   (<s> ^create-object-rep <cor>)
   (<cor> ^source <obj>)
   (<arg-info> ^create-obj <cor>)
}

# arg-type partial-predicate
sp {construct-task-argument*elaborate*create-object-rep*arg-type*predicate*obj2
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type partial-predicate
               ^2 <obj2>)
-->
   (<s> ^create-object-rep <cor>)
   (<cor> ^source <obj2>)
   (<arg-info> ^create-obj2 <cor>)
}

# arg-type until-clause/after-clause obj1
sp {construct-task-argument*elaborate*create-object-rep*arg-type*clause*obj1
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type << until-clause after-clause >>
               ^predicate <pred>)
   (<pred> ^type << state relation >>
           ^1 <obj1>)
-->
   (<s> ^create-object-rep <cor>)
   (<cor> ^source <obj1>)
   (<pred> ^create-obj1 <cor>)
}

# arg-type until-clause/after-clause obj2
sp {construct-task-argument*elaborate*create-object-rep*arg-type*clause*obj2
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type << until-clause after-clause >>
               ^predicate <pred>)
   (<pred> ^type relation
           ^2 <obj2>)
-->
   (<s> ^create-object-rep <cor>)
   (<cor> ^source <obj2>)
   (<pred> ^create-obj2 <cor>)
}

# arg-type conditions
sp {construct-task-argument*elaborate*create-object-rep*arg-type*conditions*state*predicate
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type conditions
               ^conditions.<num> <cpred>)
   (<cpred> ^type state
            ^1 <obj1>)
-->
   (<s> ^create-object-rep <cor1>)
   (<cor1> ^source <obj1>)
   (<cpred> ^create-obj1 <cor1>)
}

sp {construct-task-argument*elaborate*create-object-rep*arg-type*conditions*relation*predicate
   (state <s> ^name construct-task-argument
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type conditions
               ^conditions.<num> <cpred>)
   (<cpred> ^type relation
            ^1 <obj1>
            ^2 <obj2>)
-->
   (<s> ^create-object-rep <cor1> <cor2>)
   (<cor1> ^source <obj1>)
   (<cor2> ^source <obj2>)
   (<cpred> ^create-obj1 <cor1> <cor2>)
}


# If it is not an smem object, it is already in the proper representation
sp {construct-task-argument*elaborate*create-object-rep*not*smem
   (state <s> ^name construct-task-argument
              ^create-object-rep <cor>)
   (<cor> ^source <src-obj>)
   (<src-obj> -^object-instance true)
-->
   (<cor> ^object-rep <src-obj>)
}

