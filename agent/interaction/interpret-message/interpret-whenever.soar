###################################################################
#
# interpret-command
#   A command given by the instructor to the agent to do something
#   Construction: *-VP-T
#     E.g. 'Store the red sphere', 'Put the green large block in the pantry'
#
###################################################################

#######
# OPERATOR PROPOSAL
#######

sp {interpret-message*propose*interpret-whenever
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              -^top-state.dialog-context
              -^new-segment-info
             -^task-operator <op>)
   (<msg> ^message-type whenever
          ^if-subclause <cond>
          ^then-subclause <command>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-whenever
        ^message <msg>
        ^conditions <cond>
        ^command <command>)
}

sp {interpret-message*apply*interpret-whenever
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-whenever
        ^message <msg>
        ^conditions <conds>
        ^command <command>)
   (<command> ^action.handle <task-handle>		
             -^agent-explanation)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type whenever-task
        ^parameters <params>
        ^satisfaction.learning-event.type whenever)
   (<params> ^command-message <command>
             ^conditions <conds>)
   (<msg> ^interpreted true)
}

sp {interpret-message*apply*interpert-whenever*mark*message*learning-action
   (state <s> ^name interpret-message
              ^operator <o>
              ^top-state.interaction.stack.segment.purpose.type << get-next-subaction get-next-goal >>)
   (<o> ^name interpret-whenever
        ^command <command>)
-->
   (<command> ^task-source instruction)
}

