######################################################
#
# Propose: init-agent
#
######################################################

sp {top-state*propose*init-agent
   (state <s> ^top-state <s>
             -^name rosie)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name init-agent)
}

#######################################################
# 
# Propose: interaction 
#
########################################################

sp {top-state*propose*interaction*status-change
   (state <s> ^top-state <s>
             -^agent-params.parser-test true
              ^interaction.status.<any> <status>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction
        ^type status-change
        ^status-info <status>
        ^status-type <any>)
}

sp {top-state*propose*interaction*interaction-change
   (state <s> ^top-state <s>
             -^agent-params.parser-test true
              ^interaction.changes.<type> <change>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction
        ^type interaction-change
        ^change-info <change>
        ^change-type <type>)
}

#######################################################
# 
# Propose: satisfy-purpose
#
########################################################

sp {top-state*propose*satisfy-purpose
   (state <s> ^top-state <s>
             #-^parser-testing true
		      ^interaction.stack.segment.purpose {<p> <> none})
   (<p> -^type << goal-description >>
	     -^category goal)
-->
   (<s> ^operator <o> +)
   (<o> ^name satisfy-purpose
        ^purpose <p>)
}

sp {top-state*reject*satisfy-purpose*if*executing*task
   (state <s> ^superstate nil
              ^operator <o> +)
   (<o> ^name satisfy-purpose
        ^purpose <p>)
   (<p> ^type execute-task
        ^parameters.task-operator <task>)
-->
   (<s> ^operator <o> -)
}

sp {top-state*reject*satisfy-purpose*if*executing*learned*game
   (state <s> ^superstate nil
              ^operator <o> +
              ^task-stack.queued-tasks.task <task>)
   (<o> ^name satisfy-purpose
        ^purpose.type << finished-game-learning >>)
-->
   (<s> ^operator <o> -)
}

#######################################################
# 
# Propose: push task segment
#
########################################################

sp {top-state*propose*push-task-segment*new-task-segment
   (state <s> ^superstate nil
              ^task-stack <stack>)
   (<stack> ^top none
            ^new-task-segment <seg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name push-task-segment
        ^segment <seg>)
}

sp {top-state*propose*push-task-segment*queued*task
   (state <s> ^superstate nil
              ^task-stack <stack>)
   (<stack> ^top none
            ^queued-tasks.task <qtask>)
   (<qtask> ^order <order>
            ^task-operator <task-op>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name push-task-segment
        ^order <order>
        ^segment <qtask>)
}

sp {top-state*prefer*push-task-segment*lower*order
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name push-task-segment
         ^order <ord1>)
   (<o2> ^name push-task-segment
         ^order > <ord1>)
-->
   (<s> ^operator <o1> > <o2>)
}

#######################################################
# 
# Propose: execute-task
#
########################################################

sp {top-state*propose*execute-task
   (state <s> ^superstate nil
              ^task-stack.bottom { <seg> <> none })
-->
   (<s> ^operator <o> +)
   (<o> ^name execute-task
        ^task-segment <seg>)
}

sp {top-state*propose*execute-task*finished-game-learning
   (state <s> ^superstate nil
              ^interaction.stack.segment.purpose.type finished-game-learning 
              ^task-stack.bottom { <seg> <> none })
-->
   (<s> ^operator <o> +)
   (<o> ^name execute-task
        ^task-segment <seg>)
}


sp {top-state*apply*execute-task*remove*queued-task
	(state <s> ^superstate nil
               ^operator <o>
               ^task-stack.queued-tasks <queue>)
 (<o> ^name execute-task
      ^task-segment.task-operator <task-op>)
 (<queue> ^task <qtask>)
 (<qtask> ^task-operator <task-op>)
-->
(<queue> ^task <qtask> -)        
}

#sp {top-state*propose*lookup-preposition
#   (state <s> ^prepositions.prep.name <name>
#              ^superstate nil
#              ^top-state.robot-state.arm.action wait
#             -^found-prep.type <name>)
#-->
#   (<s> ^operator <o> + =)
#   (<o> ^name lookup-preposition
#        ^type <name>)
#}
#
